
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ПлановаяОперация) Тогда
		ТекущийОбъект.Пропустить     = ТекущийОбъект.ПлановаяОперация.ПометкаУдаления;
		ТекущийОбъект.АктуальнаяДата = ТекущийОбъект.ПлановаяОперация.Дата;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПлановыеОперации.СформироватьОборотыШаблонаДокументов(ТекущийОбъект.ВладелецРасписания);
	Если НЕ ЗначениеЗаполнено(Запись.ПлановаяОперация) Тогда
		ИзменитьОграниченияТипаПлановойОперации();
		Модифицированность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.ПлановаяОперация) Тогда
		ТекущийОбъект.ПлановаяОперация = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПлановаяОперацияПриИзменении(Элемент)
	
	ПлановаяОперацияПриИзмененииСервер();
	
КонецПроцедуры
 
&НаКлиенте
Процедура ПлановаяОперацияОчистка(Элемент, СтандартнаяОбработка)
	
	ИзменитьДоступностьРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлановаяОперацияОткрытие(Элемент, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Запись.ПлановаяОперация) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицыФормы



#КонецОбласти


#Область ОбработчикиКомандФормы



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ТипШаблона = ТипЗнч(Запись.ВладелецРасписания);
	Элементы.ГруппаСуммыПогашенияДолга.Видимость = ТипШаблона = Тип("ДокументСсылка.МыВернулиДолг")
		ИЛИ ТипШаблона = Тип("ДокументСсылка.НамВернулиДолг");
		
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("ЭтоШаблон", Ложь));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Тип", ТипЗнч(Запись.ВладелецРасписания)));
	Элементы.ПлановаяОперация.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Если НЕ ЗначениеЗаполнено(Запись.ПлановаяОперация) Тогда
		ИзменитьОграниченияТипаПлановойОперации();
	КонецЕсли; 

	Если ЗначениеЗаполнено(Запись.ВладелецРасписания) Тогда
		
		Расписание = РегистрыСведений.Расписания.СоздатьМенеджерЗаписи();
		Расписание.ВладелецРасписания = Запись.ВладелецРасписания;
		Расписание.Прочитать();
		ЗаполнитьЗначенияСвойств(ПараметрыРасписания, Расписание);
		
	КонецЕсли;
	
	ИзменитьДоступностьРеквизитов(ЭтаФорма);
	
КонецПроцедуры

Процедура ИзменитьОграниченияТипаПлановойОперации()

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипЗнч(Запись.ВладелецРасписания));
	ТипВладельца = Новый ОписаниеТипов(МассивТипов);
	Запись.ПлановаяОперация = ТипВладельца.ПривестиЗначение(Запись.ПлановаяОперация);

КонецПроцедуры
 
&НаСервере
Процедура ПлановаяОперацияПриИзмененииСервер()

	Если ЗначениеЗаполнено(Запись.ПлановаяОперация) Тогда
		Запись.Пропустить     = Запись.ПлановаяОперация.ПометкаУдаления;
		Запись.Выполнено      = Запись.ПлановаяОперация.Проведен;
		Запись.АктуальнаяДата = Запись.ПлановаяОперация.Дата;
	Иначе
		Запись.Выполнено      = Ложь;
	КонецЕсли; 
	ПлановыеОперации.ОбновитьСуммыПлановойОперации(Запись, Запись.ПлановаяОперация, ПараметрыРасписания);
	ИзменитьДоступностьРеквизитов(ЭтаФорма);

КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста 
Процедура ИзменитьДоступностьРеквизитов(Форма)

	Элементы = Форма.Элементы;
	Элементы.Пропустить.Доступность = НЕ ЗначениеЗаполнено(Форма.Запись.ПлановаяОперация);
	Элементы.АктуальнаяДата.Доступность = Элементы.Пропустить.Доступность;

КонецПроцедуры

#КонецОбласти







