#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов") 
		И ДополнительныеСвойства.ОтключитьМеханизмРегистрацииОбъектов = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// При удаленнии любого докумета происходит запись пустого набора, что приводит к регистрации изменений в планах обмена.
	//Но регистрироваться должны только документы с признаком ЭтоШаблон
	ТипОбъекта = ТипЗнч(ЭтотОбъект.Отбор.ВладелецРасписания.Значение);
	ОтменитьРегистрацию = ТипОбъекта = Тип("ДокументСсылка.ВводИзменениеОстатка") 
		Или ТипОбъекта = Тип("ДокументСсылка.РегламентированныйОтчет")
		Или ТипОбъекта = Тип("ДокументСсылка.ВыгрузкаРегламентированныхОтчетов");
		
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Отбор.ВладелецРасписания.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОтменитьРегистрацию Тогда
		
		// Регистрировать нужно только документы, в которых реквизит ЭтоШаблон установлен в Истина
		МетаданныеОбъекта = ЭтотОбъект.Отбор.ВладелецРасписания.Значение.Метаданные();
		Если МетаданныеОбъекта.Реквизиты.Найти("ЭтоШаблон") = Неопределено Тогда
			ОтменитьРегистрацию = Истина;
		ИначеЕсли ОбщегоНазначения.СсылкаСуществует(ЭтотОбъект.Отбор.ВладелецРасписания.Значение) Тогда 
			ОтменитьРегистрацию = Не ЭтотОбъект.Отбор.ВладелецРасписания.Значение.ЭтоШаблон;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтменитьРегистрацию Тогда
		
		ЭтотОбъект.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
		ЭтотОбъект.ОбменДанными.Получатели.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры


#КонецЕсли