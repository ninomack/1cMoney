#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

// Возвращает массив объектов указанного типа, которым соответствует указанный в параметрах текст 
//
//Параметры:
//	Текст - Строка - Текст, в котором нужно найти ключевые слова
//	ТипСсылки - ОписаниеТипов - тип искомого объекта
//
// Возвращаемое значение: массив
Функция СписокОбъектовПоКлючевомуСлову(Знач Текст, Знач ТипСсылки, Родитель = Неопределено, Владелец = Неопределено) Экспорт

	Результат = Новый Массив;
	
	Запрос    = Новый Запрос;
	
	ТекстУсловияПоТипу          = "";
	ТекстЗапросаПоНаименованию  = "";
	СписокТипов = ТипСсылки.Типы();
	Счетчик = 0;
	Для каждого Тип Из СписокТипов Цикл
		
		Если ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
			
			Счетчик = Счетчик + 1;
			ИмяПараметра = "Идентификатор" + Формат(Счетчик, "ЧДЦ=; ЧГ=");
			Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип);
			Запрос.УстановитьПараметр(ИмяПараметра, Идентификатор);
			ТекстУсловияПоТипу = ТекстУсловияПоТипу + ?(ТекстУсловияПоТипу = "", "", Символы.ПС + Символы.Таб + "ИЛИ ") 
						+ "КлючевыеСловаОбъектов.ИдентификаторОбъекта = &" + ИмяПараметра;
			
			Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
				
				МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
				
				Если МетаданныеТипа <> Неопределено Тогда
					ТекстЗапросаПоНаименованию  = ТекстЗапросаПоНаименованию + ?(ТекстЗапросаПоНаименованию = "", "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) + 
					"ВЫБРАТЬ
					|	Спр.Ссылка КАК Объект,
					|	""" + МетаданныеТипа.ПолноеИмя() + """ КАК Источник
					|ИЗ
					|	" + МетаданныеТипа.ПолноеИмя() + " КАК Спр
					|ГДЕ
					|	&КлючевоеСлово = Спр.Наименование";
					
					Если Родитель <> Неопределено И ТипЗнч(Родитель) = Тип Тогда
						Запрос.УстановитьПараметр("Родитель", Родитель);
						ТекстЗапросаПоНаименованию = ТекстЗапросаПоНаименованию + Символы.ПС + "	И Спр.Родитель = &Родитель";
					КонецЕсли;
					Если Владелец <> Неопределено И Метаданные.Справочники.ЗначенияСубконтоАналитика.Владельцы.Содержит(Владелец.Метаданные()) Тогда
						Запрос.УстановитьПараметр("Владелец", Владелец);
						ТекстЗапросаПоНаименованию = ТекстЗапросаПоНаименованию + Символы.ПС + "	И Спр.Владелец = &Владелец";
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекстУсловияПоТипу = "" Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КлючевоеСлово", Текст);
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	КлючевыеСловаОбъектов.Объект КАК Объект,
	|	""РегистрСведений.КлючевыеСловаОбъектов"" КАК Источник
	|ИЗ
	|	РегистрСведений.КлючевыеСловаОбъектов КАК КлючевыеСловаОбъектов
	|ГДЕ
	|	КлючевыеСловаОбъектов.КлючевоеСлово = &КлючевоеСлово
	|	И (" + ТекстУсловияПоТипу + ")
	|
	|" + ?(ТекстЗапросаПоНаименованию = "", "", "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) + ТекстЗапросаПоНаименованию;
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Устанавливает соответствие между текстом (ключевым словом) и ссылкой на объект учета.
//Каждому тексту может соответствовать только один объект указанного типа
//
//Параметры:
//	Текст - ключевое слово, которое соответствует объекту
//	СсылкаНаОбъект - объект учета
//
Процедура ЗаписатьКлючевоеСловоОбъекта(Знач Текст, СсылкаНаОбъект) Экспорт
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(СсылкаНаОбъект));
	
	НаборЗаписей = РегистрыСведений.КлючевыеСловаОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КлючевоеСлово.Установить(Текст);
	НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(ИдентификаторОбъекта);
	
	ЗаписьРегистра = НаборЗаписей.Добавить();
	ЗаписьРегистра.КлючевоеСлово           = Текст;
	ЗаписьРегистра.ИдентификаторОбъекта    = ИдентификаторОбъекта;
	ЗаписьРегистра.Объект                  = СсылкаНаОбъект;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

// Заполняет регистр сведений предопределенными ключевыми словами
Процедура ОбновитьКлючевыеСловаОбъектовПоУмолчанию() Экспорт
	
	Валюта = Справочники.Валюты.НайтиПоКоду("643");
	Если ЗначениеЗаполнено(Валюта) Тогда
		ЗаписатьКлючевоеСловоОбъекта("RUR", Валюта);
		ЗаписатьКлючевоеСловоОбъекта("RUB", Валюта);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытий



#КонецОбласти

#КонецЕсли

