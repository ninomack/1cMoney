#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик удаления документа, проверяющий использование ссылки в ресурсах регистра
//
//Параметры:
//	Ссылка - документ, который нужно проверить
//	Отказ - Булево - будет установлен в Истина, если удаление невозможно
//
Процедура ПередУдалениемШаблонаОперации(Ссылка, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ШаблонОперации", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыГрафикаПогашенияДолгов.*
	|ИЗ
	|	РегистрСведений.ПараметрыГрафикаПогашенияДолгов КАК ПараметрыГрафикаПогашенияДолгов
	|ГДЕ
	|	ПараметрыГрафикаПогашенияДолгов.ШаблонОперации = &ШаблонОперации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаписьРегистра = РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка, , "ШаблонОперации");
		ЗаписьРегистра.Записать(Истина);
		
	КонецЦикла;
	
	
КонецПроцедуры

// Обработчик удаления справочника, проверяющий использование ссылки в ресурсах регистра
//
//Параметры:
//	Ссылка - документ, который нужно проверить
//	Отказ - Булево - будет установлен в Истина, если удаление невозможно
//
Процедура ПередУдалениемСправочника(Ссылка, Отказ) Экспорт
	
	ТипСсылки = ТипЗнч(Ссылка);
	Если Не Метаданные.РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.Ресурсы.КошелекДляПогашения.Тип.СодержитТип(ТипСсылки)
		И Не Метаданные.РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.Ресурсы.ФинансоваяЦель.Тип.СодержитТип(ТипСсылки)
		И Не Метаданные.РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.Ресурсы.СтатьяДляУчетаПроцентов.Тип.СодержитТип(ТипСсылки)
		И Не Метаданные.РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.Ресурсы.СтатьяЕдиноразовойКомиссии.Тип.СодержитТип(ТипСсылки)
		И Не Метаданные.РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.Ресурсы.СтатьяЕжемесячнойКомиссии.Тип.СодержитТип(ТипСсылки)
		Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаСправочника", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыГрафикаПогашенияДолгов.*
	|ИЗ
	|	РегистрСведений.ПараметрыГрафикаПогашенияДолгов КАК ПараметрыГрафикаПогашенияДолгов
	|ГДЕ
	|	ПараметрыГрафикаПогашенияДолгов.КошелекДляПогашения = &СсылкаСправочника
	|		ИЛИ ПараметрыГрафикаПогашенияДолгов.ФинансоваяЦель = &СсылкаСправочника
	|		ИЛИ ПараметрыГрафикаПогашенияДолгов.СтатьяДляУчетаПроцентов = &СсылкаСправочника
	|		ИЛИ ПараметрыГрафикаПогашенияДолгов.СтатьяЕдиноразовойКомиссии = &СсылкаСправочника
	|		ИЛИ ПараметрыГрафикаПогашенияДолгов.СтатьяЕжемесячнойКомиссии = &СсылкаСправочника
	|";
	
	ИменаРесурсов = Новый Структура("КошелекДляПогашения,ФинансоваяЦель,СтатьяДляУчетаПроцентов,СтатьяЕдиноразовойКомиссии,СтатьяЕжемесячнойКомиссии");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаписьРегистра = РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка);
		Для каждого КлючИЗначение Из ИменаРесурсов Цикл
			Если ЗаписьРегистра[КлючИЗначение.Ключ] = Ссылка Тогда
				ЗаписьРегистра[КлючИЗначение.Ключ] = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
		ЗаписьРегистра.Записать(Истина);
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли