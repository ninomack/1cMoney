#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьПараметрыТранспорта(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ТранспортыОбменаДанными.ОбновитьПараметрыСценария(ТекущийОбъект, РасписаниеРегламентногоЗадания, ИспользоватьРегламентноеЗадание);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура FILEКаталогОбменаИнформациейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайловогоКаталога(ЭтотОбъект, "FILEКаталогОбменаИнформацией", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура FILEКаталогОбменаИнформациейОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикОткрытияФайлаИлиКаталога(ЭтотОбъект, "FILEКаталогОбменаИнформацией", СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнятьОбменДаннымиАвтоматическиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры
  
&НаКлиенте
Процедура СжиматьФайлИсходящегоСообщенияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры



#КонецОбласти



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьРасписаниеРегламентногоЗадания(Команда)
	
	РедактированиеРасписанияРегламентногоЗадания();
	
	ОбновитьПредставлениеРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверитьПодключение(Команда)
	
	ТекстРезультата = "";
	Если ПроверитьКаталогСервер(ТекстРезультата) Тогда
		ТекстРезультата = НСтр("ru = 'Каталог готов к обмену данными'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли; 
	
	ПоказатьПредупреждение(, ТекстРезультата);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	// Подготовка реквизитов формы
	ПрочитатьПараметрыТранспорта();
	
	РасписаниеРегламентногоЗадания = Справочники.ТранспортыОбменаДанными.РасписаниеСценария(Объект.Ссылка, ИспользоватьРегламентноеЗадание);
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Заголовок = ПредставлениеРасписания;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ПредставлениеРасписания = Строка(Форма.РасписаниеРегламентногоЗадания);
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Заголовок = ПредставлениеРасписания;
	
	Элементы.ПарольАрхиваСообщенияОбмена.Доступность = Объект.СжиматьФайлИсходящегоСообщения;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыТранспорта()

	Для Каждого СтрокаПараметра Из Объект.ПараметрыТранспорта Цикл
		Если ЗначениеЗаполнено(СтрокаПараметра.ИмяПараметра) Тогда
			ЭтаФорма[СтрокаПараметра.ИмяПараметра] = ?(ЗначениеЗаполнено(СтрокаПараметра.ЗначениеДлиннойСтрокой), СтрокаПараметра.ЗначениеДлиннойСтрокой, СтрокаПараметра.ЗначениеПараметра);
		КонецЕсли; 
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ЗаписатьПараметрыТранспорта(ТекущийОбъект)

	ТекущийОбъект.ПараметрыТранспорта.Очистить();
	НоваяСтрокаПараметровТранспорта(ТекущийОбъект, "FILEКаталогОбменаИнформацией", FILEКаталогОбменаИнформацией);

КонецПроцедуры

&НаСервере
Функция НоваяСтрокаПараметровТранспорта(ТекущийОбъект, ИмяПараметра, ЗначениеПараметра)

	НоваяСтрока = ТекущийОбъект.ПараметрыТранспорта.Добавить();
	НоваяСтрока.ИмяПараметра      = ИмяПараметра;
	НоваяСтрока.ЗначениеПараметра = ЗначениеПараметра;
	Если СтрДлина(ЗначениеПараметра) > 255 Тогда
		НоваяСтрока.ЗначениеДлиннойСтрокой = Строка(ЗначениеПараметра);
	КонецЕсли; 
	
	Возврат НоваяСтрока;

КонецФункции

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗадания()
	
	// если расписание не инициализировано в форме на сервере, то создаем новое
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
		
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	
	// открываем диалог для редактирования Расписания
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеРасписанияРегламентногоЗаданияЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗаданияЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание <> Неопределено Тогда
		
		РасписаниеРегламентногоЗадания = Расписание;
		
		ОбновитьПредставлениеРасписания();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеРасписания()
	
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Заголовок = ПредставлениеРасписания;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКаталогСервер(ОписаниеПроблемы)

	ОписаниеПроблемы = "";
	Если Объект.ЗапретитьИспользование Тогда
		ОписаниеПроблемы = НСтр("ru = 'Выключено использование каталога на диске'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(FILEКаталогОбменаИнформацией) Тогда
		ОписаниеПроблемы = НСтр("ru = 'Не указан каталог на диске или в локальной сети'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	
	Если ОписаниеПроблемы <> "" Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	ОбъектОбработки = Обработки.ТранспортСообщенийОбменаFILE.Создать();
	ОбъектОбработки.FILEКаталогОбменаИнформацией = FILEКаталогОбменаИнформацией;
	ОбъектОбработки.ИспользоватьВременныйКаталогДляОтправкиИПриемаСообщений = Истина;
	ОбъектОбработки.Инициализация();
	
	Если НЕ ОбъектОбработки.ПодключениеУстановлено() Тогда
		ОписаниеПроблемы = ОбъектОбработки.СтрокаСообщенияОбОшибке;
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции



#КонецОбласти


