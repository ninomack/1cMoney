////////////////////////////////////////////////////////////////////////////////
//Справочник.ПрикрепленныеФайлы.ФормаЭлемента
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыВладельца = Метаданные.ОпределяемыеТипы.ВладелецПрикрепленногоФайла.Тип;
	Если ОписаниеФайла = Неопределено Тогда
		ОписаниеФайла = ПрикрепленныеФайлыКлиентСервер.НоваяСтруктураПрикрепляемогоФайла();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОписаниеФайла = ПрикрепленныеФайлыСервер.СтруктураОписанияФайлаИзСсылки(ТекущийОбъект.Ссылка);
	АктуализироватьНабор();
	
	ОбновитьПредставлениеДанных(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Ссылка.Пустая() Тогда
		ПрикрепленныеФайлыКлиент.ДобавитьФайлыВДиалоге(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(ОписаниеФайла.НавигационнаяСсылка) Тогда
		
		Отказ = Истина;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не загружены двоичные данные файпа'"), 
						Объект.Ссылка, "СсылкаНаПредставлениеДанных");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(ОписаниеФайла.НавигационнаяСсылка) Тогда
		ТекущийОбъект.ДанныеФайла         = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(ОписаниеФайла.НавигационнаяСсылка), ПрикрепленныеФайлыСервер.ПолучитьСжатиеДанных());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьНабор();
	ОписаниеФайла = ПрикрепленныеФайлыСервер.СтруктураОписанияФайлаИзСсылки(ТекущийОбъект.Ссылка);
	АктуализироватьНабор();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ПрикрепленныеФайлыКлиент.ЭтоПрикреплениеФайла(ЭтотОбъект, ИмяСобытия, Источник)
		И Параметр.Количество() > 0 Тогда
		
		ОписаниеФайла = Параметр[0].Значение;
		ОбновитьДвоичныеДанные();
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаНаПредставлениеДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ОписаниеФайла.НавигационнаяСсылка) Тогда
		ПрикрепленныеФайлыКлиент.ДобавитьФайлыВДиалоге(ЭтотОбъект);
	Иначе
		ВоспроизвестиМедиа();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиНабораЗаписейРегистраСведений



#КонецОбласти 


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСДиска(Команда)
	
	Если ЗначениеЗаполнено(ОписаниеФайла.НавигационнаяСсылка) Тогда
		
		#Область ПоказатьВопрос
		ТекстВопроса = НСтр("ru='Заменить загруженный в базу файл на новый?'");
		
		ДополнительныеПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьСДискаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Заменить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, Заголовок);
		#КонецОбласти
		
		Возврат;
		 
	КонецЕсли;
	 
	ПрикрепленныеФайлыКлиент.ДобавитьФайлыВДиалоге(ЭтотОбъект);
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ВоспроизвестиМедиа();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзменения(Команда)
	
	Если ПрикрепленныеФайлыКлиент.ОбновитьСодержимоеФайла(ОписаниеФайла, ЭтотОбъект) Тогда
		ОбновитьДвоичныеДанные();
	КонецЕсли;
	
КонецПроцедуры
 


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ОбновитьПредставлениеДанных(Форма)

	Форма.СсылкаНаПредставлениеДанных = ПрикрепленныеФайлыКлиентСервер.ПредставлениеФайлаНаФорме(Форма.Объект.СпособОткрытияФайла,
									Форма.ОписаниеФайла.НавигационнаяСсылка);

КонецПроцедуры

&НаКлиенте
Процедура ВоспроизвестиМедиа()

	Если ОписаниеФайла <> Неопределено И ЗначениеЗаполнено(ОписаниеФайла.НавигационнаяСсылка) Тогда
		ОбновитьОписаниеФайла(ЭтотОбъект);
		ПрикрепленныеФайлыКлиент.ВоспроизвестиФайл(ОписаниеФайла);
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Если Форма.ОписаниеФайла = Неопределено Или Не ЗначениеЗаполнено(Форма.ОписаниеФайла.ИмяФайла)  Тогда
		Форма.Элементы.ОткрытьФайл.Доступность       = Истина;
		Форма.Элементы.ПолучитьИзменения.Доступность = Ложь;
	Иначе
		Форма.Элементы.ОткрытьФайл.Доступность       = Ложь;
		Форма.Элементы.ПолучитьИзменения.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура АктуализироватьНабор()

	НаборВладельцев.Очистить();
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей();
	Набор.Отбор.ПрикрепленныйФайл.Установить(Объект.Ссылка);
	Набор.Прочитать();
	
	НаборВладельцев.Загрузить(Набор.Выгрузить());

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНабор()

	Набор = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей();
	Набор.Отбор.ПрикрепленныйФайл.Установить(Объект.Ссылка);
	
	ТаблицаВладельцев = НаборВладельцев.Выгрузить();
	ТаблицаВладельцев.Свернуть("ВладелецФайла");
	
	Для каждого Строка Из ТаблицаВладельцев Цикл
		
		Если ЗначениеЗаполнено(Строка.ВладелецФайла) Тогда
			Запись = Набор.Добавить();
			Запись.ВладелецФайла = Строка.ВладелецФайла;
			Запись.ПрикрепленныйФайл = Объект.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Набор.Записать(Истина);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОписаниеФайла(Форма)

	Форма.ОписаниеФайла.Наименование       = Форма.Объект.Наименование;
	Форма.ОписаниеФайла.Комментарий        = Форма.Объект.Комментарий;
	Форма.ОписаниеФайла.НеСинхронизировать = Форма.Объект.НеСинхронизировать;
	Форма.ОписаниеФайла.СпособОткрытия     = Форма.Объект.СпособОткрытияФайла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДвоичныеДанные()

	Если ОписаниеФайла <> Неопределено И ЭтоАдресВременногоХранилища(ОписаниеФайла.НавигационнаяСсылка) Тогда
		
		СпрачочникОбъект = РеквизитФормыВЗначение("Объект");
		СпрачочникОбъект.ДанныеФайла         = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(ОписаниеФайла.НавигационнаяСсылка), ПрикрепленныеФайлыСервер.ПолучитьСжатиеДанных());
		СпрачочникОбъект.Наименование        = ОписаниеФайла.Наименование;
		СпрачочникОбъект.Комментарий         = ОписаниеФайла.Комментарий;
		СпрачочникОбъект.РасширениеФайла     = ОписаниеФайла.Расширение;
		СпрачочникОбъект.РазмерФайла         = ОписаниеФайла.Размер;
		СпрачочникОбъект.ДатаСоздания        = ОписаниеФайла.ДатаСоздания;
		СпрачочникОбъект.СпособОткрытияФайла = ОписаниеФайла.СпособОткрытия;
		ЗначениеВРеквизитФормы(СпрачочникОбъект, "Объект");
		
		Модифицированность = Истина;
		
		ОбновитьПредставлениеДанных(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСДискаЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПрикрепленныеФайлыКлиент.ДобавитьФайлыВДиалоге(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры


#КонецОбласти