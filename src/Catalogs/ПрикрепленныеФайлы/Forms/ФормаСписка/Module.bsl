
&НаКлиенте 
Перем АктивнаяСтрока;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыВладельца = Метаданные.ОпределяемыеТипы.ВладелецПрикрепленногоФайла.Тип;
	
КонецПроцедуры


#КонецОбласти

#Область СобытияСписка

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	Если АктивнаяСтрока <> Элементы.Список.ТекущаяСтрока Тогда
		АктивнаяСтрока = Элементы.Список.ТекущаяСтрока;
		СписокПриАктивизацииСтрокиСервер(Элементы.Список.ТекущиеДанные.Ссылка, Элементы.Список.ТекущиеДанные.СпособОткрытияФайла);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеФайлаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФайлСсылка = Элементы.Список.ТекущаяСтрока;
	Если ФайлСсылка <> Неопределено Тогда
		Элементы.Список.ИзменитьСтроку();
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти 


#Область СобытияТаблицыФормы_ВладельцыФайла
	
&НаКлиенте
Процедура ВладельцыФайлаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Не ЗначениеЗаполнено(АктивнаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("СсылкаНаФайл", АктивнаяСтрока);
	Оповещение = Новый ОписаниеОповещения("ВладельцыФайлаПередНачаломДобавленияЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
	ПоказатьВводЗначения(Оповещение, ТипыВладельца.ПривестиЗначение(Неопределено), НСтр("ru='Выберите владельца файла'"), ТипыВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВладельцыФайлаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ДанныеСтроки = Элементы.ВладельцыФайла.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ДополнительныеПараметры = Новый Структура();
		Оповещение = Новый ОписаниеОповещения("ВладельцыФайлаПередНачаломИзмененияЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
		ПоказатьЗначение(Оповещение, ДанныеСтроки.ВладелецФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура УстановитьОтборВладельцевПоФайлу(ЭлементФайла)

	ОтборСписка = ВладельцыФайла.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ВладельцыФайла.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки);
	Если ОтборСписка <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "ПрикрепленныйФайл", 
							ЭлементФайла, ВидСравненияКомпоновкиДанных.Равно, , Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеФайла(Ссылка, СпособОткрытияФайла)

	АдресДанных = ПолучитьНавигационнуюСсылку(Ссылка, "ДанныеФайла");
	ПредставлениеФайла = ПрикрепленныеФайлыКлиентСервер.ПредставлениеФайлаНаФорме(СпособОткрытияФайла, АдресДанных);

КонецПроцедуры

&НаСервере
Процедура СписокПриАктивизацииСтрокиСервер(Ссылка, СпособОткрытияФайла)

	Если Ссылка <> Неопределено Тогда
		УстановитьОтборВладельцевПоФайлу(Ссылка);
		ОбновитьПредставлениеФайла(Ссылка, СпособОткрытияФайла);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВладельцыФайлаПередНачаломДобавленияЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДобавитьВладельцаФайла(Ответ, ДополнительныеПараметры.СсылкаНаФайл);
	Элементы.ВладельцыФайла.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьВладельцаФайла(Владелец, СсылкаНаФайл)

	ЗаписьРегистра = РегистрыСведений.ПринадлежностьФайлов.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.ВладелецФайла     = Владелец;
	ЗаписьРегистра.ПрикрепленныйФайл = СсылкаНаФайл;
	ЗаписьРегистра.Записать(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ВладельцыФайлаПередНачаломИзмененияЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Элементы.ВладельцыФайла.Обновить();

КонецПроцедуры
 

#КонецОбласти