#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

// Возвращает структуру, заполненную свойствами, важними для изменения варианта бюджета
//
//Возвращаемое значение:
//	Структура
//		* ПределПланирования - Дата - значение одноименной константы
//		* ЕстьПлановыеПоказателиБюджета - Булево
//		* МинимальнаяДатаПоказателя - Дата - наименьшая дата, на которую имеется сумма в бюджете
//
Функция СвойстваВариантаБюджета(СсылкаВариантаБюджета) Экспорт

	Результат = Новый Структура("ПределПланирования,ЕстьПлановыеПоказателиБюджета,МинимальнаяДатаПоказателя", 
			Константы.ПределПланирования.Получить(), Ложь, Дата(1,1,1));
			
	Если Не ЗначениеЗаполнено(СсылкаВариантаБюджета) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВариантБюджета", СсылкаВариантаБюджета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ПоказателиБюджета.НачалоПериода) КАК НачалоПериода
	|ИЗ
	|	РегистрСведений.ПоказателиБюджета КАК ПоказателиБюджета
	|ГДЕ
	|	ПоказателиБюджета.ВариантБюджета = &ВариантБюджета
	|	И (ПоказателиБюджета.Сумма <> 0
	|			ИЛИ ПоказателиБюджета.Комментарий <> """")";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.МинимальнаяДатаПоказателя = Выборка.НачалоПериода;
		Результат.ЕстьПлановыеПоказателиБюджета = ЗначениеЗаполнено(Результат.МинимальнаяДатаПоказателя);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	// Проверяем/устанавливаем стандартные параметры отбора
	Если Не Параметры.Свойство("Отбор") Тогда
		Параметры.Вставить("Отбор", Новый Структура);
	КонецЕсли;
	
	Если Не Параметры.Отбор.Свойство("ПометкаУдаления") Тогда
		Параметры.Отбор.Вставить("ПометкаУдаления", Ложь);
	КонецЕсли;
	
	// Условие отбора на исключение конкретного варианта бюджета
	Если Параметры.Отбор.Свойство("ИсключаемаяСсылка") Тогда
		
		СтандартнаяОбработка = Ложь;
		ИсключатьКрупнуюДетальность = Параметры.Отбор.Свойство("ИсключатьКрупнуюДетальность")
									И Параметры.Отбор.ИсключатьКрупнуюДетальность;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИсключаемаяСсылка", Параметры.Отбор.ИсключаемаяСсылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ВариантыБюджетов.Ссылка = &ИсключаемаяСсылка
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Порядок,
		|	ВариантыБюджетов.Ссылка,
		|	ВариантыБюджетов.Представление,
		|	ВЫБОР
		|		КОГДА ВариантыБюджетов.ДетальностьПланирования = ЗНАЧЕНИЕ(Перечисление.ПериодичностьПланирования.Неделя)
		|			ТОГДА 1
		|		КОГДА ВариантыБюджетов.ДетальностьПланирования = ЗНАЧЕНИЕ(Перечисление.ПериодичностьПланирования.Месяц)
		|			ТОГДА 2
		|		КОГДА ВариантыБюджетов.ДетальностьПланирования = ЗНАЧЕНИЕ(Перечисление.ПериодичностьПланирования.Квартал)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ КАК ИндексДетальности
		|ИЗ
		|	Справочник.ВариантыБюджетов КАК ВариантыБюджетов
		|ГДЕ
		|	НЕ ВариантыБюджетов.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	ВариантыБюджетов.Представление";
		
		ДанныеВыбора = Новый СписокЗначений;
		Выборка = Запрос.Выполнить().Выбрать();
		МинимальныйИндексДетальности = 0;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Ссылка = Параметры.Отбор.ИсключаемаяСсылка Тогда
				МинимальныйИндексДетальности = Выборка.ИндексДетальности;
				Продолжить;
			КонецЕсли;
			
			Если ИсключатьКрупнуюДетальность И Выборка.ИндексДетальности > МинимальныйИндексДетальности Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
			
		КонецЦикла;
		 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
