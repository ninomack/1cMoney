////////////////////////////////////////////////////////////////////////////////
//Справочник.ВариантыБюджетов.Форма.ФормаСписка
//  
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверяем режим открытия формы списка
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора      = Истина;
		Элементы.Список.ОтображатьКорень = Истина;
		Элементы.Список.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВерхнийУровень;
		ЭтотОбъект.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	
	ОбновитьПараметрыБюджетирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменены параметры бюджетрования" Тогда
		ОбновитьПараметрыБюджетирования();
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Редактировать(Команда)
	
	Вариант = Элементы.Список.ТекущаяСтрока;
	Если НЕ ЗначениеЗаполнено(Вариант) Тогда
		Возврат;
	КонецЕсли; 
	
	ПараметрыРедкатора = Новый Структура;
	ПараметрыРедкатора.Вставить("ВариантБюджета", Вариант);
	ОткрытьФорму("Обработка.РазделБюджет.Форма", ПараметрыРедкатора, ЭтаФорма, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьОсновнымВариантом(Команда)
	
	ИзменитьОсновнойВариантСервер(Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПределПланирования(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПараметрыБюджетирования");
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзменитьОсновнойВариантСервер(Вариант)

	Если ТипЗнч(Вариант) = Тип("СправочникСсылка.ВариантыБюджетов") И ЗначениеЗаполнено(Вариант) Тогда
		Константы.ОсновнойВариантБюджета.Установить(Вариант);
		ОсновнойВариант = Вариант;
		ОбновитьПараметрыБюджетирования();
	КонецЕсли; 

КонецПроцедуры
 
&НаСервере
Процедура ОбновитьПараметрыБюджетирования()

	ОсновнойВариант = Константы.ОсновнойВариантБюджета.Получить();
	Если НЕ ЗначениеЗаполнено(ОсновнойВариант) Тогда
		ОсновнойВариант = Справочники.ВариантыБюджетов.БазовыйВариант;
	КонецЕсли; 
	ПределПланирования = Константы.ПределПланирования.Получить();
	ПредставлениеПределаПланирования = НСтр("ru = 'Все варианты рассчитаны по %1'"); 
	ПредставлениеПределаПланирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеПределаПланирования, Формат(ПределПланирования, "ДФ=дд.ММ.гггг") );
	ПредставлениеПределаПланирования = ПредставлениеПределаПланирования + Символы.ПС + НСтр("ru = 'Основной вариант бюджета: '") + ОсновнойВариант; 
	

КонецПроцедуры
 


#КонецОбласти







