////////////////////////////////////////////////////////////////////////////////
// ОбслуживаниеДокументов: 
//	* Обработка событий объектов документов
//	* Общий функционал, используемый в модулях объектов и менеджеров документов
//	* Функции подготовки движений документов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
//	* обработка событий объектов документов

// Обработка подписки на событие ЗаписьОбъектовОпераций
Процедура ЗаписьОбъектовОпераций(Источник, Отказ) Экспорт
	
	Если Отказ Или Источник.ОбменДанными.Загрузка 
		Или Не Источник.ДополнительныеСвойства.Свойство("ТаблицыДокумента")
		Или Не Источник.ДополнительныеСвойства.ТаблицыДокумента.Свойство("ОбъектыОперации") Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ОбъектыОпераций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Операция.Установить(Источник.Ссылка);
	НаборЗаписей.Загрузить(Источник.ДополнительныеСвойства.ТаблицыДокумента.ОбъектыОперации);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

// Обработка подписки на событие ПередУдалениемДокумента
Процедура ПередУдалениемДокумента(Источник, Отказ) Экспорт

	Если Не Отказ Тогда
		ПлановыеОперации.ПередУдалениемПлановойОперации(Источник, Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.ПередУдалениемШаблонаОперации(Источник.Ссылка, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Обработка подписки на событие ЗаписьШаблоновИПлановыхОперацийПриЗаписи
Процедура ЗаписьШаблоновИПлановыхОперацийПриЗаписи(Источник, Отказ) Экспорт
	
	
	Если Отказ Или Источник.ОбменДанными.Загрузка 
		Или Не Источник.ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		Возврат;
	КонецЕсли;
	
	
	Если Источник.ЭтоШаблон Тогда
		
		// формируем плановые движения по шаблону операций
		ПлановыеОперации.СформироватьОборотыШаблонаДокументов(Источник.Ссылка, Источник.ДополнительныеСвойства);
		
	ИначеЕсли Источник.ДополнительныеСвойства.Свойство("ЭтоПлановаяОперация") И Источник.ДополнительныеСвойства.ЭтоПлановаяОперация 
			И Источник.ДополнительныеСвойства.Свойство("ШаблонОперации") И ЗначениеЗаполнено(Источник.ДополнительныеСвойства.ШаблонОперации)
			Тогда
			
		// Непроведенная плановая операция является исключением (содержит суммы, отличные от сумм шаблона)
		// для учета ее сумм нужно переформировать обороты по шаблону
		Если Источник.Проведен Тогда 
			
			Если Источник.ДополнительныеСвойства.ШаблонОперации = Источник.Ссылка Тогда
				// Операция запланирована без шаблона. Считываем плановые обороты, записанные до проведения
				Источник.Движения.ОборотыПлановыхОпераций.Прочитать();
				Источник.ДополнительныеСвойства.Вставить("ПлановыеДвиженияОперации", Источник.Движения.ОборотыПлановыхОпераций.Выгрузить());
			КонецЕсли;
			
		Иначе
			
			// формируем новые движения по плановой операции
			ПлановыеОперации.СформироватьОборотыШаблонаДокументов(Источник.ДополнительныеСвойства.ШаблонОперации,  Неопределено);
			
		КонецЕсли;
		
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
//	* Общий функционал, используемый в модулях объектов и менеджеров документов

// Возвращает дату с учетом настроек пользователя: либо текущую дату/время, либо дату последней записанной операции
//
//Параметры:
//	<нет>
//
Функция ДатаНовогоДокумента() Экспорт

	Результат = ТекущаяДатаСеанса();
	
	ТипДатыНовойОперации   = ПользовательскиеНастройкиДеньгиСервер.ТипДатыНовойОперации();
	Если ТипДатыНовойОперации = "ИзПоследнейОперации" И ЗначениеЗаполнено(ПараметрыСеанса.ДатаПоследнейОперации) Тогда
		
		Результат = НачалоДня(ПараметрыСеанса.ДатаПоследнейОперации) + (Результат - НачалоДня(Результат))
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Проверяет основные реквизиты документа, при необходимости заполняет их в соответствии с настройками пользователя
// Вызывается из обработчика ОбработкаЗаполнения модулей объекта документов
//
//Параметры
//	ДокументОбъект - заполняемый документ
//	ЭтоКопия - Булево - признак того, что документ заполнен копированием
//
Процедура ПроверитьЗаполнениеНовогоДокумента(ДокументОбъект, ЭтоКопия = Ложь) Экспорт
	
	Если (ЭтоКопия Или Не ЗначениеЗаполнено(ДокументОбъект.Дата)) И Не ДокументОбъект.ЭтоШаблон Тогда
		ДокументОбъект.Дата = ОбслуживаниеДокументов.ДатаНовогоДокумента();
	КонецЕсли;
	
	Если ЭтоКопия Или Не ЗначениеЗаполнено(ДокументОбъект.Пользователь) Тогда
		ДокументОбъект.Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
//	* Функции подготовки движений документов

Процедура ОбщаяПроверкаДополнительныхСвойствДокумента(Операция, ДополнительныеСвойства) Экспорт
	
	Если ТипЗнч(ДополнительныеСвойства) <> Тип("Структура") Тогда
		ДополнительныеСвойства = Новый Структура;
	КонецЕсли; 
	
	Если НЕ ДополнительныеСвойства.Свойство("ВалютаУчета") ИЛИ НЕ ЗначениеЗаполнено(ДополнительныеСвойства.ВалютаУчета) Тогда
		ДополнительныеСвойства.Вставить("ВалютаУчета", Константы.ВалютаУчета.Получить())
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("Период") ИЛИ НЕ ЗначениеЗаполнено(ДополнительныеСвойства.Период) Тогда
		ДополнительныеСвойства.Вставить("Период", Операция.Дата)
	КонецЕсли;

	Если Не ДополнительныеСвойства.Свойство("ЭтоПлановаяОперация") И Не Операция.ЭтоШаблон Тогда
		
		// Проверяем, является ли операция шаблоном или плановой операцией
		СвойстваПлановойОперации = ПлановыеОперации.СвойстваПлановойОперации(Операция.Ссылка);
		Для каждого КлючИЗначение Из СвойстваПлановойОперации Цикл
			ДополнительныеСвойства.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеВосстанавливатьПоследовательность") И Не ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Операция)) Тогда
		
		Если Операция.ПринадлежностьПоследовательностям.Найти("ВводИзменениеОстатков") <> Неопределено Тогда
			Операция.ПринадлежностьПоследовательностям.ВводИзменениеОстатков.ДополнительныеСвойства.Вставить(
					"НеВосстанавливатьПоследовательность", ДополнительныеСвойства.НеВосстанавливатьПоследовательность)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция НоваяТаблицаРегистровОборотовБюджета() Экспорт

	Результат = РегистрыНакопления.ФактическиеОборотыБюджета.СоздатьНаборЗаписей();
	Возврат Результат.ВыгрузитьКолонки();

КонецФункции

Функция НоваяТаблицаРегистраОбъектыОпераций() Экспорт

	Результат = РегистрыСведений.ОбъектыОпераций.СоздатьНаборЗаписей();
	Возврат Результат.ВыгрузитьКолонки();

КонецФункции

// Предварительная обработка документа при записи
//
//Параметры:
//	ДокументОбъект - записываемый документ
//
Процедура ПриЗаписиОбъектаДокумента(ДокументОбъект) Экспорт
	
	КлючевыеСвойства = СтруктураКлючевыхДополнительныхСвойствОперации(ДокументОбъект, 
			"ОтложеннаяЗапись,ОтложенноеПроведение,ОтключитьМеханизмРегистрацииОбъектов,ПропуститьПроверкуЗапретаИзменения,СведенияОВерсииОбъекта,
			|НеПроверятьСостояние,НеОбновлятьПорядокВПоказателях,НеВосстанавливатьПоследовательность,УдалятьРегистрацию,УзелКорреспондента");
	
	// Копирование ключевых дополнительных свойтв из Документа в его движения
	Для каждого Набор Из ДокументОбъект.Движения Цикл
		
		Для каждого КлючИЗначение Из КлючевыеСвойства Цикл
			Набор.ДополнительныеСвойства.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
		Если ДокументОбъект.ОбменДанными.Загрузка И ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.ОборотыПлановыхОпераций") Тогда
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.ОбменДанными.Отправитель = ДокументОбъект.ОбменДанными.Отправитель;
		КонецЕсли;
		 
	КонецЦикла;
	
КонецПроцедуры

// Выбирает и перепроводит все операции воода/изменения остатка за указанный период
//
//Параметры:
//	НачалоПериода- Дата - начало периода, с которого нужно выбрать все операции Ввода/изменения остатка
//
Процедура ПерепровестиИзмененияОстатков(НачалоПериода, УзелКорреспондента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВводИзменениеОстатка.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВводИзменениеОстатка КАК ВводИзменениеОстатка
	|ГДЕ
	|	ВводИзменениеОстатка.Дата >= &НачалоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВводИзменениеОстатка.Дата,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВводИзменениеОстатков.ОбъектУчета КАК ОбъектУчета,
	|	МАКСИМУМ(ВводИзменениеОстатков.Регистратор) КАК Регистратор,
	|	МАКСИМУМ(ВводИзменениеОстатков.Период) КАК Период,
	|	ВводИзменениеОстатков.РазделУчета
	|ИЗ
	|	Последовательность.ВводИзменениеОстатков КАК ВводИзменениеОстатков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последовательность.ВводИзменениеОстатков.Границы КАК ВводИзменениеОстатковГраницы
	|		ПО ВводИзменениеОстатков.ОбъектУчета = ВводИзменениеОстатковГраницы.ОбъектУчета
	|			И ВводИзменениеОстатков.РазделУчета = ВводИзменениеОстатковГраницы.РазделУчета
	|			И ВводИзменениеОстатков.МоментВремени > ВводИзменениеОстатковГраницы.МоментВремени
	|			И (ВводИзменениеОстатков.Регистратор.Проведен)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВводИзменениеОстатков.ОбъектУчета,
	|	ВводИзменениеОстатков.РазделУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	Выборка = ПакетРезультатов[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		УдалятьРегистрациюИзменений =  ЗначениеЗаполнено(УзелКорреспондента) 
				И Не ПланыОбмена.ИзменениеЗарегистрировано(УзелКорреспондента, Выборка.Ссылка);
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
		ДокументОбъект.ДополнительныеСвойства.Вставить("НеВосстанавливатьПоследовательность",  Истина);
		
		Попытка
			ДокументОбъект.Записать(?(ДокументОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалость обработать <%1>.
				|Информация для службы поддержки: ""%2""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ДокументОбъект, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Сообщить(ТекстСообщения);
		КонецПопытки; 
		
		//Удаляем регистрацию полученного документа
		Если УдалятьРегистрациюИзменений Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелКорреспондента, Выборка.Ссылка);
		КонецЕсли; 
		
	КонецЦикла;
	
	// Сдвигаем границу последовательности на последний документ для каждого объекта учета
	Выборка = ПакетРезультатов[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Последовательности.ВводИзменениеОстатков.УстановитьГраницу(Выборка.Регистратор.МоментВремени(), 
						Новый Структура("ОбъектУчета,РазделУчета", Выборка.ОбъектУчета, Выборка.РазделУчета));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураКлючевыхДополнительныхСвойствОперации(ДокументОбъект, ИменаСвойств) 

	Результат = Новый Структура;
	ИскомыеСвойства = СтрРазделить(ИменаСвойств, ",", Ложь);
	Для каждого ИмяСвойства Из ИскомыеСвойства Цикл
		ЗначениеСвойства = Неопределено;
		Если ДокументОбъект.ДополнительныеСвойства.Свойство(ИмяСвойства, ЗначениеСвойства) Тогда
			Результат.Вставить(ИмяСвойства, ОбщегоНазначенияКлиентСервер.СкопироватьРекурсивно(ЗначениеСвойства));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции
 
#КонецОбласти


