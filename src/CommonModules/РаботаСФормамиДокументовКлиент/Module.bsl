////////////////////////////////////////////////////////////////////////////////
// РаботаСФормамиДокументовКлиентСервер: обслуживание форм докумнетов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открыват список видов операций для осуществления выбора
//
//Параметры:
//	ИсключаемыеВидыОпераций - строка или список значений с именами видов операций - виды операций, которые нужно исключить из выбора
//	ОповещениеОВыборе - описание оповещения о выборе
//	ТекущийВидОперации - строка - наименование вида операции, которую нужно выделить при отрытии списка
//	Заголовок - заголовок списка выбора
//
Процедура ПоказатьВыборВидаОперации(ИсключаемыеВидыОпераций, ОповещениеОВыборе, ТекущийВидОперации = Неопределено, Заголовок = "") Экспорт
	
	СписокВидовОпераций = ДеньгиВызовСервера.ПолучитьСписокВидовОпераций(Неопределено, ИсключаемыеВидыОпераций);
	
	Если Не ЗначениеЗаполнено(Заголовок) Тогда
		Заголовок = НСтр("ru='Выберите вид операции'"); 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийВидОперации) Тогда
		ТекущийЭлемент = СписокВидовОпераций.НайтиПоЗначению(ТекущийВидОперации);
	Иначе
		ТекущийЭлмент = Неопределено;
	КонецЕсли;
	
	СписокВидовОпераций.ПоказатьВыборЭлемента(ОповещениеОВыборе, Заголовок, ТекущийЭлемент);
	
КонецПроцедуры

// Открывает форму нового документа с проверкой необходимости выбирать шаблон его шаблона
//
//Параметры:
//	ИмяВидаДокумента - Строка - Имя объекта метаданных - создаваемого документа
//	ПараметрыДокумента - Структура (не обязательно) - структура, которая содержит параметры заполнения и другую инфомрацию
//	ВладелецФормыДокумента - УправляемаяФорма или Элемент формы (не обязательно) - владелец открываемой формы документа
//	ОповещениеОЗакрытииФормыДокумента - ОписаниеОповещения (не обязательно) - оповещение о закрытии формы нового документа (не списка шаблонов!)
//	
Процедура ОткрытьФормуНовогоДокументаСУчетомШаблона(ИмяВидаДокумента, ПараметрыДокумента = Неопределено, ВладелецФормыДокумента = Неопределено, ОповещениеОЗакрытииФормыДокумента = Неопределено) Экспорт

	// Проверка полученных параметров
	Если Не ЗначениеЗаполнено(ИмяВидаДокумента) Тогда
		ВызватьИсключение НСтр("ru='Не указан вид создаваемой операции'");
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыДокумента) <> тип("Структура") Тогда
		ПараметрыФормыНовогоДокумента = Новый Структура;
	КонецЕсли;
	
	// Создаем структуру дополнительных параметров для описания оповещения
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяВидаДокумента", ИмяВидаДокумента);
	ДополнительныеПараметры.Вставить("ОповещениеОЗакрытииФормыДокумента", ОповещениеОЗакрытииФормыДокумента);
	ДополнительныеПараметры.Вставить("ПараметрыДокумента", ПараметрыДокумента);
	ДополнительныеПараметры.Вставить("ВладелецФормыДокумента", ВладелецФормыДокумента);
	
	// Проверяем количество шаблонов для операций указанного вида
	ИнформацияОШаблонах = ОбслуживаниеДокументовВызовСервера.ИнформацияОШаблонахИПлановыхОперациях(ИмяВидаДокумента);
	Если ИнформацияОШаблонах.КоличествоПлановыхОпераций + ИнформацияОШаблонах.КоличествоШаблоновБезРасписания = 0 Тогда
		
		// Нет ни шаблонов, ни плановых операций. Сразу открываем форму нового документа
		ВыборШаблонаЗавершение(ИмяВидаДокумента, ДополнительныеПараметры);
		
	Иначе
		
		// Открываем форму выбора шаблона
		ПарметрыФормыВыбора = Новый Структура;
		ПарметрыФормыВыбора.Вставить("СписокПлановыхОпераций", ИнформацияОШаблонах.СписокПлановыхОпераций);
		ПарметрыФормыВыбора.Вставить("ИмяВидаДокумента",       ИмяВидаДокумента);
		Оповещение = Новый ОписаниеОповещения("ВыборШаблонаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("ОбщаяФорма.ВыборШаблонаОперации", ПарметрыФормыВыбора, ВладелецФормыДокумента, , , , Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму нового документа, заполненного по выбраному шаблону
Процедура ОбработатьВыборШаблонаДокумента(ИмяВидаДокумента, СсылкаНаШаблон, ДатаПлановойОперации, АдресПараметровФормы, ВладелецФормы) Экспорт

	ПараметрыФормыНовогоДокумента = Неопределено;
	Если ЗначениеЗаполнено(АдресПараметровФормы) Тогда
		ПараметрыФормыНовогоДокумента = ПолучитьИзВременногоХранилища(АдресПараметровФормы);
	КонецЕсли; 
	Если ТипЗнч(ПараметрыФормыНовогоДокумента) <> Тип("Структура") Тогда
		ПараметрыФормыНовогоДокумента = Новый Структура;
	КонецЕсли; 
	ПараметрыФормыНовогоДокумента.Вставить("ЗначениеКопирования", СсылкаНаШаблон);
	ПараметрыФормыНовогоДокумента.Вставить("ЗначенияЗаполнения", Новый Структура("ЭтоШаблон, Дата", Ложь, ДатаПлановойОперации));
	ПараметрыФормыНовогоДокумента.Вставить("ВладелецРасписания", СсылкаНаШаблон);
	ПараметрыФормыНовогоДокумента.Вставить("ПлановаяДата",   ДатаПлановойОперации);
	ПараметрыФормыНовогоДокумента.Вставить("АктуальнаяДата", ДатаПлановойОперации);
	
	ОткрытьФорму("Документ." + ИмяВидаДокумента + ".ФормаОбъекта", ПараметрыФормыНовогоДокумента, ВладелецФормы);

КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Завершение диалога выбора шаблона
Процедура ВыборШаблонаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДокумента = ДополнительныеПараметры.ПараметрыДокумента;
	
	Если ТипЗнч(Результат) = Тип("Структура") И ЗначениеЗаполнено(Результат.ШаблонОперации) Тогда
		
		ПараметрыДокумента.Вставить("ВладелецРасписания",  Результат.ШаблонОперации);	
		ПараметрыДокумента.Вставить("ПлановаяДата",        Результат.ПлановаяДата);	
		ПараметрыДокумента.Вставить("АктуальнаяДата",      Результат.АктуальнаяДата);	
		
		Если ЗначениеЗаполнено(Результат.ПлановаяОперация) Тогда
			
			ПараметрыДокумента.Вставить("Ключ", Результат.ПлановаяОперация);
			Если ПараметрыДокумента.Свойство("ЗначениеКопирования") Тогда
				ПараметрыДокумента.Удалить("ЗначениеКопирования");
			КонецЕсли;
			
		Иначе
			
			ПараметрыДокумента.Вставить("ЗначениеКопирования", Результат.ШаблонОперации);
			
			Если Не ПараметрыДокумента.Свойство("ЗначенияЗаполнения") Или ПараметрыДокумента.ЗначенияЗаполнения = Неопределено Тогда
				ПараметрыДокумента.Вставить("ЗначенияЗаполнения", Новый Структура);
			КонецЕсли;
			ПараметрыДокумента.ЗначенияЗаполнения.Вставить("ЭтоШаблон", Ложь);
			ПараметрыДокумента.ЗначенияЗаполнения.Вставить("Дата", ?(ЗначениеЗаполнено(Результат.АктуальнаяДата), Результат.АктуальнаяДата, Результат.ПлановаяДата));
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОткрытьФорму("Документ." + ДополнительныеПараметры.ИмяВидаДокумента + ".ФормаОбъекта", ПараметрыДокумента, 
					ДополнительныеПараметры.ВладелецФормыДокумента, , , , ДополнительныеПараметры.ОповещениеОЗакрытииФормыДокумента);
	
КонецПроцедуры
 

#КонецОбласти


