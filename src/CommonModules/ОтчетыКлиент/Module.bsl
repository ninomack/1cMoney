////////////////////////////////////////////////////////////////////////////////
// ОтчетыКлиент: обработка расшифровки и других событий отчетов на клиенте
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ОбработкаРасшифровки(ФормаОтчета, Расшифровка, СтандартнаяОбработка) Экспорт
	
	Перем ВыбранноеДействие, ПараметрыВыбранногоДействия;
	
	СтандартнаяОбработка = Ложь;
	
	ИсточникДоступныхНастроекКД = Новый ИсточникДоступныхНастроекКомпоновкиДанных(ФормаОтчета.ПараметрыОбъекта.АдресСхемыКомпоновкиДанных);
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ФормаОтчета.ДанныеРасшифровки, ИсточникДоступныхНастроекКД);
	
	ДоступныеДействия = Новый Массив;
	Если ТипЗнч(ФормаОтчета.ОтчетТабличныйДокумент.ТекущаяОбласть) = Тип("РисунокТабличногоДокумента") Тогда
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	Иначе
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("Форма, Расшифровка", ФормаОтчета, Расшифровка);
	ЗавершениеВыбора = Новый ОписаниеОповещения("ВыбратьДействиеЗавершение", ОтчетыКлиент, ДополнительныеПараметры);
	ОбработкаРасшифровки.ПоказатьВыборДействия(ЗавершениеВыбора, Расшифровка, ДоступныеДействия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик для завершения выбора действия из процедуры ОбработкаРасшифровки()
Процедура ВыбратьДействиеЗавершение(ВыполненноеДействие, ПараметрВыполненногоДействия, ДополнительныеПараметры) Экспорт
	
	Если ВыполненноеДействие = Неопределено ИЛИ ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет Тогда
		
	ИначеЕсли ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
		
		ПоказатьЗначение(, ПараметрВыполненногоДействия);
		
	Иначе
		
		// Откроем расшифровку в новой форме
		ПараметрыФормы = Новый Структура("Расшифровка, СформироватьПриОткрытии", 
			Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(ДополнительныеПараметры.Форма.ДанныеРасшифровки, 
					ДополнительныеПараметры.Расшифровка, 
					ПараметрВыполненногоДействия), 
				Истина);
		ОткрытьФорму(ДополнительныеПараметры.Форма.ИмяФормы, ПараметрыФормы, , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

