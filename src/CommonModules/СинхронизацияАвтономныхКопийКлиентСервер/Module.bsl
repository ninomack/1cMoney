////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает URI пространства имен для указанной версии обмена
//
//Параметры:
//	НомерВерсии - Строка - номер версии обмена, для которой нужно вернуть URI
//
//Возвращаемое значение:
//	Строка
//
Функция URIПространстваИмен(НомерВерсии = Неопределено) Экспорт
	
	Если СтрНайти(НомерВерсии, "1.") = 1 Тогда
		Возврат "http://v8.1c.ru/Money/MobileConfig";
	Иначе
		Возврат "http://www.1c.ru/money/2.0/DataExchange";
	КонецЕсли;
	
КонецФункции

// Возвращает имя сервиса синхронизации данных
//
//Параметры:
//	НомерВерсии - Строка - номер версии обмена, для которой нужно вернуть имя сервиса
//
//Возвращаемое значение:
//	Строка
//
Функция ИмяСервисаСинхронизации(НомерВерсии = Неопределено) Экспорт
	
	Если СтрНайти(НомерВерсии, "1.") = 1 Тогда
		Возврат "MobileService";
	Иначе
		Возврат "MoneyDataExchange";
	КонецЕсли;
	
КонецФункции


// Возвращает текущую версию формата
//
//Параметры:
//	нет
//
//Возвращаемое значение:
//	Строка
//
Функция ТекущаяВерсияФормата() Экспорт
	Возврат "2.0";
КонецФункции

// Возвращает минимальную поддерживаемую в обмене версию формата
//
//Параметры:
//	нет
//
//Возвращаемое значение:
//	Строка
//
Функция МинимальнаяВерсияФормата() Экспорт
	Возврат "1.6.3";
КонецФункции

// Выделяет номер версии из строки пространства имен
//
//Параметры:
//	ПространствоИмен - Строка - типа "http://v8.1c.ru/Money/MobileConfig"
//
//Возвращаемое значение:
//	Строка - номер версии, если найден
//	Неопределено - если номер версии не найден
//
Функция ВерсияФорматаОбменаИзПространстваИмен(Знач ПространствоИмен) Экспорт

	Если Не ЗначениеЗаполнено(ПространствоИмен) Тогда
		Возврат Неопределено; 
	КонецЕсли;
	
	ПространствоИмен = НРег(ПространствоИмен);
	Если ПространствоИмен = НРег("http://v8.1c.ru/Money/MobileConfig") Тогда
		Возврат "1.6.3"; 
	КонецЕсли;
	 
	ИмяФормата   = "http://v8.1c.ru/Money/MoneyData/";
	ПозицияИмени = СтрНайти(ПространствоИмен, НРег(ИмяФормата));
	Если ПозицияИмени = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = "";
	Для Счетчик = ПозицияИмени + СтрДлина(ИмяФормата) По СтрДлина(ПространствоИмен) Цикл
	
		Символ = Сред(ПространствоИмен, Счетчик, 1);
		Если СтрНайти("0123456789.", Символ) > 0 Тогда
			Результат = Результат + Символ;
		Иначе
			Прервать;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ?(ЗначениеЗаполнено(Результат), Результат, Неопределено);

КонецФункции

// Проверяет возможность выполнить обмен по указанному номеру версии формата
//
//Параметры:
//	ПроверяемыйНомерВерсии - Строка, например, "2.0.1"
//
//Возвращаемое значение:
//	Булево - истина, если версия поддерживается и Ложь в ином случае
//
Функция ВерсияФорматаПоддерживается(ПроверяемыйНомерВерсии) Экспорт

	Возврат ДеньгиКлиентСервер.СравнитьВерсии(ПроверяемыйНомерВерсии, "1.6") > 0
		И ДеньгиКлиентСервер.СравнитьВерсии(ПроверяемыйНомерВерсии, ТекущаяВерсияФормата()) <= 0;

КонецФункции



#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверитьЗавершениеURL(URL) Экспорт
	
	Если Не ЗначениеЗаполнено(URL) Тогда
		Возврат;
	КонецЕсли;
	
	Если Прав(СокрП(URL), 1) <> "/" Тогда
		URL = СокрП(URL) + "/";
	КонецЕсли;
	
КонецПроцедуры
 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции



#КонецОбласти