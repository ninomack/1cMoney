
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтотУзел = Объект.Ссылка = ПланыОбмена.МобильноеПриложение.ЭтотУзел();
	
	Если НЕ ЗначениеЗаполнено(Объект.КоличествоВыгружаемыхПериодов) Тогда
		КоличествоВыгружаемыхПериодов = 1;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидВыгружаемогоПериода) Тогда
		ВидВыгружаемогоПериода = Перечисления.Периодичность.Месяц;
	КонецЕсли; 
	
	Элементы.Код.Заголовок = ?(ЭтотУзел, 
		НСтр("ru = 'Код центральной базы'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		НСтр("ru = 'Код мобильного устройства'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()) );
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если Не ЗавершениеРаботы Тогда
		Оповестить("Запись_УзелПланаОбмена");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ОбменДаннымиСервер.ФормаУзлаПриЗаписиНаСервере(ТекущийОбъект, Отказ);
КонецПроцедуры


#КонецОбласти




#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СписокУстройствОткрытьСписокПолученныхИзменений(Команда)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("МестоИзменения", Объект.Ссылка);
	
	ОткрытьФорму("РегистрСведений.ИзмененияОбъектовВоВнешнихПрограммах.ФормаСписка", Новый Структура("Отбор", СтруктураОтбора));
	
КонецПроцедуры
 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Элементы.НомерОтправленного.Доступность  = НЕ Форма.ЭтотУзел;
	Элементы.НомерПринятого.Доступность      = НЕ Форма.ЭтотУзел;
	Элементы.Активность.Доступность          = НЕ Форма.ЭтотУзел;
	
КонецПроцедуры


#КонецОбласти


