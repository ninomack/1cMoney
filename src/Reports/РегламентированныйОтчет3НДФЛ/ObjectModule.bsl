
#Область ПрограммныйИнтерфейс



// Возвращает таблицу значений, заполненную кодами вычетов по указанным в параметрах условиям:
//
//Параметры:
//	СтатусНалогоплательщика - Число - 1 = для резидента, 2 - для не резидента
//	ГодОтчета - Число - год, для которого подбираются вычеты
//	СтавкаНалога - Число или Неопределено - ставка налога, для которой нужно подобрать вычеты
//
//Возвращаемое значение
//	ТаблицаЗначений
Функция ТаблицаКодовВычетов(СтатусНалогоплательщика, ГодОтчета, СтавкаНалога = Неопределено) Экспорт

	// Параметры проверки заданных условий
	ГодПроверки        = ?(ГодОтчета = Неопределено, Год(ТекущаяДатаСеанса()), ГодОтчета); 
	ГодМакета          = ?(ГодПроверки = 2014, "2015", Формат(ГодПроверки + 1, "ЧДЦ=; ЧГ="));
	СтавкаСтрокой      = ?(СтавкаНалога = Неопределено, "0", Формат(СтавкаНалога, "ЧДЦ=; ЧГ=")) + ",";
	НомерКолонкиСтавки = ?(СтатусНалогоплательщика = 2, 10, 9);
	
	Макет = ПолучитьМакет("КодыДоходовИВычетов" + ГодМакета  + "кв1");
	
	Результат = ПустаяТаблицаКодовВычетов();
	
	ОбластьВычетов = Макет.ПолучитьОбласть("Вычеты");
	Для Счетчик = 1 По ОбластьВычетов.ВысотаТаблицы Цикл
		
		// проверяем соответствие Вычета заданным условиям
		// Год прекращения вычета
		ГодОкончания = ОбластьВычетов.Область(Счетчик, 11, Счетчик, 11).Текст;
		Если ЗначениеЗаполнено(ГодОкончания) Тогда
			Попытка
				ГодОкончания = Число(ГодОкончания);
			Исключение
				Продолжить;
			КонецПопытки; 
			Если ГодОкончания < ГодПроверки Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Соответствие вычета ставке налога
		СтавкаВычета = ОбластьВычетов.Область(Счетчик, НомерКолонкиСтавки, Счетчик, НомерКолонкиСтавки).Текст;
		Если ПустаяСтрока(СтавкаВычета) Тогда
			Продолжить;
		КонецЕсли;
		 
		Если СтавкаНалога <> Неопределено И Найти(СтавкаВычета + ",", СтавкаСтрокой) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Добавляем вычет в таблицу
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.Код = ОбластьВычетов.Область(Счетчик, 1, Счетчик, 1).Текст;
		НоваяСтрока.Наименование = ОбластьВычетов.Область(Счетчик, 2, Счетчик, 2).Текст;
		НоваяСтрока.ТолькоДляДохода = НЕ ПустаяСтрока(ОбластьВычетов.Область(Счетчик, 3, Счетчик, 3).Текст);
		НоваяСтрока.Группа = ОбластьВычетов.Область(Счетчик, 4, Счетчик, 4).Текст;
		НоваяСтрока.СпособРасчета = ОбластьВычетов.Область(Счетчик, 5, Счетчик, 5).Текст;
		
		РазмерВычета = ОбластьВычетов.Область(Счетчик, 6, Счетчик, 6).Текст;
		Если ПустаяСтрока(РазмерВычета) Тогда
			РазмерВычета = ОбластьВычетов.Область(Счетчик, 7, Счетчик, 7).Текст;
		КонецЕсли;
		 
		Если ЗначениеЗаполнено(РазмерВычета) Тогда
			Попытка
				РазмерВычета = Число(РазмерВычета);
			Исключение
				РазмерВычета = 0;
			КонецПопытки; 
		Иначе
			РазмерВычета = 0;
		КонецЕсли;
		НоваяСтрока.РазмерВычета = РазмерВычета;
		
		ПределДохода = ОбластьВычетов.Область(Счетчик, 8, Счетчик, 8).Текст;
		Если ЗначениеЗаполнено(ПределДохода) Тогда
			Попытка
				ПределДохода = Число(ПределДохода);
			Исключение
				ПределДохода = 0;
			КонецПопытки; 
		Иначе
			ПределДохода = 0;
		КонецЕсли;
		НоваяСтрока.ПределДохода = ПределДохода;
		
		
	КонецЦикла;	 
	
	Возврат Результат;

КонецФункции
 
// Возвращает таблицу значений, заполненную кодами доходов по указанным в параметрах условиям:
//
//Параметры:
//	СтатусНалогоплательщика - Число - 1 = для резидента, 2 - для не резидента
//	ГодОтчета - Число - год, для которого подбираются доходы
//	СтавкаНалога - Число или Неопределено - ставка налога, для которой нужно подобрать доходы
//
//Возвращаемое значение
//	ТаблицаЗначений
Функция ТаблицаКодовДоходов(СтатусНалогоплательщика, ГодОтчета, СтавкаНалога = Неопределено, ТолькоДля2НДФЛ = Ложь) Экспорт

	// Параметры проверки заданных условий
	ГодПроверки         = ?(ГодОтчета = Неопределено, Год(ТекущаяДатаСеанса()), ГодОтчета); 
	ГодМакета           = ?(ГодПроверки = 2014, "2015", Формат(ГодПроверки + 1, "ЧДЦ=; ЧГ="));
	СтавкаСтрокой       = ?(СтавкаНалога = Неопределено, "0", Формат(СтавкаНалога, "ЧДЦ=; ЧГ=")) + ",";
	НомерКолонкиСтавки  = ?(СтатусНалогоплательщика = 2, 4, 3);
	НомерКолонкиВычетов = ?(СтатусНалогоплательщика = 2, 8, 7);
	
	Макет = ПолучитьМакет("КодыДоходовИВычетов" + ГодМакета  + "кв1");
	
	Результат = ПустаяТаблицаКодовДоходов();
	
	ОбластьДоходов = Макет.ПолучитьОбласть("Доходы");
	Для Счетчик = 1 По ОбластьДоходов.ВысотаТаблицы Цикл
		
		// Соответствие вычета ставке налога
		ОграничениеСтавки = ОбластьДоходов.Область(Счетчик, НомерКолонкиСтавки, Счетчик, НомерКолонкиСтавки).Текст;
		Если ПустаяСтрока(ОграничениеСтавки) Тогда
			Продолжить;
		КонецЕсли;
		 
		Если СтавкаНалога <> Неопределено И Найти(ОграничениеСтавки + ",", СтавкаСтрокой) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТолькоДля2НДФЛ И ПустаяСтрока(ОбластьДоходов.Область(Счетчик, 9, Счетчик, 9).Текст) Тогда
			Продолжить;
		КонецЕсли;
		 
		
		// Добавляем доход в таблицу
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.Код                = ОбластьДоходов.Область(Счетчик, 1, Счетчик, 1).Текст;
		НоваяСтрока.Наименование       = ОбластьДоходов.Область(Счетчик, 2, Счетчик, 2).Текст;
		НоваяСтрока.Группа             = ОбластьДоходов.Область(Счетчик, 5, Счетчик, 5).Текст;
		НоваяСтрока.КодыВычетов        = СокрЛП(ОбластьДоходов.Область(Счетчик, НомерКолонкиВычетов, Счетчик, НомерКолонкиВычетов).Текст);
		НоваяСтрока.ВычетПоУмолчанию   = СокрЛП(ОбластьДоходов.Область(Счетчик, 6, Счетчик, 6).Текст);
		НоваяСтрока.ИспользуетсяВ2НДФЛ = НЕ ПустаяСтрока(ОбластьДоходов.Область(Счетчик, 9, Счетчик, 9).Текст);
		
		// Определяем ставку налога
		Если Найти(ОграничениеСтавки, ",") > 0 Тогда
			ОграничениеСтавки = Лев(ОграничениеСтавки, Найти(ОграничениеСтавки, ",") - 1);
		КонецЕсли; 
		НоваяСтрока.СтавкаНалога = ?(СтавкаНалога = Неопределено, Число(ОграничениеСтавки), СтавкаНалога);
		
	КонецЦикла;	 
	
	Возврат Результат;

КонецФункции
 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПустаяТаблицаКодовВычетов() 

	ОписаниеТекста = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000));
	ОписаниеСтроки = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50));
	ОписаниеСуммы = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(14, 2));
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
	Результат.Колонки.Добавить("Наименование", ОписаниеТекста);
	Результат.Колонки.Добавить("Группа", ОписаниеСтроки);
	Результат.Колонки.Добавить("СпособРасчета", ОписаниеСтроки);
	Результат.Колонки.Добавить("РазмерВычета", ОписаниеСуммы);
	Результат.Колонки.Добавить("ПределДохода", ОписаниеСуммы);
	Результат.Колонки.Добавить("ТолькоДляДохода", Новый ОписаниеТипов("Булево"));

	Возврат Результат;
	
КонецФункции
 
Функция ПустаяТаблицаКодовДоходов() 

	ОписаниеТекста = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000));
	ОписаниеСтроки = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50));
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)));
	Результат.Колонки.Добавить("Наименование",       ОписаниеТекста);
	Результат.Колонки.Добавить("КодыВычетов",        ОписаниеСтроки);
	Результат.Колонки.Добавить("ВычетПоУмолчанию",   ОписаниеСтроки);
	Результат.Колонки.Добавить("Группа",             ОписаниеСтроки);
	Результат.Колонки.Добавить("ИспользуетсяВ2НДФЛ", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СтавкаНалога",       Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0)));

	Возврат Результат;
	
КонецФункции
 

#КонецОбласти

