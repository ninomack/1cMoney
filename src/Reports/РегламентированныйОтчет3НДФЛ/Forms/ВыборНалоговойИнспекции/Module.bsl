////////////////////////////////////////////////////////////////////////////////
// Модуль формы <ВыборНалоговойИнспекции>
//  
//Параметры формы:  
//	КодИФНС - Строка - код ИФНС, на которой нужно установить курсор
//  
//  
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <Регионы>

&НаКлиенте
Процедура РегионыПриАктивизацииСтроки(Элемент)
	
	СтрокаРегиона = Элементы.Регионы.ТекущиеДанные;
	Если СтрокаРегиона =  Неопределено ИЛИ СтрокаРегиона.Код = КодВыведенногоРегиона Тогда
		Возврат;
	КонецЕсли; 
	
	РегионыПриАктивизацииСтрокиСервер(СтрокаРегиона.Код);
	КодВыведенногоРегиона = СтрокаРегиона.Код;
	
КонецПроцедуры
 
&НаКлиенте
Процедура СписокИнспекцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыборНаКлиенте();
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	ВыборНаКлиенте();
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	// Подготовка реквизитов формы
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	МакетРегионов = ОтчетОбъект.ПолучитьМакет("Регионы");
	
	МакетСписка = МакетРегионов.ПолучитьОбласть("Данные");
	Для счетчик=1 По МакетСписка.ВысотаТаблицы Цикл
	
		код = МакетСписка.Область(счетчик,1,счетчик,1).Текст;
		Если ПустаяСтрока(код) Тогда
			Прервать;
		КонецЕсли; 
		
		НоваяСтрока = Регионы.Добавить();
		НоваяСтрока.Код = код;
		НоваяСтрока.Наименование = МакетСписка.Область(счетчик,2,счетчик,2).Текст;
	
	КонецЦикла; 
	
	ТекущийКодРегиона = "00";
	
	Если ЗначениеЗаполнено(Параметры.КодИФНС) Тогда
		ТекущийКодИнспекции = СокрЛП(Параметры.КодИФНС);
		ТекущийКодРегиона = Лев(Параметры.КодИФНС, 2);
	КонецЕсли; 
	
	Строкарегиона = Регионы.НайтиСтроки(Новый Структура("Код", ТекущийКодРегиона));
	Если Строкарегиона.Количество() > 0 Тогда
		Элементы.Регионы.ТекущаяСтрока = Строкарегиона[0].ПолучитьИдентификатор();
	КонецЕсли; 
	
	// Настройка внешнего вида
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УправлениеФормой(Форма)


КонецПроцедуры

&НаСервере
Процедура РегионыПриАктивизацииСтрокиСервер(КодРегиона)

	ТаблицаПоРегиону = Неопределено;
	ТаблицаПоРегиону = ВывестиИнспекцииРегиона(КодРегиона);
	
	СписокИнспекций.Загрузить(ТаблицаПоРегиону);
	
	Если ЗначениеЗаполнено(ТекущийКодИнспекции) Тогда
		СтрокаИнспекции = СписокИнспекций.НайтиСтроки(Новый Структура("Код", ТекущийКодИнспекции));
		Если СтрокаИнспекции.Количество() > 0 Тогда
			Элементы.СписокИнспекций.ТекущаяСтрока = СтрокаИнспекции[0].ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры
 
&НаСервере
Функция ВывестиИнспекцииРегиона(КодРегиона, ОтчетОбъект = Неопределено)

	Если ОтчетОбъект = Неопределено Тогда
		ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	КонецЕсли; 
	МакетИнспекций = ОтчетОбъект.ПолучитьМакет("СписокИнспекций");
	ОбластьРегиона = МакетИнспекций.ПолучитьОбласть("_" + КодРегиона);
	
	ТаблицаСписка = Новый ТаблицаЗначений;
	ТаблицаСписка.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка"));
	ТаблицаСписка.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	
	Для Счетчик=1 По ОбластьРегиона.ВысотаТаблицы Цикл
		НоваяСтрока = ТаблицаСписка.Добавить();
		НоваяСтрока.Наименование = ОбластьРегиона.Область(Счетчик,1,Счетчик,1).Текст;
		НоваяСтрока.Код = ОбластьРегиона.Область(Счетчик,2,Счетчик,2).Текст;
	КонецЦикла; 
	
	Возврат ТаблицаСписка;

КонецФункции

&НаКлиенте
Процедура ВыборНаКлиенте()

	РезультатВыбора = Новый Структура("ВидВыбранногоЗанчения,Код,Наименование", "НалоговаяИнспекция");
	СтрокаВыбора = Элементы.СписокИнспекций.ТекущиеДанные;
	Если СтрокаВыбора <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(РезультатВыбора, СтрокаВыбора);
	КонецЕсли; 
	
	ОповеститьОВыборе(РезультатВыбора);

КонецПроцедуры
 



