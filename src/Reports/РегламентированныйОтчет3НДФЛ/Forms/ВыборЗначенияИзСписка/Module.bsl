
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяПоказателя = Параметры.ИмяПоказателя;
	Если ЗначениеЗаполнено(Параметры.ЗаголовокФормыВыбора) Тогда
		Заголовок = Параметры.ЗаголовокФормыВыбора;
	Иначе
		Заголовок = НСтр("ru = 'Выбор значения'") ;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокКолонкиКода) Тогда
		Элементы.СписокВыбораКод.Заголовок = Параметры.ЗаголовокКолонкиКода;
	Иначе
		Элементы.СписокВыбораКод.Заголовок = НСтр("ru = 'Код'") ;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокКолонкиНаименования) Тогда
		Элементы.СписокВыбораНаименование.Заголовок = Параметры.ЗаголовокКолонкиНаименования;
	Иначе
		Элементы.СписокВыбораНаименование.Заголовок = НСтр("ru = 'Наименование'") ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИмяМакета) Тогда
		
		ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
		Макет = ОтчетОбъект.ПолучитьМакет(Параметры.ИмяМакета);
		Если ЗначениеЗаполнено(Параметры.ИмяОбласти) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть(Параметры.ИмяОбласти);
		Иначе
			ОбластьМакета = Макет;
		КонецЕсли; 
		
		Если Параметры.ИмяОбласти = "КодыДоходов" Тогда
			
			Если ЗначениеЗаполнено(Параметры.КодыДоходов) И Прав(Параметры.КодыДоходов, 1) <> "," Тогда
				Параметры.КодыДоходов = Параметры.КодыДоходов + ",";
			КонецЕсли; 
			ИскомаяСтавкаНалога = Строка(Параметры.СтавкаНалога) + ",";
			
			Для Счетчик=1 По ОбластьМакета.ВысотаТаблицы Цикл
				
				Если Параметры.СтатусНалогоплательщика = 2 Тогда
					СтавкаНалога = ОбластьМакета.Область(Счетчик,6,Счетчик,6).Текст;
					КодыВычетов = ОбластьМакета.Область(Счетчик,8,Счетчик,8).Текст;
				Иначе
					СтавкаНалога = ОбластьМакета.Область(Счетчик,5,Счетчик,5).Текст;
					КодыВычетов = ОбластьМакета.Область(Счетчик,7,Счетчик,7).Текст;
				КонецЕсли; 
				КодДохода = ОбластьМакета.Область(Счетчик,1,Счетчик,1).Текст;
				
				// Проверяем фильтры на ставку налога и строку кодов:
				Если ЗначениеЗаполнено(Параметры.СтавкаНалога) И Найти(СтавкаНалога + ",", ИскомаяСтавкаНалога) = 0 Тогда
					Продолжить;
				КонецЕсли;
				Если ЗначениеЗаполнено(Параметры.КодыДоходов) И Найти(Параметры.КодыДоходов, КодДохода + ",") = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Параметры.СтавкаНалога) Тогда
					СтавкаЧислом = Параметры.СтавкаНалога;
				ИначеЕсли СтавкаНалога = "13,30" Тогда
					СтавкаЧислом = 30;
				КонецЕсли; 
				
				НоваяСтрока = СписокВыбора.Добавить();
				НоваяСтрока.Код = КодДохода;
				НоваяСтрока.Наименование = ОбластьМакета.Область(Счетчик,2,Счетчик,2).Текст;
				НоваяСтрока.СтавкаНалога = СтавкаЧислом;
				НоваяСтрока.КодыВычетов  = КодыВычетов;
				
			КонецЦикла; 
			
		ИначеЕсли Параметры.ИмяОбласти = "КодыВычетов" Тогда
			
			Если ЗначениеЗаполнено(Параметры.КодыДоходов) И НЕ ЗначениеЗаполнено(Параметры.КодыВычетов) Тогда
				Параметры.КодыВычетов = ПолучитьСтрокуКодовВычетаПоКодуДохода(ОтчетОбъект, Параметры.ИмяМакета, Параметры.КодыДоходов, Параметры.СтатусНалогоплательщика);
				Если Параметры.КодыВычетов <> "" Тогда
					Параметры.КодыВычетов = Параметры.КодыВычетов + ",";
				Иначе
					Возврат;
				КонецЕсли; 
			КонецЕсли; 
				
			Для Счетчик=1 По ОбластьМакета.ВысотаТаблицы Цикл
				
				ПределВычета = ОбластьМакета.Область(Счетчик, 5, Счетчик, 5).Текст;
				ПределВычета = ?(ЗначениеЗаполнено(ПределВычета), Число(ПределВычета), 0);
				ПределДохода = ОбластьМакета.Область(Счетчик, 6, Счетчик, 6).Текст;
				ПределДохода = ?(ЗначениеЗаполнено(ПределДохода), Число(ПределДохода), 0);
				СпособВычета = ОбластьМакета.Область(Счетчик, 7, Счетчик, 7).Текст;
				СпособВычета = ?(ЗначениеЗаполнено(СпособВычета), Число(СпособВычета), 0);
				
				КодВычета = ОбластьМакета.Область(Счетчик, 1, Счетчик, 1).Текст;
				
				// Проверяем фильтры на строку кодов:
				Если ЗначениеЗаполнено(Параметры.КодыВычетов) И Найти(Параметры.КодыВычетов, КодВычета + ",") = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = СписокВыбора.Добавить();
				НоваяСтрока.Код = КодВычета;
				НоваяСтрока.Наименование         = ОбластьМакета.Область(Счетчик,2,Счетчик,2).Текст;
				НоваяСтрока.ПределВычета         = ПределВычета;
				НоваяСтрока.ПределДохода         = ПределДохода;
				НоваяСтрока.СпособРасчетаВычета  = СпособВычета;
				
			КонецЦикла; 
			
		Иначе
			
			Для Счетчик=1 По ОбластьМакета.ВысотаТаблицы Цикл
				НоваяСтрока = СписокВыбора.Добавить();
				НоваяСтрока.Наименование = ОбластьМакета.Область(Счетчик,2,Счетчик,2).Текст;
				НоваяСтрока.Код = ОбластьМакета.Область(Счетчик,1,Счетчик,1).Текст;
			КонецЦикла; 
			
		КонецЕсли; 
		
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.КодТекущегоЗначения) Тогда
		СтрокиВыбора = СписокВыбора.НайтиСтроки(Новый Структура("Код", Параметры.КодТекущегоЗначения));
		Если СтрокиВыбора.Количество() > 0 Тогда
			Элементы.СписокВыбора.ТекущаяСтрока = СтрокиВыбора[0].ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере 
Функция ПолучитьСтрокуКодовВычетаПоКодуДохода(ОтчетОбъект, ИмяМакета, КодыДоходов, СтатусНалогоплательщика)

	Макет = ОтчетОбъект.ПолучитьМакет(ИмяМакета);
	ОбластьМакета = Макет.ПолучитьОбласть("КодыДоходов");
	ОбластьДохода = ОбластьМакета.НайтиТекст(КодыДоходов, , ОбластьМакета.Область(1, 1, ОбластьМакета.ВысотаТаблицы, 1), , Истина);
	Если ОбластьДохода <> Неопределено Тогда
		
		Если СтатусНалогоплательщика = 2 Тогда
			Возврат ОбластьМакета.Область(ОбластьДохода.Верх, 8, ОбластьДохода.Верх, 8).Текст;
		Иначе
			Возврат ОбластьМакета.Область(ОбластьДохода.Верх, 7, ОбластьДохода.Верх, 7).Текст;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат "";

КонецФункции
 

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РезультатВыбора = Новый Структура("ВидВыбранногоЗанчения,Код,Наименование,КодыВычетов,
			|СтавкаНалога,ПределВычета,ПределДохода,СпособРасчетаВычета", ИмяПоказателя);
	СтрокаВыбора = Элементы.СписокВыбора.ТекущиеДанные;
	Если СтрокаВыбора <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(РезультатВыбора, СтрокаВыбора);
	КонецЕсли; 
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

