

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	мСкопированаФорма = Параметры.мСкопированаФорма;
	мСохраненныйДок   = Параметры.мСохраненныйДок;
	
	Налогоплательщик = Параметры.Организация;
	
	Если Не ЗначениеЗаполнено(Налогоплательщик) Тогда
		Налогоплательщик = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации").ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	// ДЕНЬГИ
	//РегламентированнаяОтчетность.ПолучитьСписокДоступныхИндивидуальныхПредпринимателей(Элементы.Организация.СписокВыбора);
	Обработки.ПомощникЗаполнения3НДФЛ.ЗаполнитьСписокВыбораНалогоплательщиков(Элементы.Налогоплательщик.СписокВыбора);
	// Конец Деньги
	Если Элементы.Налогоплательщик.СписокВыбора.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Декларацию представляют: %1'"), КтоСдаетОтчет()));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Налогоплательщик) И Элементы.Налогоплательщик.СписокВыбора.НайтиПоЗначению(Налогоплательщик) = Неопределено Тогда
		Налогоплательщик = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации").ПустаяСсылка();
	КонецЕсли;
	
	ИсточникОтчета = РегламентированнаяОтчетностьВызовСервера.ИсточникОтчета(ИмяФормы);
	мТаблицаФормОтчета.Загрузить(РегламентированнаяОтчетностьВызовСервера.ОтчетОбъект(ИсточникОтчета).ТаблицаФормОтчета());
	
	мПериодичность = Перечисления.Периодичность.Год;
	
	Если ЗначениеЗаполнено(Параметры.мДатаНачалаПериодаОтчета) И ЗначениеЗаполнено(Параметры.мДатаКонцаПериодаОтчета) Тогда
		мДатаНачалаПериодаОтчета = НачалоГода(Параметры.мДатаНачалаПериодаОтчета);
		мДатаКонцаПериодаОтчета  = КонецГода(Параметры.мДатаКонцаПериодаОтчета);
	Иначе
		Период = ОбщегоНазначения.РабочаяДатаПользователя();
		Если Не ЗначениеЗаполнено(Период) Тогда
			Период = ТекущаяДатаСеанса();
		КонецЕсли;
		мДатаНачалаПериодаОтчета = НачалоГода(ДобавитьМесяц(Период, -12));
		мДатаКонцаПериодаОтчета  = КонецГода(мДатаНачалаПериодаОтчета);
	КонецЕсли;
	ПриИзмененииПериода();
	
	НадписьКтоСдаетОтчет = КтоСдаетОтчет();
	
	ПолеСсылкаИзмененияЗаконодательства = РегламентированнаяОтчетность.ПредставлениеСсылкиИзмененияЗаконодательства();
	ОбщаяЧастьСсылкиНаИзмененияЗаконодательства = РегламентированнаяОтчетность.ОбщаяЧастьСсылкиНаИзмененияЗаконодательства(ИсточникОтчета);
	
	УправлениеФормой(ЭтотОбъект);
	
	// ДЕНЬГИ
	ИспользоватьПомощникЗаполнения = Год(мДатаНачалаПериодаОтчета) > 2010;
	// Конец ДЕНЬГИ
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = "Активизировать" И ИмяСобытия = Заголовок Тогда
		Активизировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждениеЗавершение(Результат) Экспорт
	
	Активизировать();
	
КонецПроцедуры 

// ДЕНЬГИ
&НаКлиентеНаСервереБезКонтекста
Функция ДопустимоКопированиеОтчета(Знач ИмяКопируемойФормы, Знач ИмяФормыНазначения)

	ВерсияКопируемойФормы = Сред(ИмяКопируемойФормы, 12, 4);
	ВерсияФормыНазначения = Сред(ИмяФормыНазначения, 12, 4);
	
	Если ВерсияКопируемойФормы = ВерсияФормыНазначения 
		Или Число(ВерсияФормыНазначения) = Число(ВерсияКопируемойФормы) + 1
		Или Число(ВерсияКопируемойФормы) >= 2017
		Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяДатаКлиентСервер()
	#Если Сервер Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли 
КонецФункции 

// Конец ДЕНЬГИ 


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЗначениеЗаполнено(Налогоплательщик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеРедакцияФормыПриИзменении(Элемент)
	
	НайденныеСтроки = мТаблицаФормОтчета.НайтиСтроки(Новый Структура("РедакцияФормы", ПолеРедакцияФормы));
	Если НайденныеСтроки.Количество() <> 0 Тогда
		мВыбраннаяФорма = НайденныеСтроки[0].ФормаОтчета;
		ОписаниеНормативДок = НайденныеСтроки[0].ОписаниеОтчета;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеСсылкаИзмененияЗаконодательстваНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(ОбщаяЧастьСсылкиНаИзмененияЗаконодательства) Тогда
		СсылкаИзмененияЗаконодательства = ОбщаяЧастьСсылкиНаИзмененияЗаконодательства
			+ СтрШаблон("&currentYear=%1", Формат(Год(мДатаКонцаПериодаОтчета), "ЧГ=0"));
		ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке(СсылкаИзмененияЗаконодательства);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПредыдущийПериод(Команда)
	
	ИзменитьПериод(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСледующийПериод(Команда)
	
	ИзменитьПериод(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтчета(Команда)
	
	Заполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБезПомощника(Команда)
	
	Заполнить(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФорму(Команда)
	
	РегламентированнаяОтчетностьКлиент.ВыбратьФормуОтчетаИзДействующегоСписка(ЭтотОбъект,
		Новый ОписаниеОповещения("ВыбратьФормуЗавершение", ЭтотОбъект),
		СтрШаблон(НСтр("ru = 'Декларацию представляют: %1'"), НадписьКтоСдаетОтчет));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФормуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		мВыбраннаяФорма = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ОткрытьФормуОтчета.Доступность = ЗначениеЗаполнено(Форма.Налогоплательщик);
	Элементы.ГруппаПериод.Доступность = Истина; // ДЕНЬГИ ЗначениеЗаполнено(Форма.Организация);
	
	Элементы.ПолеСсылкаИзмененияЗаконодательства.Видимость = Не ПустаяСтрока(Форма.ОбщаяЧастьСсылкиНаИзмененияЗаконодательства)
		И РегламентированнаяОтчетностьКлиентСервер.ДоступенМониторингИзмененияЗаконодательства(Форма.мДатаКонцаПериодаОтчета);
	
	КоличествоФорм = РегламентированнаяОтчетностьКлиентСервер.КоличествоФормСоответствующихВыбранномуПериоду(Форма);
	Элементы.ПолеРедакцияФормы.Видимость = (КоличествоФорм > 1);
	Элементы.ОписаниеНормативДок.Видимость = (КоличествоФорм = 1);
	Элементы.ОткрытьФормуОтчета.Доступность = (КоличествоФорм > 0);
	
	Элементы.ФормаСоздатьБезПомощника.Видимость = ОтчетМожноЗаполнитьВПомощнике(Форма.мВыбраннаяФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПериод(Шаг)
	
	мДатаКонцаПериодаОтчета  = КонецГода(ДобавитьМесяц(мДатаКонцаПериодаОтчета, Шаг * 12));
	мДатаНачалаПериодаОтчета = НачалоГода(мДатаКонцаПериодаОтчета);
	
	ПриИзмененииПериода();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПериода()
	
	ПолеВыбораПериодичностиПоказаПериода = ПредставлениеПериода(
		НачалоДня(мДатаНачалаПериодаОтчета), КонецДня(мДатаКонцаПериодаОтчета), "ФП = Истина");
	
	НадписьСрокПредставленияОтчета = СрокПредставленияОтчетности(ЭтотОбъект);
	
	мВыбраннаяФорма = Отчеты.РегламентированныйОтчет3НДФЛ.ФормаПоУмолчанию(
		мДатаКонцаПериодаОтчета, ОписаниеНормативДок, ПолеРедакцияФормы);
	
	ДействующиеФормы = Отчеты.РегламентированныйОтчет3НДФЛ.ДействующиеФормы(мДатаКонцаПериодаОтчета);
	Элементы.ПолеРедакцияФормы.СписокВыбора.Очистить();
	Для Каждого СтрокаТаблицы Из ДействующиеФормы Цикл
		Элементы.ПолеРедакцияФормы.СписокВыбора.Добавить(СтрокаТаблицы.РедакцияФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СрокПредставленияОтчетности(Форма)
	
	ДатаСрокаПредставления = Дата(Год(Форма.мДатаКонцаПериодаОтчета) + 1, 4, 30);
	
	Возврат СтрШаблон(НСтр("ru = 'Не позднее %1'"), Формат(ДатаСрокаПредставления, "ДЛФ=DD"));
	
КонецФункции

&НаСервереБезКонтекста
Функция КтоСдаетОтчет()
	
	Возврат НСтр("ru = 'Физические лица, являющиеся налоговыми резидентами РФ, а также физические лица, не являющиеся налоговыми резидентами, но получающие доходы от источников в РФ.'");
	
КонецФункции

&НаКлиенте
Процедура Заполнить(БезПомощника = Ложь)
	
	Если мСкопированаФорма <> Неопределено Тогда
		// ДЕНЬГИ
		//	Если мВыбраннаяФорма <> мСкопированаФорма Тогда
		Если Не ДопустимоКопированиеОтчета(мСкопированаФорма, мВыбраннаяФорма) Тогда
		// Конец ДЕНЬГИ
			ПоказатьПредупреждение(, НСтр("ru = 'Форма отчета изменилась, копирование невозможно!'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
	Если Не ЗначениеЗаполнено(мСкопированаФорма)
		И Не БезПомощника
		И ОтчетМожноЗаполнитьВПомощнике(мВыбраннаяФорма) Тогда
		
		ПараметрыПомощника = Новый Структура;
		ПараметрыПомощника.Вставить("Организация", Налогоплательщик);
		ПараметрыПомощника.Вставить("Период", мДатаНачалаПериодаОтчета);
		ПараметрыПомощника.Вставить("КонтекстныйВызов", Истина);
		ПараметрыПомощника.Вставить("СоздатьПриОткрытии", Истина);
		ПараметрыПомощника.Вставить("ВыбраннаяФорма", мВыбраннаяФорма);
		
		// ДЕНЬГИ
		// для совместимости со старыми версиями
		ПараметрыПомощника.Вставить("мДатаНачалаПериодаОтчета", мДатаНачалаПериодаОтчета);
		ПараметрыПомощника.Вставить("мСохраненныйДок",          мСохраненныйДок);
		ПараметрыПомощника.Вставить("мСкопированаФорма",        мСкопированаФорма);
		ПараметрыПомощника.Вставить("мДатаКонцаПериодаОтчета",  мДатаКонцаПериодаОтчета);
		ПараметрыПомощника.Вставить("мПериодичность",           мПериодичность);
		ПараметрыПомощника.Вставить("мВыбраннаяФорма",          мВыбраннаяФорма);
		ПараметрыПомощника.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417", РегламентированнаяОтчетностьКлиент.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417());
		
		//ИмяФормыПомощника = "Обработка.ПомощникЗаполнения3НДФЛ.Форма";
		//
		//ОткрытьФорму(ИмяФормыПомощника, ПараметрыПомощника);
		
		ПараметрыПомощника.Вставить("ИспользоватьПомощникЗаполнения", Истина);
		ОткрытьФорму("Отчет.РегламентированныйОтчет3НДФЛ.Форма", ПараметрыПомощника);
		// Конец ДЕНЬГИ
		
	Иначе
		
		ПараметрыФормы = РегламентированнаяОтчетностьКлиент.НовыеПараметрыФормы();
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, ЭтотОбъект);
		// ДЕНЬГИ
		//ОткрытьФорму(СтрЗаменить(ИмяФормы, "ОсновнаяФорма", мВыбраннаяФорма), ПараметрыФормы, , Истина);
		ПараметрыФормы.Вставить("ИспользоватьПомощникЗаполнения", Истина);
		ОткрытьФорму("Отчет.РегламентированныйОтчет3НДФЛ.Форма", ПараметрыФормы);
		// Конец ДЕНЬГИ
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтчетМожноЗаполнитьВПомощнике(Знач ВыбраннаяФорма = Неопределено)
	
	Если Метаданные.Обработки.Найти("ПомощникЗаполнения3НДФЛ") <> Неопределено Тогда
		МенеджерПомощника = ОбщегоНазначения.ОбщийМодуль("Обработки.ПомощникЗаполнения3НДФЛ");
		ФормаЗаполняетсяПомощником = Не ЗначениеЗаполнено(ВыбраннаяФорма) Или МенеджерПомощника.ФормаЗаполняетсяПомощником(ВыбраннаяФорма);
	Иначе
		ФормаЗаполняетсяПомощником = Ложь;
	КонецЕсли;
	
	Возврат ФормаЗаполняетсяПомощником;
	
КонецФункции



#КонецОбласти