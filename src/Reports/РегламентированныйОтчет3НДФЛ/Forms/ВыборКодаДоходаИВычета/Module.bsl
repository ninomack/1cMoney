
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбраннаяГруппа = "-";
	ИмяГруппы = "";
	Если Параметры.Свойство("ПараметрыОтбора", ПараметрыОтбора) И ПараметрыОтбора <> Неопределено Тогда
		ПараметрыОтбора.Свойство("ВыбраннаяГруппа", ИмяГруппы)
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ТаблицаКодов) = Тип("Строка") Тогда
		ТаблицаПараметра = ПолучитьИзВременногоХранилища(Параметры.ТаблицаКодов);
	Иначе
		ТаблицаПараметра = Параметры.ТаблицаКодов;
	КонецЕсли;
	
	ВидКодов = Параметры.ВидКодов;
	Если ВидКодов = "КодыДоходов" Тогда
		КодыДоходов.Загрузить(ТаблицаПараметра);
		ОбновитьТаблицуГрупп(ТаблицаПараметра);
		Заголовок = НСтр("ru='Выбор кода дохода'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()) 
	Иначе
		КодыВычетов.Загрузить(ТаблицаПараметра);
		Элементы.ТаблицаГрупп.Видимость = Ложь;
		Заголовок = НСтр("ru='Выбор кода вычета'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()) 
	КонецЕсли;
	
	ОбновитьТаблицуКодов(ИмяГруппы, Ложь);
	Элементы.ГруппаСтрокаДляПоиска.Видимость = НЕ ПустаяСтрока(СтрокаДляПоиска) ИЛИ ТаблицаКодов.Количество() > 5;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаГруппПриАктивизацииСтроки(Элемент)
	
	ДанныеСтроки = Элементы.ТаблицаГрупп.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено ИЛИ ДанныеСтроки.Группа = "<Все группы>" Тогда
		НоваяГруппа = "";
	Иначе
		НоваяГруппа = ДанныеСтроки.Группа;
	КонецЕсли;
	
	ОбновитьТаблицуКодов(НоваяГруппа, Ложь);
	
КонецПроцедуры
 
&НаКлиенте
Процедура СтрокаДляПоискаПриИзменении(Элемент)
	ОбновитьТаблицуКодов(ВыбраннаяГруппа, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляПоискаОчистка(Элемент, СтандартнаяОбработка)
	ОбновитьТаблицуКодов(ВыбраннаяГруппа, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКодовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВидКодов = "КодыДоходов" Тогда
		Результат = Новый Структура("ЗначениеВыбрано,Код,Наименование,Группа,КодыВычетов,ВычетПоУмолчанию,НормаВычетаВПроцентах,НормаВычетаСумма,СтавкаНалога", Ложь);
		ТаблицаПоиска = КодыДоходов;
	Иначе
		Результат = Новый Структура("ЗначениеВыбрано,Код,Наименование,Группа,СпособРасчета,РазмерВычета,ПределДохода,ТолькоДляДохода", Ложь);
		ТаблицаПоиска = КодыВычетов;
	КонецЕсли;
	 
	СтрокаТаблицы = ТаблицаКодов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если СтрокаТаблицы <> Неопределено Тогда
		
		СтрокиКодов = ТаблицаПоиска.НайтиСтроки(Новый Структура("Код", СтрокаТаблицы.Код));
		Если СтрокиКодов.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат, СтрокиКодов[0]);
			Результат.ЗначениеВыбрано = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуГрупп(Таблица)

	Если Таблица = Неопределено Тогда
		Таблица = КодыДоходов.Выгрузить();
	КонецЕсли;
	
	КопияТаблицы = Таблица.Скопировать(, "Группа");
	КопияТаблицы.Свернуть("Группа");
	
	ТаблицаГрупп.Загрузить(КопияТаблицы);
	НоваяСтрока = ТаблицаГрупп.Вставить(0);
	НоваяСтрока.Группа = "<Все группы>";

КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуКодов(ИмяГруппы, ПоСтрокеПоиска = Ложь)

	Если ИмяГруппы = ВыбраннаяГруппа И НЕ ПоСтрокеПоиска Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидКодов = "КодыДоходов" Тогда
		ТаблицаПоиска = КодыДоходов.Выгрузить();
	Иначе
		ТаблицаПоиска = КодыВычетов.Выгрузить();
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыОтбора) = Тип("Структура") Тогда
		СтрокиТаблицы = ТаблицаПоиска.НайтиСтроки(ПараметрыОтбора);
	Иначе
		СтрокиТаблицы = ТаблицаПоиска;
	КонецЕсли;
	 
	КоличествоСлов = 0;
	Если НЕ ПустаяСтрока(СтрокаДляПоиска) Тогда
		МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(СтрокаДляПоиска, " ");
		КоличествоСлов = МассивСлов.Количество();
	КонецЕсли;
	
	ТаблицаКодов.Очистить();
	
	Для каждого НайденаяСтрока Из СтрокиТаблицы Цикл
		
		// Проверяем группу
		Если НЕ ПустаяСтрока(ИмяГруппы) И ИмяГруппы <> НайденаяСтрока.Группа Тогда
			Продолжить;
		КонецЕсли;
		 
		
		// Проверяем строку поиска
		Если КоличествоСлов > 0 Тогда
			
			КоличествоСовпадений = 0;
			Для каждого Слово Из МассивСлов Цикл
				Если Найти(Врег(НайденаяСтрока.Наименование), Врег(Слово)) > 0 Или Найти(Врег(НайденаяСтрока.Код), Врег(Слово)) > 0 Тогда
					КоличествоСовпадений = КоличествоСовпадений + 1
				КонецЕсли;
			КонецЦикла;
			
			Если КоличествоСовпадений < КоличествоСлов Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрока = ТаблицаКодов.Добавить();
		НоваяСтрока.Код = НайденаяСтрока.Код;
		НоваяСтрока.Наименование = НайденаяСтрока.Наименование;
		
	КонецЦикла;
	
	ВыбраннаяГруппа = ИмяГруппы;

КонецПроцедуры


#КонецОбласти