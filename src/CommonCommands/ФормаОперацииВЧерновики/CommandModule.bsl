
// "В черновики" записывает документ непроведенным и закрывает его форму
//	Перед изменением пометки на удаление запрашивается подтверждение пользователя
//
//Параметры: штатные параметры обработчика команды
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Форма  = ПараметрыВыполненияКоманды.Источник;
	Объект = Форма.Объект;
	
	Если Объект.ПометкаУдаления Тогда
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаКомандыПриПометкеУдаленияЗавершение", ЭтотОбъект, ПараметрыВыполненияКоманды);
		Если Объект.ЭтоШаблон Тогда
			ТекстВопроса = НСтр("ru='Снять пометку на удаление с этого шаблона?'");
		Иначе
			ТекстВопроса = НСтр("ru='Снять пометку на удаление с этой операции?'");
		КонецЕсли;
		 
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Снять пометку на удаление'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отмена'"));
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , , Форма.Заголовок);
		
	Иначе
		
		ЗаписатьОперациюКлиент(Форма, Объект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыПриПометкеУдаленияЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Форма  = ДополнительныеПараметры.Источник;
		Объект = Форма.Объект;
		
		ЗакрыватьФорму = Не Объект.ПометкаУдаления;
		Если Объект.ПометкаУдаления Тогда
			Объект.ПометкаУдаления = Ложь;
		КонецЕсли;
		
		ЗаписатьОперациюКлиент(Форма, Объект, ЗакрыватьФорму);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОперациюКлиент(Форма, Объект, ЗакрыватьФорму = Истина)

	РежимЗаписи = ?(Объект.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись);
	ПараметрыЗаписи = Новый Структура("РежимЗаписи,ЗакрыватьФорму,ИмяКоманды", РежимЗаписи, Не Объект.ПометкаУдаления, "СохранитьКакЧерновик");
	
	Если Форма.Записать(ПараметрыЗаписи) И Форма.Открыта() Тогда
		Если ЗакрыватьФорму Тогда
			Форма.Закрыть(Объект.Ссылка);
		Иначе
			РаботаСФормамиДокументовКлиентСервер.ОбновитьЭлементыФормыПоСостояниюОперации(Форма);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

