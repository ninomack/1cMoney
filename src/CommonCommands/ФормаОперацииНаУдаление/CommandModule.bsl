
// "К удалению" помечает документ на удаление и закрывает его форму
//	Перед изменением пометки на удаление запрашивается подтверждение пользователя
//
//Параметры: штатные параметры обработчика команды
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Форма  = ПараметрыВыполненияКоманды.Источник;
	Объект = Форма.Объект;
	
	Кнопки = Новый СписокЗначений;
	Если Объект.ПометкаУдаления Тогда
		
		Если Объект.ЭтоШаблон Тогда
			ТекстВопроса = НСтр("ru='Снять пометку на удаление с этого шаблона?'");
		Иначе
			ТекстВопроса = НСтр("ru='Снять пометку на удаление с этой операции?'");
		КонецЕсли;
		 
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Снять пометку на удаление'"));
		
	Иначе
		
		Если Объект.ЭтоШаблон Тогда
			ТекстВопроса = НСтр("ru='Пометить на удаление этот шаблон?'");
		Иначе
			ТекстВопроса = НСтр("ru='Пометить на удаление эту операцию?'");
		КонецЕсли;
		 
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Пометить на удаление'"));
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаКомандыПриПометкеУдаленияЗавершение", ЭтотОбъект, ПараметрыВыполненияКоманды);
	
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Отмена'"));
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , , Форма.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыПриПометкеУдаленияЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Форма  = ДополнительныеПараметры.Источник;
		Объект = Форма.Объект;
		
		Объект.ПометкаУдаления = Не Объект.ПометкаУдаления;
		
		РежимЗаписи = ?(Объект.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись);
	ПараметрыЗаписи = Новый Структура("РежимЗаписи,ЗакрыватьФорму", РежимЗаписи, Истина);
		
		Если Форма.Записать(ПараметрыЗаписи) И Форма.Открыта() Тогда
			Форма.Закрыть();
		КонецЕсли;
		
		ОповеститьОбИзменении(Объект.Ссылка);
		
	КонецЕсли;

КонецПроцедуры

