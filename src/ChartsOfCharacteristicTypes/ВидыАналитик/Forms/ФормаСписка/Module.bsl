#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДопНастройкиФормы = Новый Структура;
	ДопНастройкиФормы.Вставить("СписокТиповИФорм", СписокТиповИФорм());
	
	УправлениеФормой();

КонецПроцедуры
 

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(Элементы.Список.ТекущиеДанные.Ссылка) Тогда
		Оповестить("Записан вид характеристик", Новый Структура("ИмяПланаВидовХарактеристик,Ссылка,ВладелецФормы", "ВидыАналитик", 
					Элементы.Список.ТекущиеДанные.Ссылка, 
					?(ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения"), ВладелецФормы.УникальныйИдентификатор, Неопределено)));
	КонецЕсли; 
	
	УправлениеФормой();
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура ВыключитьИспользованиеАналитики(Команда)
	
	ТекстВопроса = НСтр("ru = 'Отключить использование аналитики в программе?'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	ОбработчикОтвета = Новый ОписаниеОповещения("ВыключитьИспользованиеАналитикиЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНастройкиВидимостиАналитикПоСтатьям(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "Основной");
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ПользовательскиеНастройкиПоУмолчанию");
	ПараметрыФормы.Вставить("ВывестиРазделОсновныеНастройки", Ложь);
	ПараметрыФормы.Вставить("УровеньСверткиСтрокПриФормировании", 1);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.НастройкиАналитикиСтатей.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияАналитики(Команда)
	
	ВыбраннаяАналитика = Элементы.Список.ТекущиеДанные;
	Если ВыбраннаяАналитика = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементСпискаТиповИФорм = Неопределено;
	Для Каждого ТипВидаАналитики Из ВыбраннаяАналитика.ТипЗначения.Типы() Цикл
		
		ЭлементСпискаТиповИФорм  = ДопНастройкиФормы.СписокТиповИФорм.НайтиПоЗначению(ТипВидаАналитики);
		Если ЭлементСпискаТиповИФорм <> Неопределено Тогда
			Прервать;
		КонецЕсли; 
		
	КонецЦикла; 
	
	Если ЭлементСпискаТиповИФорм = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для выделенной аналитики не предусмотрен список значений'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()) );
		Возврат;
	КонецЕсли; 
	
	ПараметрыФормыСписка = Новый Структура;
	ПараметрыФормыСписка.Вставить("Отбор", Новый Структура("Владелец", ВыбраннаяАналитика.Ссылка));
	ОткрытьФорму(ЭлементСпискаТиповИФорм.Представление, ПараметрыФормыСписка);
	
КонецПроцедуры
 
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыключитьИспользованиеАналитикиЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыключитьИспользованиеАналитикиСервер();
		ПоказатьПредупреждение(, НСтр("ru = 'Ни одна аналитика больше не используется.
			|Для включения аналитики установите флаг ""Использовать для статей дохода"" или ""Использовать для статей расхода"" хотя бы в одном виде аналитики'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	КонецЕсли;

КонецПроцедуры

&НаСервере 
Процедура ВыключитьИспользованиеАналитикиСервер()

	АналитикаСтатей.ВыключитьАктуальностьАналитик();
	Элементы.ДекорацияПояснение.Видимость = Истина;
	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()

	Элементы.ДекорацияПояснение.Видимость = НЕ ПолучитьФункциональнуюОпцию("ИспользоватьАналитикуСтатей");

КонецПроцедуры

&НаСервере
Функция СписокТиповИФорм()

	СписокТипов = Новый СписокЗначений;
	ТипДопЗначений = Тип("СправочникСсылка.ДополнительныеЗначенияВидовАналитик");
	
	Для Каждого ТипИзНабора Из Метаданные.ПланыВидовХарактеристик.ВидыАналитик.Тип.Типы() Цикл
		МетаданныеТипа = Метаданные.НайтиПоТипу(ТипИзНабора);
		Если МетаданныеТипа <> Неопределено И Метаданные.Справочники.Содержит(МетаданныеТипа) Тогда
			СписокТипов.Добавить(ТипИзНабора, МетаданныеТипа.ПолноеИмя() + ".ФормаСписка",
				ТипИзНабора = ТипДопЗначений);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат СписокТипов;

КонецФункции
 

#КонецОбласти







