////////////////////////////////////////////////////////////////////////////////
//ОбщаяФорма.ВыборСтандартногоПериода
//  Используется для выбора относительного или абсолютного периода
//  Из всего набора вариантов стандартного периода использует только самые распространенные в обиходе
//  
//Параметры формы:
//  ОповеститьОВыборе - Булево
//  Период - СтандартныйПериод - текущее значение периода
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОповеститьОВыборе = Параметры.ОповеститьОВыборе;
	ЦветКнопки = ЦветаСтиля.ФонВыделеннойКнопки;
	
	РезультатВыбора = Параметры.Период;
	Если НЕ ЗначениеЗаполнено(РезультатВыбора.ДатаНачала) И НЕ ЗначениеЗаполнено(РезультатВыбора.ДатаОкончания) Тогда
		РезультатВыбора = Новый СтандартныйПериод(ВариантСтандартногоПериода.ЭтотМесяц);
	КонецЕсли;
	
	НомерМесяца = Месяц(РезультатВыбора.ДатаНачала);
	НомерГода   = Год(РезультатВыбора.ДатаНачала);
	
	// Добавим в список кнопки, у которых может измениться цвет фона
	СписокКнопок = Новый Структура;
	Для каждого Команда Из Команды Цикл
		Если Найти("Ок,", Команда.Имя + ",") = 0 Тогда
			СписокКнопок.Вставить(Команда.Имя);
		КонецЕсли;
	КонецЦикла;
	СписокКнопок.Вставить("ГруппаМесяцГод");
	СписокКнопок.Вставить("ГруппаПроизвольныйДень");
	СписокКнопок.Вставить("ГруппаПроизвольныйГод");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПрочитатьПериод();
	ПереключитьСтраницу(Ложь);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПериодаПриИзменении(Элемент)
	
	ПереключитьСтраницу();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыбораДатаНачала1ПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(РезультатВыбора.ДатаНачала) Тогда
		РезультатВыбора.ДатаНачала = Мин(НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()), НачалоДня(РезультатВыбора.ДатаОкончания));
	КонецЕсли;
	
	ПриИзмененииДатыНачала();

КонецПроцедуры

&НаКлиенте
Процедура НомерГодаПриИзменении(Элемент)
	
	Если НомерГода < 2000 Тогда
		НомерГода = 2000;
	КонецЕсли;
	
	Если НомерГода > 2999 Тогда
		НомерГода = 2999;
	КонецЕсли;
	
	НомерДня    = ?(ТипПериода = 4, 1, День(РезультатВыбора.ДатаНачала));
	НомерМесяца = ?(ТипПериода = 4, 1, Месяц(РезультатВыбора.ДатаНачала));
	РезультатВыбора.ДатаНачала = Дата(НомерГода, НомерМесяца, НомерДня);
	ПриИзмененииДатыНачала();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерГода1ПриИзменении(Элемент)
	
	Если НомерГода < 2000 Тогда
		НомерГода = 2000;
	КонецЕсли;
	
	Если НомерГода > 2999 Тогда
		НомерГода = 2999;
	КонецЕсли;
	
	РезультатВыбора.ДатаНачала = Дата(НомерГода, НомерМесяца, 1);
	ПриИзмененииДатыНачала();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерМесяцаПриИзменении(Элемент)
	
	Если НомерМесяца < 1 Тогда
		НомерМесяца = 1;
	КонецЕсли;
	
	Если НомерМесяца > 12 Тогда
		НомерМесяца = 12;
	КонецЕсли;
	
	РезультатВыбора.ДатаНачала = Дата(НомерГода, НомерМесяца, 1);
	ПриИзмененииДатыНачала();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыбораДатаНачалаПриИзменении(Элемент)
	ПриИзмененииДатыНачала();
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыбораДатаОкончанияПриИзменении(Элемент)
	ПриИзмененииДатыОкончания();
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сегодня(Команда)
	
	РезультатВыбора.Вариант = ВариантСтандартногоПериода[Команда.Имя];
	Если ОповеститьОВыборе Тогда
		ОповеститьОВыборе(РезультатВыбора);
	Иначе
		Закрыть(РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	
	Если ОповеститьОВыборе Тогда
		ОповеститьОВыборе(РезультатВыбора);
	Иначе
		Закрыть(РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Определяет вид периода, переданного в параметрах
&НаКлиенте
Процедура ПрочитатьПериод()

	Если НачалоДня(РезультатВыбора.ДатаНачала) = НачалоДня(РезультатВыбора.ДатаОкончания) Тогда
		
		ТипПериода      = 1;
		Если РезультатВыбора.Вариант = ВариантСтандартногоПериода.Вчера Тогда
			Элементы.Вчера.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.Сегодня Тогда
			Элементы.Сегодня.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.Завтра Тогда
			Элементы.Завтра.ЦветФона = ЦветКнопки;
		Иначе
			Элементы.ГруппаПроизвольныйДень.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	ИначеЕсли НачалоНедели(РезультатВыбора.ДатаНачала) = НачалоНедели(РезультатВыбора.ДатаОкончания)
		Или РезультатВыбора.Вариант = ВариантСтандартногоПериода.Последние7Дней Тогда
		
		ТипПериода      = 2;
		Если РезультатВыбора.Вариант = ВариантСтандартногоПериода.ПрошлаяНеделя Тогда
			Элементы.ПрошлаяНеделя.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.Последние7Дней Тогда
			Элементы.Последние7Дней.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.ЭтаНеделя Тогда
			Элементы.ЭтаНеделя.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.СледующаяНеделя Тогда
			Элементы.СледующаяНеделя.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	ИначеЕсли НачалоМесяца(РезультатВыбора.ДатаНачала) = НачалоМесяца(РезультатВыбора.ДатаОкончания)
		Или РезультатВыбора.Вариант = ВариантСтандартногоПериода.Месяц Тогда
		
		ТипПериода      = 3;
		Если РезультатВыбора.Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц Тогда
			Элементы.ПрошлыйМесяц.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.Месяц Тогда
			Элементы.Месяц.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.ЭтотМесяц Тогда
			Элементы.ЭтотМесяц.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.СледующийМесяц Тогда
			Элементы.СледующийМесяц.ЦветФона = ЦветКнопки;
		Иначе
			Элементы.ГруппаМесяцГод.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	ИначеЕсли НачалоДня(РезультатВыбора.ДатаНачала) = НачалоГода(РезультатВыбора.ДатаНачала) 
		И КонецДня(РезультатВыбора.ДатаОкончания) = КонецГода(РезультатВыбора.ДатаОкончания) Тогда
		
		ТипПериода      = 4;
		Если РезультатВыбора.Вариант = ВариантСтандартногоПериода.ПрошлыйГод Тогда
			Элементы.ПрошлыйГод.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.ЭтотГод Тогда
			Элементы.ЭтотГод.ЦветФона = ЦветКнопки;
		ИначеЕсли РезультатВыбора.Вариант = ВариантСтандартногоПериода.СледующийГод Тогда
			Элементы.СледующийГод.ЦветФона = ЦветКнопки;
		Иначе
			Элементы.ГруппаПроизвольныйГод.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	Иначе
		
		ТипПериода      = 0;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВыделениеКнопок()

	ОчисититьФонКнопок();
	
	Если ТипПериода = 1 Тогда
		
		Если Элементы.ГруппаПроизвольныйДень.ЦветФона <> ЦветКнопки Тогда
			Элементы.ГруппаПроизвольныйДень.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	ИначеЕсли ТипПериода = 3 Тогда
		
		Если Элементы.ГруппаМесяцГод.ЦветФона <> ЦветКнопки Тогда
			Элементы.ГруппаМесяцГод.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	ИначеЕсли ТипПериода = 4 Тогда
		
		Если Элементы.ГруппаПроизвольныйГод.ЦветФона <> ЦветКнопки Тогда
			Элементы.ГруппаПроизвольныйГод.ЦветФона = ЦветКнопки;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницу(ИзменятьВыделениеВыбора = Истина)

	Если ТипПериода = 1 Тогда
		
		Элементы.ГруппаСтраницыВариантов.ТекущаяСтраница = Элементы.ГруппаСтраницаДень;
		
	ИначеЕсли ТипПериода = 2 Тогда
		
		Элементы.ГруппаСтраницыВариантов.ТекущаяСтраница = Элементы.ГруппаСтраницаНеделя;
		
	ИначеЕсли ТипПериода = 3 Тогда
		
		Элементы.ГруппаСтраницыВариантов.ТекущаяСтраница = Элементы.ГруппаСтраницаМесяц;
		
	ИначеЕсли ТипПериода = 4 Тогда
		
		Элементы.ГруппаСтраницыВариантов.ТекущаяСтраница = Элементы.ГруппаСтраницаГод;
		
	ИначеЕсли ТипПериода = 0 Тогда
		
		Элементы.ГруппаСтраницыВариантов.ТекущаяСтраница = Элементы.ГруппаСтраницаПроизвольно;
		
	КонецЕсли;

	Если ИзменятьВыделениеВыбора Тогда
		ПриИзмененииДатыНачала();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисититьФонКнопок()

	АвтоФон = Новый Цвет();
	Для каждого КлючИЗначение Из СписокКнопок Цикл
		Если Элементы[КлючИЗначение.Ключ].ЦветФона <> АвтоФон Тогда
			Элементы[КлючИЗначение.Ключ].ЦветФона = АвтоФон;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДатыНачала()

	РезультатВыбора.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	
	Если ТипПериода = 1 Тогда
		РезультатВыбора.ДатаНачала      = НачалоДня(РезультатВыбора.ДатаНачала);
		РезультатВыбора.ДатаОкончания   = КонецДня(РезультатВыбора.ДатаНачала);
	ИначеЕсли ТипПериода = 2 Тогда
		РезультатВыбора.ДатаНачала      = НачалоНедели(РезультатВыбора.ДатаНачала);
		РезультатВыбора.ДатаОкончания   = КонецНедели(РезультатВыбора.ДатаНачала);
	ИначеЕсли ТипПериода = 3 Тогда
		РезультатВыбора.ДатаНачала      = НачалоМесяца(РезультатВыбора.ДатаНачала);
		РезультатВыбора.ДатаОкончания   = КонецМесяца(РезультатВыбора.ДатаНачала);
	ИначеЕсли ТипПериода = 4 Тогда
		РезультатВыбора.ДатаНачала      = НачалоГода(РезультатВыбора.ДатаНачала);
		РезультатВыбора.ДатаОкончания   = КонецГода(РезультатВыбора.ДатаНачала);
	Иначе
		Если РезультатВыбора.ДатаОкончания <= РезультатВыбора.ДатаНачала Тогда
			РезультатВыбора.ДатаОкончания   = КонецДня(РезультатВыбора.ДатаНачала);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьВыделениеКнопок();

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДатыОкончания()

	РезультатВыбора.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	РезультатВыбора.ДатаОкончания = КонецДня(РезультатВыбора.ДатаОкончания);
	
	Если ТипПериода = 1 Тогда
		РезультатВыбора.ДатаОкончания   = КонецДня(РезультатВыбора.ДатаОкончания);
		РезультатВыбора.ДатаНачала      = НачалоДня(РезультатВыбора.ДатаОкончания);
	ИначеЕсли ТипПериода = 2 Тогда
		РезультатВыбора.ДатаОкончания   = КонецНедели(РезультатВыбора.ДатаОкончания);
		РезультатВыбора.ДатаНачала      = НачалоНедели(РезультатВыбора.ДатаОкончания);
	ИначеЕсли ТипПериода = 3 Тогда
		РезультатВыбора.ДатаОкончания   = КонецМесяца(РезультатВыбора.ДатаОкончания);
		РезультатВыбора.ДатаНачала      = НачалоМесяца(РезультатВыбора.ДатаОкончания);
	ИначеЕсли ТипПериода = 4 Тогда
		РезультатВыбора.ДатаОкончания   = КонецГода(РезультатВыбора.ДатаОкончания);
		РезультатВыбора.ДатаНачала      = НачалоГода(РезультатВыбора.ДатаОкончания);
	Иначе
		Если РезультатВыбора.ДатаОкончания <= РезультатВыбора.ДатаНачала Тогда
			РезультатВыбора.ДатаНачала   = НачалоДня(РезультатВыбора.ДатаОкончания);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьВыделениеКнопок();

КонецПроцедуры

#КонецОбласти


