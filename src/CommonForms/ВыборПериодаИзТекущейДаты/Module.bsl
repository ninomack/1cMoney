////////////////////////////////////////////////////////////////////////////////
//ОбщаяФорма.ВыборПериодаИзТекущейДаты
//  Предназначена для выбора варианта относительного периода до или от текущей даты
//  
//Параметры формы:
//  ВариантБюджета - СправочникСсылка.ВариантыБюджета
//  ВидПериода - Строка - имена стандартных периодов + период бюджета
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
//  ДопустимыеВидыПериода - СписокЗначений - список вариантов, которые допускаются в данный момент
//					Если не заполнено, разрешены все варианты
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СтрокаИменПараметров());
	Если ЗначениеЗаполнено(Параметры.ДопустимыеВидыПериода) Тогда
		ЗаполнитьСписокВыбораВидаПериода(Элементы.ВидПериода.СписокВыбора, Параметры.ДопустимыеВидыПериода);
	Иначе
		ЗаполнитьСписокВыбораВидаПериода(Элементы.ВидПериода.СписокВыбора, ДеньгиКлиентСервер.СписокВариантовПеродаИзТекущейДаты(0, Истина));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантБюджета) Тогда
		ВариантБюджета = Элементы.ВидПериода.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантБюджета) Тогда
		ВариантБюджета = Константы.ОсновнойВариантБюджета.Получить();
	КонецЕсли;
	Если  Не ЗначениеЗаполнено(ДатаНачалаБюджетногоПериода) 
		Или Не ЗначениеЗаполнено(ДатаОкончанияБюджетногоПериода) Тогда
		ПериодПланирования = БюджетированиеСервер.НоваяСтрукрураБюджетногоПериода(ВариантБюджета, ТекущаяДатаСеанса());
		ДатаНачалаБюджетногоПериода    = ПериодПланирования.Начало;
		ДатаОкончанияБюджетногоПериода = ПериодПланирования.Окончание;
	КонецЕсли;
	
	ОбновитьЗначенияДат(ЭтотОбъект);
	ОбновитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы



#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицы

&НаКлиенте
Процедура ВидПериодаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ВариантБюджета) Тогда
		ВариантБюджета = Элементы.ВидПериода.СписокВыбора[0].Значение;
	КонецЕсли;
	
	ОбновитьЗначенияДат(ЭтотОбъект);
	ОбновитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если ДатаНачала > ДатаОкончания Тогда
		ДатаНачала = НачалоДня(ДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	Если ДатаНачала > ДатаОкончания Тогда
		ДатаОкончания = КонецДня(ДатаНачала);
	КонецЕсли;
	
КонецПроцедуры
 


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура(СтрокаИменПараметров());
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Результат.ДатаОкончания = КонецДня(Результат.ДатаОкончания);
	Результат.ДатаОкончанияБюджетногоПериода = КонецДня(Результат.ДатаОкончанияБюджетногоПериода);
	
	Закрыть(Результат);
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаИменПараметров()

	Возврат("ВариантБюджета,ВидПериода,ДатаНачала,ДатаОкончания,ДатаНачалаБюджетногоПериода,ДатаОкончанияБюджетногоПериода");

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораВидаПериода(СписокВыбора, СписокВариантов)

	СписокВыбора.Очистить();
	Для каждого ЭлементСписка Из СписокВариантов Цикл
		СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗначенияДат(Форма)

	ДеньгиКлиентСервер.ОбновитьДатыПоВидуПериода(Форма.ВидПериода, Форма.ДатаНачала, Форма.ДатаОкончания,
				Форма.ВариантБюджета, Форма.ДатаНачалаБюджетногоПериода, Форма.ДатаОкончанияБюджетногоПериода);
				
	Если Форма.ВидПериода = "НачалоБюджетногоПериода" Или Форма.ВидПериода = "КонецБюджетногоПериода" Тогда
		ТекстЗаголовокаБюджета = НСтр("ru='Вариант бюджета: %1'");
		ТекстЗаголовокаБюджета = СтрШаблон(ТекстЗаголовокаБюджета, Форма.ВариантБюджета);
	Иначе
		ТекстЗаголовокаБюджета = "";
	КонецЕсли;
	Форма.Элементы.ПредставлениеВариантаБюджета.Заголовок = ТекстЗаголовокаБюджета;

КонецПроцедуры
 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭлементыФормы(Форма)

	Форма.Элементы.ДатаНачала.Доступность    = Форма.ВидПериода = "НачалоПроизвольнойДаты";
	Форма.Элементы.ДатаОкончания.Доступность = Форма.ВидПериода = "КонецПроизвольнойДаты";
	
КонецПроцедуры


#КонецОбласти
