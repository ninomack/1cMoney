#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "ТекстПояснения, ТекстСообщения");
	Элементы.ТекстПояснения.Видимость = ЗначениеЗаполнено(ТекстПояснения);
	
	Если Не ПустаяСтрока(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	ШиринаЗаголовка = 1.3 * СтрДлина(Заголовок);
	Если ШиринаЗаголовка > 40 И ШиринаЗаголовка < 80 Тогда
		Ширина = ШиринаЗаголовка;
	КонецЕсли;
	
	Если ТекстСообщения = "" Тогда
		ТекстСообщения = Новый ФорматированнаяСтрока(НСтр("ru = 'Невозможно открыть форму'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	КонецЕсли; 
	
	СтрокТекста = Окр(СтрДлина(ТекстСообщения) / 40, 0, РежимОкругления.Окр15как10);
	Если СтрокТекста > 1 Тогда
		Элементы.ТекстСообщения.Высота = 2 + СтрокТекста;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ТекстПояснения) Тогда
		СтрокТекста = Окр(СтрДлина(ТекстПояснения) / 60, 0, РежимОкругления.Окр15как10);
		Если СтрокТекста > 1 Тогда
			Элементы.ТекстПояснения.Высота = 2 + СтрокТекста;
		КонецЕсли; 
	КонецЕсли; 
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("ОбщаяФорма.ОбъектБезИнтерактивногоДоступа", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры


#КонецОбласти

