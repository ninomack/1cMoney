#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода  = Параметры.НачалоПериода;
	КонецПериода   = Параметры.КонецПериода;
	ВыбиратьНеделю = Параметры.ВыбиратьНеделю;
	Если ЗначениеЗаполнено(НачалоПериода) Тогда
		День = НачалоПериода;
	Иначе
		День = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ВыбиратьНеделю Тогда
		
		День = НачалоНедели(День);
		Элементы.День.ВыделенныеДаты.Очистить();
		Для Счетчик = 1 По 7 Цикл
			Элементы.День.ВыделенныеДаты.Добавить(День + (Счетчик - 1) * 86400);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВыбиратьНеделю Тогда
		ПодключитьОбработчикОжидания("ВыделитьНеделю", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеньПриИзменении(Элемент)
	
	Если ВыбиратьНеделю Тогда
		ВыделитьНеделю();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ДеньПриИзменении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДеньПриИзменении()

	Если ВыбиратьНеделю Тогда
		РезультатВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоНедели(День), КонецНедели(День));
	Иначе
		РезультатВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоДня(День), КонецДня(День));
	КонецЕсли;
	 
	Закрыть(РезультатВыбора);

КонецПроцедуры

&НаКлиенте
Процедура ВыделитьНеделю()

	Если Элементы.День.РежимВыделения = РежимВыделенияДаты.Одиночный Тогда
		Элементы.День.РежимВыделения = РежимВыделенияДаты.Интервал;
	КонецЕсли;
	
	День = НачалоНедели(День);
	Элементы.День.ВыделенныеДаты.Очистить();
	Для Счетчик = 1 По 7 Цикл
		Элементы.День.ВыделенныеДаты.Добавить(День + (Счетчик - 1) * 86400);
	КонецЦикла;

КонецПроцедуры
 
#КонецОбласти