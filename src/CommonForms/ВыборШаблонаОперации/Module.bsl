////////////////////////////////////////////////////////////////////////////////
//ОбщаяФорма.ВыборШаблонаОперации
//  Позволяет выбрать шаблон операции определенного вида
//	Вызывается из РаботаСФормамиДокументовКлиент.ОткрытьФормуНовогоДокументаСУчетомШаблона(), где заполняется
//	список шаблонов
//  
//Параметры формы:
//  ИмяВидаДокумента - Строка - имя метаданных-документа
//  СписокПлановыхОпераций - Строка - адрес во временном хранилище со списком плановых операций и шаблонов
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяВидаДокумента = Параметры.ИмяВидаДокумента;
	Список.Загрузить(ПолучитьИзВременногоХранилища(Параметры.СписокПлановыхОпераций));
	
	УстановитьВидимостьЭлементовФормы();
	
	Элементы.Список.ТекущаяСтрока = Список[0].ПолучитьИдентификатор();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗавершитьВыбор(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьОперациюНаПросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)

	ВыбраннаяСтрока = Элементы.Список.ТекущиеДанные;
	Элементы.СписокКомандаВыбрать.Доступность = ВыбраннаяСтрока <> Неопределено;
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПосмотретьОперацию(Команда)
	
	ОткрытьОперациюНаПросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если НЕ ВыбраннаяСтрока = Неопределено Тогда
		ЗавершитьВыбор(ВыбраннаяСтрока);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()

	// В открываемом списке уберем лишние колонки, переименуем оставшиеся:
	Если ИмяВидаДокумента = "Доход" Тогда
		
		Элементы.СписокГруппаСуммаСписания.Видимость = Ложь;
		
	ИначеЕсли ИмяВидаДокумента = "Расход" Тогда
		
		Элементы.СписокСуммаПоступления.Заголовок = "Стоимость имущества";
		Элементы.СписокСуммаСписания.Заголовок    = "Сумма оплаты";
		
	ИначеЕсли ИмяВидаДокумента = "ВозвратИзНакоплений" Тогда
		
		Элементы.СписокСуммаПоступления.Заголовок = "Отложено в накопления";
		Элементы.СписокСуммаСписания.Заголовок    = "Возврат из накоплений";
		
	ИначеЕсли ИмяВидаДокумента = "МыВзялиВДолг" Тогда
		
		Элементы.СписокСуммаПоступления.Заголовок = "Сумма кредита";
		Элементы.СписокСуммаСписания.Заголовок    = "Дополнительные расходы";
		
	ИначеЕсли ИмяВидаДокумента = "МыДалиВДолг" Тогда
		
		Элементы.СписокГруппаСуммаПоступления.Видимость = Ложь;
		Элементы.СписокСуммаСписания.Заголовок    = "Выдано взаймы";
		
	ИначеЕсли ИмяВидаДокумента = "НамВернулиДолг" Тогда
		
		Элементы.СписокСуммаПоступления.Заголовок = "Сумма возврата";
		Элементы.СписокСуммаСписания.Заголовок    = "Дополнительные расходы";
		
	ИначеЕсли ИмяВидаДокумента = "МыВернулиДолг" Тогда
		
		Элементы.СписокГруппаСуммаПоступления.Видимость = Ложь;
		Элементы.СписокСуммаСписания.Заголовок    = "Сумма возврата";
		
	КонецЕсли;

КонецПроцедуры
 
&НаКлиенте
Процедура ЗавершитьВыбор(ИДСтрокиШаблона)

	СтрокаШаблона = Список.НайтиПоИдентификатору(ИДСтрокиШаблона);
	РезультатВыбора = Новый Структура();
	РезультатВыбора.Вставить("ШаблонОперации",     СтрокаШаблона.Ссылка);
	РезультатВыбора.Вставить("ПлановаяОперация",   СтрокаШаблона.ПлановаяОперация);
	РезультатВыбора.Вставить("ПлановаяДата",       СтрокаШаблона.ПлановаяДата);
	РезультатВыбора.Вставить("АктуальнаяДата",     СтрокаШаблона.АктуальнаяДата);
	
	Закрыть(РезультатВыбора);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОперациюНаПросмотр()

	СтрокаШаблона = Элементы.Список.ТекущиеДанные;
	Если СтрокаШаблона = Неопределено ИЛИ СтрокаШаблона.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ, ТолькоПросмотр", СтрокаШаблона.Ссылка, Истина);
	
	ОткрытьФорму("Документ." + ИмяВидаДокумента + ".ФормаОбъекта", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры


#КонецОбласти




