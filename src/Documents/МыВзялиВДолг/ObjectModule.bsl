#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		// Заполняем данными заполнения отмеченные в конфигураторе реквизиты
		ОбщегоНазначенияДеньги.ЗаполнитьОбъектПоСтруктуре(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли; 
	
	// Проверяем реквизиты нового документа в соответствии с настройками пользователя
	ОбслуживаниеДокументов.ПроверитьЗаполнениеНовогоДокумента(ЭтотОбъект);
	
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	
	// Заполняем незаполненные реквизиты значениями по умолчанию:
	Если НЕ ЗначениеЗаполнено(Кредитор) и ЗначениеЗаполнено(Кредит) Тогда
		Кредитор = Кредит.Контакт;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Кошелек) Тогда
		ВалютаПолучено = Кошелек.Валюта;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Кредит) Тогда
		ВалютаКредита = Кредит.Валюта;
	КонецЕсли; 
	
	ПараметрыГрафика = РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.СоздатьМенеджерЗаписи();
	ПараметрыГрафика.Долг = Кредит;
	ПараметрыГрафика.Прочитать();
	
	Если ЗначениеЗаполнено(Кредит) И Кредит.ИспользоватьДляУчетаВзятыхДолгов И НЕ Кредит.ИспользоватьДляУчетаВыданныхДолгов Тогда
		
		Если ЗначениеЗаполнено(ПараметрыГрафика.КошелекДляПогашения) И НЕ ЗначениеЗаполнено(Кошелек) Тогда
			Кошелек = ПараметрыГрафика.КошелекДляПогашения;
			ВалютаПолучено = Кошелек.Валюта;
		КонецЕсли; 
		
		Если ВалютаПолучено = ВалютаУчета Или  НЕ ЗначениеЗаполнено(ВалютаПолучено) Тогда
			КурсПолучено      = 1;
			КратностьПолучено = 1;
		Иначе
			КурсИКратность    = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаПолучено, Дата, ВалютаУчета);
			КурсПолучено      = КурсИКратность.Курс;
			КратностьПолучено = КурсИКратность.Кратность;
		КонецЕсли; 
		Если ВалютаКредита = ВалютаУчета Или  НЕ ЗначениеЗаполнено(ВалютаКредита) Тогда
			КурсКредита      = 1;
			КратностьКредита = 1;
		Иначе
			КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаКредита, Дата, ВалютаУчета);
			КурсКредита      = КурсИКратность.Курс;
			КратностьКредита = КурсИКратность.Кратность;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(ПараметрыГрафика.СуммаДолга) И НЕ ЗначениеЗаполнено(СуммаКредита) Тогда
			СуммаКредита = ПараметрыГрафика.СуммаДолга;
		КонецЕсли; 
		Если ЗначениеЗаполнено(СуммаКредита) И НЕ ЗначениеЗаполнено(СуммаПолучено) Тогда
			СуммаПолучено = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СуммаКредита,
				ВалютаКредита, ВалютаПолучено,
				КурсКредита, КурсПолучено,
				КратностьКредита, КратностьПолучено);
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// Общий функционал документов:
	ОбщегоНазначенияДеньги.ОбработкаСобытияПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если ЭтоШаблон Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Кошелек");
		МассивНепроверяемыхРеквизитов.Добавить("Кредит");
	КонецЕсли; 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	// Проверяем/заполняем итоговые и информационные реквизиты
	Документы.МыВзялиВДолг.ОбновитьИтоговыеСуммыДокумента(ЭтотОбъект);
	
	Если ЭтоШаблон Тогда
		Проведен = Ложь;
		Если РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли; 
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	// Заполняем дополнительные реквизиты информацией, необходимой для формирования записей в регистры
	Документы.МыВзялиВДолг.ПроверитьДополнительныеСвойстваОперации(ЭтотОбъект, ДополнительныеСвойства, Истина);
	ОбслуживаниеДокументов.ПриЗаписиОбъектаДокумента(ЭтотОбъект);
	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка ИЛИ ЭтоШаблон Тогда
		Возврат;
	КонецЕсли; 
	
	Документы.МыВзялиВДолг.СформироватьДвиженияДокумента(Ссылка, Движения, ДополнительныеСвойства);
	ПлановыеОперации.ПроверитьОборотыПлановойОоперацииПриПроведении(ЭтотОбъект);
	
	Движения.ЖурналОпераций.Записывать            = Истина;
	Движения.ФактическиеОборотыБюджета.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
