&НаКлиенте
Перем ДополнительныеПараметрыНаКлиенте Экспорт;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	#Область ПрикрепленныеФайлы
	МедиаФайлы = ПрикрепленныеФайлыСервер.ПрочитатьСписокФайлов(Объект.Ссылка);
	#КонецОбласти
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Записан элемент справочника" И ТипЗнч(Параметр) = Тип("Структура") Тогда
		
		ИмяСправочника = Неопределено;
		ФормаВладельца = Неопределено;
		Если Параметр.Свойство("ИмяСправочника", ИмяСправочника) И ВРЕГ(ИмяСправочника) = ВРЕГ("КошелькиИСчета")
			И Параметр.Свойство("ВладелецФормы", ФормаВладельца) И ЗначениеЗаполнено(ФормаВладельца)
			И ФормаВладельца = ЭтаФорма.УникальныйИдентификатор Тогда
			Объект.Кошелек = Параметр.Ссылка;
		КонецЕсли; 
		
	ИначеЕсли ИмяСобытия = "Записана операция" И ТипЗнч(Параметр) = Тип("Структура") Тогда
		
		ФормаВладельца = Неопределено;
		Если Параметр.Свойство("ВладелецФормы", ФормаВладельца) И ФормаВладельца = ЭтаФорма.УникальныйИдентификатор И Параметр.ЭтоШаблон <> Объект.ЭтоШаблон Тогда
			ЗаполнитьДанныеПоШаблону(Параметр.Ссылка);
		КонецЕсли;
	КонецЕсли;
		
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	#Область ПрикрепленныеФайлы
	Если (ПрикрепленныеФайлыКлиент.ЭтоПрикреплениеФайла(ЭтаФорма, ИмяСобытия, Источник))Тогда
		ПрикрепленныеФайлыКлиент.ДобавитьФайлВСписокФормы(ЭтотОбъект, Параметр); 
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Обслуживание плановых операций
	ПлановыеОперацииКлиент.ПередЗаписьюДокумента(ЭтаФорма, Отказ, ПараметрыЗаписи);
	// Общий функционал перед записью документов
	ОбслуживаниеДокументовКлиент.ПередЗаписью(ЭтаФорма, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обслуживание плановых операций
	Документы.НамВернулиДолг.ОбновитьИтоговыеСуммыДокумента(ТекущийОбъект);
	ПлановыеОперации.ПроверкаПлановПередЗаписьюДокумента(ТекущийОбъект, ПараметрыЗаписи, ЭтотОбъект);
	
	// Обслуживание аналитики статей:
	АналитикаСтатей.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	#Область ПрикрепленныеФайлы
	ПрикрепленныеФайлыСервер.ОбработатьИЗаписатьДанныеМультимедиа(МедиаФайлы, ТекущийОбъект.Ссылка);
	#КонецОбласти 
	
	ЗаполнитьДобавленныеКолонки();
	ПараметрыСеанса.ДатаПоследнейОперации = ТекущийОбъект.Дата;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
		Оповестить("Записана операция", Новый Структура("Дата, ВидДокумента, Ссылка, ЭтоШаблон, ВладелецФормы", 
			Объект.Дата, "НамВернулиДолг", Объект.Ссылка, Объект.ЭтоШаблон, 
			?(ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения"), ВладелецФормы.УникальныйИдентификатор, Неопределено)));
			
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Общий функционал перед закрытием форм документов
	ОбслуживаниеДокументовКлиент.ПередЗакрытием(ЭтаФорма, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И Открыта() Тогда
		Закрыть(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	РаботаСФормамиДокументовКлиентСервер.ОбновитьЗаголовкиКомандФормы(ЭтотОбъект);
	
	Если НачалоМесяца(Объект.Дата) = МесяцДокумента Тогда
		Возврат;
	КонецЕсли; 
	
	МесяцДокумента = НачалоМесяца(Объект.Дата);
	
	Если ЗначениеЗаполнено(ПараметрыГрафика.СпособПогашения) Тогда
		ТекстВопроса = НСтр("ru = 'Заполнить суммы погашения из графика платежа?'");
		ОбработчикОтвета = Новый ОписаниеОповещения("ДатаПриИзмененииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		РассчитатьОстаткиДолга();
	КонецЕсли; 
	
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактПриИзменении(Элемент)
	
	КонтактПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	КонтактНачалоВыбораИзСпискаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолгПриИзменении(Элемент)
	
	ДолгПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолгНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ДолгНачалоВыбораИзСпискаСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КошелекПриИзменении(Элемент)
	
	КошелекПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ФинансоваяЦельПриИзменении(Элемент)
	ОбновитьОстаткиДенег();
КонецПроцедуры

&НаКлиенте
Процедура СуммаПлатежаВсегоПриИзменении(Элемент)
	
	Объект.СуммаПолучено = СуммаПлатежаВсего - (Объект.СуммаПроцентов - Объект.ДополнительныеРасходы.Итог("СуммаРасхода"));
	СуммаПолученоПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПолученоПриИзменении(Элемент)
	
	Объект.СуммаВозвратаДолга = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПолучено,
		Объект.ВалютаПолучено, Объект.ВалютаДолга, КурсВыдано, КурсЗайма, КратностьВыдано, КратностьЗайма);
	ОстатокЗадолженностиНаКонец = ОстатокЗадолженности - Объект.СуммаВозвратаДолга;
	Если Объект.СуммаСписания > ОстатокЗадолженностиНаКонец ИЛИ ОстатокЗадолженностиНаКонец = 0 Тогда
		Объект.СуммаСписания  = 0;
		Объект.СтатьяСписания = Неопределено;
		СписатьОстатокДолга   = Ложь;
	КонецЕсли; 
	Если СписатьОстатокДолга Тогда
		Объект.СуммаСписания  = ОстатокЗадолженностиНаКонец;
	КонецЕсли; 
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	
	Элементы.СписатьОстатокДолга.Доступность = ОстатокЗадолженностиНаКонец <> 0;
	Элементы.СтатьяСписания.Видимость        = СписатьОстатокДолга;
	ОформлениеАналитикиСтатьиСписания(ЭтаФорма);
	
	ОбновитьЗаголовокГруппыОсновнойДолг(ЭтаФорма);
	ОбновитьЗаголовокГруппыОстатокДолгаСписаниеДолга(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВозвратаДолгаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.СуммаПолучено) Тогда
		Объект.СуммаПолучено = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаВозвратаДолга,
			 Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
		СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	КонецЕсли; 
	ОстатокЗадолженностиНаКонец = ОстатокЗадолженности - Объект.СуммаВозвратаДолга;
	Если Объект.СуммаСписания > ОстатокЗадолженностиНаКонец ИЛИ ОстатокЗадолженностиНаКонец = 0 Тогда
		Объект.СуммаСписания  = 0;
		Объект.СтатьяСписания = Неопределено;
		СписатьОстатокДолга   = Ложь;
	КонецЕсли; 
	Если СписатьОстатокДолга Тогда
		Объект.СуммаСписания  = ОстатокЗадолженностиНаКонец;
	КонецЕсли; 
	
	Элементы.СписатьОстатокДолга.Доступность       = ОстатокЗадолженностиНаКонец <> 0;
	Элементы.СтатьяСписания.Видимость              = СписатьОстатокДолга;
	ОформлениеАналитикиСтатьиСписания(ЭтаФорма);
	
	ОбновитьЗаголовокГруппыОсновнойДолг(ЭтаФорма);
	ОбновитьЗаголовокГруппыОстатокДолгаСписаниеДолга(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПроцентовПриИзменении(Элемент)
	
	Объект.СуммаПроцентовВВалютеДолга = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПроцентов,
		Объект.ВалютаПолучено, Объект.ВалютаДолга, КурсВыдано, КурсЗайма, КратностьВыдано, КратностьЗайма);
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПроцентовВВалютеДолгаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.СуммаПроцентов) Тогда
		Объект.СуммаПроцентов = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПроцентовВВалютеДолга,
			 Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
		СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	КонецЕсли; 
	
	УправлениеФормой(ЭтаФорма);
		 
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДоходовПоПроцентамПриИзменении(Элемент)
	
	// Обслуживание аналитики статей:
	АналитикаСтатейВызовСервера.ПриИзмененииСтатьиВШапке(Объект.СтатьяДоходовПоПроцентам, Объект.АналитикаСтатьиДоходовПоПроцентам);
	
	ОформлениеАналитикиСтатьиПоПроцентам(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДоходовПоПроцентамОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбслуживаниеСправочниковКлиент.РасширенноеПолучениеДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСтатьиДоходовПоПроцентамСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Меню = Новый СписокЗначений;
	Меню.Добавить("Изменить", "Изменить...", , БиблиотекаКартинок.Изменить);
	Меню.Добавить("Очистить", , , БиблиотекаКартинок.Очистить);
	ОбработчикВыбора = Новый ОписаниеОповещения("АналитикаСтатьиДоходовПоПроцентамСтрокойНажатиеЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОбработчикВыбора, Меню, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписатьОстатокДолгаПриИзменении(Элемент)
	
	Если СписатьОстатокДолга Тогда
		Объект.СуммаСписания  = ОстатокЗадолженностиНаКонец;
	Иначе
		Объект.СуммаСписания  = 0;
		Объект.СтатьяСписания = Неопределено;
	КонецЕсли; 
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяСписанияПриИзменении(Элемент)
	
	// Обслуживание аналитики статей:
	АналитикаСтатейВызовСервера.ПриИзмененииСтатьиВШапке(Объект.СтатьяСписания, Объект.АналитикаСтатьиСписания);
	
	ОформлениеАналитикиСтатьиСписания(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяСписанияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбслуживаниеСправочниковКлиент.РасширенноеПолучениеДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСтатьиСписанияСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Меню = Новый СписокЗначений;
	Меню.Добавить("Изменить", "Изменить...", , БиблиотекаКартинок.Изменить);
	Меню.Добавить("Очистить", , , БиблиотекаКартинок.Очистить);
	ОбработчикВыбора = Новый ОписаниеОповещения("АналитикаСтатьиСписанияСтрокойНажатиеЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОбработчикВыбора, Меню, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОткрытьГрафикНажатие(Элемент)
	
	ПлановыеОперацииКлиент.ОткрытьРедакторРасписания(ЭтаФорма);
	
КонецПроцедуры

// Обслуживание плановых операций
&НаКлиенте
Процедура ИзменитьРасписание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПлановыеОперацииКлиент.ОткрытьРедакторРасписания(ЭтаФорма);
	
КонецПроцедуры

// Обслуживание плановых операций
&НаКлиенте
Процедура ИнформацияОШаблонеНажатие(Элемент, СтандартнаяОбработка)
	
	ПлановыеОперацииКлиент.ИнформацияОШаблонеНажатие(ЭтаФорма, Элемент, СтандартнаяОбработка, "НамВернулиДолг");
	
КонецПроцедуры

// Обслуживание аналитики статей:
&НаКлиенте
Процедура Подключаемый_ПриИзмененииАналитикиВШапке(Элемент)
	
	АналитикаСтатейКлиент.ПриИзмененииАналитикиВШапке(ЭтаФорма, Элемент);
	
КонецПроцедуры

#Область ПрикрепленныеФайлы

&НаКлиенте
Процедура ПредставлениеФайловОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ПрикрепленныеФайлыКлиент.ОбработчикНажатияНаПредставлениеФайлов(ЭтотОбъект, СтандартнаяОбработка, НавигационнаяСсылкаФорматированнойСтроки);
КонецПроцедуры

#КонецОбласти 


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеРасходы

&НаКлиенте
Процедура ДополнительныеРасходыПриИзменении(Элемент)
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	
	ОбновитьЗаголовокГруппыДополнительныеРасходы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРасходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		ДанныеСтроки = Элементы.ДополнительныеРасходы.ТекущиеДанные;
		ДанныеСтроки.КошелекРасхода = Объект.Кошелек;
		ДанныеСтроки.ФинансоваяЦель = Объект.ФинансоваяЦель;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРасходыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// Обслуживание аналитики статей:
	ТекущиеДанные = Элементы.ДополнительныеРасходы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И НЕ ЗначениеЗаполнено(ТекущиеДанные.СтатьяРасхода) Тогда
		СтатьяПриИзмененииСервер(Элементы.ДополнительныеРасходы.ТекущаяСтрока);
	КонецЕсли;
	Если НЕ АналитикаСтатейКлиент.ВидимостьЭлементовШапкиИзменилась(ЭтаФорма) Тогда
		АналитикаСтатейКлиент.ПриОкончанииРедактированияСтрокиТабличнойЧасти(ЭтаФорма, Истина, "ДополнительныеРасходы", ТекущиеДанные);
	Иначе
		УправлениеРеквизитамиАналитики();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРасходыПослеУдаления(Элемент)
	
	// Обслуживание аналитики статей:
	Если НЕ АналитикаСтатейКлиент.ВидимостьЭлементовШапкиИзменилась(ЭтаФорма) Тогда
		АналитикаСтатейКлиент.ПослеУдаленияСтрокиТабличнойЧасти(ЭтаФорма, Истина, "ДополнительныеРасходы");
	Иначе
		УправлениеРеквизитамиАналитики();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРасходыСтатьяРасходаПриИзменении(Элемент)
	
	СтатьяПриИзмененииСервер(Элементы.ДополнительныеРасходы.ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеРасходыСтатьяРасходаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбслуживаниеСправочниковКлиент.РасширенноеПолучениеДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСуммамиИзГрафика(Команда)
	
	ЗаполнитьСуммамиГрафикаСервер();
	
КонецПроцедуры

// Обслуживание аналитики статей:
&НаКлиенте
Процедура Подключаемый_НастройкаКолонокАналитики(Команда)

	АналитикаСтатейКлиент.ИзменитьНастройкиКолонокАналитики(ЭтаФорма, Истина, "НамВернулиДолг");

КонецПроцедуры

// Обслуживание аналитики статей:
&НаКлиенте
Процедура НастройкаКолонокАналитикиЗавершение(НастройкиИзменены) Экспорт

	Если НастройкиИзменены Тогда
		УправлениеРеквизитамиАналитики(Истина, Истина);
	КонецЕсли; 

КонецПроцедуры
 
// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	СтильПоясняющийОшибкуТекст = ЦветаСтиля.ПоясняющийОшибкуТекст;
	СтильЗаголовокГруппыЦвет   = ЦветаСтиля.ЗаголовокГруппыЦвет;
	
	МесяцДокумента = НачалоМесяца(Объект.Дата);
	
	// Подготовка реквизитов формы
	ВалютаУчета         = Константы.ВалютаУчета.Получить();
	ВалютаОперации      = ?(ЗначениеЗаполнено(Объект.Долг), Объект.Долг.Валюта, ВалютаУчета);
	СписатьОстатокДолга = ЗначениеЗаполнено(Объект.СтатьяСписания) ИЛИ ЗначениеЗаполнено(Объект.СуммаСписания);
	Если Объект.ВалютаПолучено = ВалютаУчета ИЛИ НЕ ЗначениеЗаполнено(Объект.ВалютаПолучено) Тогда
		КурсВыдано      = 1;
		КратностьВыдано = 1;
	Иначе
		КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаПолучено, Объект.Дата, ВалютаУчета);
		КурсВыдано      = КурсИКратность.Курс;
		КратностьВыдано = КурсИКратность.Кратность;
	КонецЕсли; 
	Если Объект.ВалютаДолга = ВалютаУчета ИЛИ НЕ ЗначениеЗаполнено(Объект.ВалютаДолга) Тогда
		КурсЗайма      = 1;
		КратностьЗайма = 1;
	Иначе
		КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДолга, Объект.Дата, ВалютаУчета);
		КурсЗайма      = КурсИКратность.Курс;
		КратностьЗайма = КурсИКратность.Кратность;
	КонецЕсли; 
	ТекстПоясненияГрафика = НСтр("ru='Реквизиты операции автоматически заполняются из графика платежей.'"); 
	
	// Контакт должен соответствовать долгу
	Если Объект.Контакт <> Объект.Долг.Контакт Тогда
		Объект.Контакт = Объект.Долг.Контакт;
		Модифицированность = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Контакт) Тогда
		Массив = Новый Массив();
		Массив.Добавить(Новый СвязьПараметраВыбора("Отбор.Контакт", "Объект.Контакт", РежимИзмененияСвязанногоЗначения.НеИзменять));
		Элементы.Долг.СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
	Иначе
		Элементы.Долг.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив());
	КонецЕсли;
	
	// Обслуживание аналитики статей:
	АналитикаСтатей.ПриСозданииНаСервере(ЭтаФорма, "", "", "СтатьяРасхода", "АналитикаСтатьи", "ДополнительныеРасходыКоманднаяПанель");
	
	// Заполнение дополнительных колонок
	ЗаполнитьДобавленныеКолонки();
	
	// Обслуживание плановых операций
	РаботаСФормамиДокументов.ПодготовитьФормуНаСервереПоРаботеСШаблонами(ЭтаФорма);
	
	// Для нового шаблона погашения займа поправим настройки расписания:
	Если Параметры.Ключ.Пустая() И Объект.ЭтоШаблон И ЗначениеЗаполнено(Объект.Долг) Тогда
		ПараметрыРасписания.Периодичность        = Перечисления.СпособыПовторовСобытий.ВУказанныеДаты;
		ПараметрыРасписания.ПорядокПериодов      = 0;
		ПараметрыРасписания.ПорядокДнейНедели    = 0;
		ПараметрыРасписания.ШаблонДнейНедели     = "";
		ПараметрыРасписания.ШаблонЧиселМесяца    = "";
		ПараметрыРасписания.ШаблонНомеровМесяцев = "";
		// получим пустую таблицу произвольных дат:
		ТаблицаДат = ПлановыеОперации.ПолучитьТаблицуПроизвольныхДатДляРедактирования(Объект.Ссылка, ПараметрыРасписания.ДатаНачалаРасписания, ПараметрыРасписания.ДатаНачалаРасписания);
		//Добавим дату и сохраним таблицу во временном хранилище:
		СтрокаДаты = ТаблицаДат.Добавить();
		СтрокаДаты.ПлановаяДата = ДобавитьМесяц(ПараметрыРасписания.ДатаНачалаРасписания, 1);
		СтрокаДаты.СуммаДолга = Объект.СуммаВозвратаДолга;
		АдресПроизвольныхДатРасписания = ПоместитьВоВременноеХранилище(ТаблицаДат, УникальныйИдентификатор);
		ПараметрыРасписания.ОписаниеРасписания   = ПлановыеОперацииКлиентСервер.ПредставлениеРасписания(ПараметрыРасписания, ТаблицаДат);
	КонецЕсли; 
	
	ПрочитатьПараметрыГрафикаПогашения();
	
	Если НЕ Объект.ЭтоШаблон И Параметры.Ключ.Пустая() Тогда
		ЗаполнитьСуммамиГрафикаСервер(); 
	КонецЕсли; 
	
	Если Объект.Ссылка.Пустая() И НЕ ЗначениеЗаполнено(Объект.Кошелек) Тогда
		Объект.Кошелек = ПользовательскиеНастройкиДеньгиСервер.ОсновнойКошелек();
		Объект.ВалютаПолучено = Объект.Кошелек.Валюта;
	КонецЕсли; 
	
	РассчитатьОстаткиДолга();
	
	ПериодОстатков = ?(Объект.Проведен, Объект.Ссылка.МоментВремени(), КонецДня(ТекущаяДатаКлиентСервер()));
	ОстатокВКошельке = РазделыУчета.ПолучитьОстатокПоСубконто(ПланыСчетов.РазделыУчета.Деньги, Объект.Кошелек, ПериодОстатков);
	ТекстЗаголовка = НСтр("ru = 'Остаток до операции: %1 %2'");
	Элементы.ОстатокВКошельке.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, Формат(ОстатокВКошельке, "ЧДЦ=2; ЧН=0,00"), Объект.ВалютаПолучено);
	
	ВидимостьФинансовойЦели = ЗначениеЗаполнено(Объект.Кошелек) И Объект.Кошелек.ИспользоватьДляНакоплений;
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	
	// Общие настройки форм элементов документов
	РаботаСФормамиДокументов.ФормаДокументаПриСозданииНаСервере(ЭтаФорма);
	
	// Обслуживание аналитики статей:
	УправлениеРеквизитамиАналитики();
	
	#Область ПрикрепленныеФайлы
	ПрикрепленныеФайлыКлиентСервер.ОбновитьПредставлениеМедиафайлов(ЭтотОбъект);
	#КонецОбласти	
	
	ОбновитьОстаткиДенег();
	
	// Настройка внешнего вида
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьДобавленныеКолонки()

	// Обслуживание аналитики статей:
	АналитикаСтатей.ЗаполнитьРеквизитыАналитикой(ЭтаФорма);
	
	СписатьОставшуюсяСуммуДолга = ЗначениеЗаполнено(Объект.СуммаСписания);

	РассчитатьОстаткиДолга();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УправлениеФормой(Форма)

	Элементы               = Форма.Элементы;
	ПараметрыГрафика       = Форма.ПараметрыГрафика;
	ПараметрыРасписания    = Форма.ПараметрыРасписания;
	Объект                 = Форма.Объект;
	
	// Обслуживание плановых операций
	ПлановыеОперацииКлиентСервер.ОбновитьИнформациюОСвязиОперацииСРасписанием(Форма);
	
	Элементы.ФормаЗаполнитьСуммамиИзГрафика.Доступность = НЕ Объект.ЭтоШаблон И ЗначениеЗаполнено(ПараметрыГрафика.ШаблонОперации);
		
	РаботаСФормамиДокументовКлиентСервер.ОбновитьСостояниеОперации(Форма);
	
	Элементы.ГруппаКошелекФинансоваяЦельОстаток.Видимость = НЕ Объект.ЭтоШаблон;
	Элементы.ГруппаСуммаПлатежаВсегоВалюта.Видимость      = НЕ Объект.ЭтоШаблон;
	Элементы.ГруппаОсновнойДолг.Видимость                 = НЕ Объект.ЭтоШаблон;
	Элементы.ГруппаПроценты.Видимость                     = НЕ Объект.ЭтоШаблон;
	Элементы.ГруппаОстатокДолгаСписаниеДолга.Видимость    = НЕ Объект.ЭтоШаблон;
	Элементы.ГруппаНадписьОткрытиеГрафика.Видимость       = Объект.ЭтоШаблон;
	Элементы.ГруппаКошелекИзГрафика.Видимость             = Объект.ЭтоШаблон;
	Элементы.НадписьСуммаПлатежаВсегоИзГрафика.Видимость  = Объект.ЭтоШаблон;
	Элементы.ГруппаСуммыИзГрафика.Видимость               = Объект.ЭтоШаблон;
	
	ОбновитьЗаголовокГруппыДополнительныеРасходы(Форма);
	
	Если НЕ Объект.ЭтоШаблон Тогда
		
		Элементы.ВТомЧисле.Ширина = 10;
		
		ОбновитьЗаголовокГруппыПроценты(Форма);
		ОбновитьЗаголовокГруппыОсновнойДолг(Форма);
		ОбновитьЗаголовокГруппыОстатокДолгаСписаниеДолга(Форма);
		
		Элементы.ФинансоваяЦель.Видимость = Форма.ВидимостьФинансовойЦели;
		
		ВидимостьСуммВВалютеДолга = ВалютыПлатежаИДолгаОтличаются(Объект);
		Элементы.ГруппаОсновнойДолгВВалютеДолга.Видимость = ВидимостьСуммВВалютеДолга;
		Элементы.ГруппаПроцентыВВалютеДолга.Видимость     = ВидимостьСуммВВалютеДолга;
		
		Элементы.СтатьяДоходовПоПроцентам.Доступность = Объект.СуммаПроцентов <> 0 ИЛИ Объект.СуммаПроцентовВВалютеДолга <> 0;
		
		Элементы.СписатьОстатокДолга.Доступность = Форма.ОстатокЗадолженностиНаКонец <> 0;
		Элементы.СтатьяСписания.Видимость        = Форма.СписатьОстатокДолга;
		
		ОформлениеАналитикиСтатьиПоПроцентам(Форма);
		ОформлениеАналитикиСтатьиСписания(Форма);
		
	Иначе
		
		Элементы.ВТомЧисле.Ширина = 12;
		
		Форма.НадписьСуммаИзГрафика = НСтр("ru='По графику платежей'"); 
		
	КонецЕсли; 
	
	РаботаСФормамиДокументовКлиентСервер.ОбновитьЭлементыФормыПоСостояниюОперации(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыГрафикаПогашения()

	ЗаписьПараметровГрафика      = РегистрыСведений.ПараметрыГрафикаПогашенияДолгов.СоздатьМенеджерЗаписи();
	ЗаписьПараметровГрафика.Долг = Объект.Долг;
	ЗаписьПараметровГрафика.Прочитать();
	
	Если ЗаписьПараметровГрафика.Выбран() И ЗаписьПараметровГрафика.ШаблонОперации = Объект.Ссылка Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыГрафика, ЗаписьПараметровГрафика);
	КонецЕсли;
	ПараметрыГрафика.Долг = Объект.Долг;
	
	// Предварительно очистим ЗаписьПлановойОперации
	ПустаяЗапись = РегистрыСведений.ДатыРасписаний.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(ЗаписьПлановойОперации, ПустаяЗапись);
	
	Если ЗначениеЗаполнено(ЗаписьПараметровГрафика.ШаблонОперации) Тогда
		
		// Найдем запись по текущему документу или месяцу документа
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВладелецРасписания", ЗаписьПараметровГрафика.ШаблонОперации);
		Запрос.УстановитьПараметр("ЭтотДокумент", Объект.Ссылка);
		Запрос.УстановитьПараметр("ЭтотДокументСуществует", НЕ Объект.Ссылка.Пустая());
		Запрос.УстановитьПараметр("МесяцДокумента", НачалоМесяца(Объект.Дата));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыРасписаний.ВладелецРасписания,
		|	ДатыРасписаний.ПлановаяДата КАК ПлановаяДата,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ Измерения
		|ИЗ
		|	РегистрСведений.ДатыРасписаний КАК ДатыРасписаний
		|ГДЕ
		|	&ЭтотДокументСуществует И ДатыРасписаний.ПлановаяОперация = &ЭтотДокумент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДатыРасписаний.ВладелецРасписания,
		|	ДатыРасписаний.ПлановаяДата,
		|	2
		|ИЗ
		|	РегистрСведений.ДатыРасписаний КАК ДатыРасписаний
		|ГДЕ
		|	ДатыРасписаний.ВладелецРасписания = &ВладелецРасписания
		|	И НАЧАЛОПЕРИОДА(ДатыРасписаний.ПлановаяДата, МЕСЯЦ) = &МесяцДокумента
		|	И ДатыРасписаний.ПлановаяОперация <> &ЭтотДокумент
		|	И НЕ ДатыРасписаний.Пропустить
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДатыРасписаний.ВладелецРасписания,
		|	ДатыРасписаний.ПлановаяДата КАК ПлановаяДата,
		|	ДатыРасписаний.АктуальнаяДата КАК АктуальнаяДата,
		|	ДатыРасписаний.Пропустить,
		|	ДатыРасписаний.СуммаДолга,
		|	ДатыРасписаний.СуммаПроцентов,
		|	ДатыРасписаний.СуммаКомиссии,
		|	ДатыРасписаний.ПлановаяОперация
		|ИЗ
		|	Измерения КАК Измерения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДатыРасписаний КАК ДатыРасписаний
		|		ПО Измерения.ВладелецРасписания = ДатыРасписаний.ВладелецРасписания
		|			И Измерения.ПлановаяДата = ДатыРасписаний.ПлановаяДата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Измерения.Приоритет,
		|	ПлановаяДата,
		|	АктуальнаяДата";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ЗаписьПлановойОперации, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСуммамиГрафикаСервер()

	Если НЕ ЗначениеЗаполнено(Объект.Долг) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ПараметрыГрафика.КошелекДляПогашения) Тогда
		Объект.Кошелек = ПараметрыГрафика.КошелекДляПогашения;
	КонецЕсли; 
	Если Объект.Кошелек.ИспользоватьДляНакоплений Тогда
		Объект.ФинансоваяЦель = ?(ЗначениеЗаполнено(ПараметрыГрафика.ФинансоваяЦель), ПараметрыГрафика.ФинансоваяЦель, Справочники.ФинансовыеЦели.ОбщиеНакопления);
	Иначе
		Объект.ФинансоваяЦель = Неопределено;
	КонецЕсли; 
	
	Объект.СуммаВозвратаДолга         = ЗаписьПлановойОперации.СуммаДолга;
	
	Объект.СуммаПроцентовВВалютеДолга = ЗаписьПлановойОперации.СуммаПроцентов;
	Объект.СтатьяДоходовПоПроцентам = ПараметрыГрафика.СтатьяДляУчетаПроцентов;
	АналитикаСтатей.ПриИзмененииСтатьиВШапке(Объект.СтатьяДоходовПоПроцентам, Объект.АналитикаСтатьиДоходовПоПроцентам);
	
	Если ЗначениеЗаполнено(ЗаписьПлановойОперации.СуммаКомиссии) Тогда
		Строки = Объект.ДополнительныеРасходы.НайтиСтроки(Новый Структура("КошелекРасхода, СтатьяРасхода", Объект.Кошелек, ПараметрыГрафика.СтатьяЕжемесячнойКомиссии));
		Если ЗначениеЗаполнено(Строки) Тогда
			СтрокаДопРасхода = Строки[0];
		Иначе
			СтрокаДопРасхода = Объект.ДополнительныеРасходы.Добавить();
		КонецЕсли;
		СтрокаДопРасхода.КошелекРасхода = Объект.Кошелек;
		СтрокаДопРасхода.СтатьяРасхода  = ПараметрыГрафика.СтатьяЕжемесячнойКомиссии;
		СтрокаДопРасхода.СуммаРасхода   = ЗаписьПлановойОперации.СуммаКомиссии;
	КонецЕсли; 
	
	// Пересчет сумм в валюту кошелька
	КошелекПриИзмененииСервер();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьОстаткиДолга()
	
	ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.МоментВремени(), КонецДня(ТекущаяДатаКлиентСервер()));
	ОстатокЗадолженности         = РазделыУчета.ПолучитьОстатокПоСубконто(ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов, Объект.Долг, ДатаОстатков);
	ОстатокЗадолженностиНаКонец  = ОстатокЗадолженности - Объект.СуммаВозвратаДолга;
	Если СписатьОстатокДолга Тогда
		Объект.СуммаСписания  = ОстатокЗадолженностиНаКонец;
	КонецЕсли; 
	
	ОстатокПроцентов           = РазделыУчета.ПолучитьОстатокПоСубконто(ПланыСчетов.РазделыУчета.ПроцентыПоДолгам, Объект.Долг, ДатаОстатков);
	Элементы.ГруппаОстатокПроцентов.Видимость = (ОстатокПроцентов <> 0);
	
	ОбновитьЗаголовокГруппыОстатокДолгаСписаниеДолга(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура КонтактПриИзмененииСервер()

	Если Объект.Долг.Контакт <> Объект.Контакт Тогда
		Объект.Долг = Неопределено;
	КонецЕсли;
	
	ДолгиКонтакта = Новый СписокЗначений;
	ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.МоментВремени(), КонецДня(ТекущаяДатаКлиентСервер()));
	РазделыУчета.ЗаполнитьСписокВыбораДолговКонтакта(ДолгиКонтакта, Объект.Контакт, ДатаОстатков, Истина, Ложь);
	Если ДолгиКонтакта.Количество() = 1 Тогда
		Объект.Долг = ДолгиКонтакта[0].Значение;
	КонецЕсли;
	
	ДолгПриИзмененииСервер();

КонецПроцедуры

&НаСервере
Процедура КонтактНачалоВыбораИзСпискаСервер()
	
	СписокВыбора = Элементы.Контакт.СписокВыбора;
	
	ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.МоментВремени(), КонецДня(ТекущаяДатаКлиентСервер()));
	РазделыУчета.ЗаполнитьСписокВыбораЗаемщиковКредиторов(СписокВыбора, ДатаОстатков, ВалютаУчета, Истина, Ложь);
	
	Если НЕ ЗначениеЗаполнено(СписокВыбора) Тогда
		ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.Дата, КонецДня(ТекущаяДатаКлиентСервер()));
		Представление = НСтр("ru = '<Нет должников на %1>'");
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Представление, Формат(ДатаОстатков, "ДФ='дд.ММ.гггг (ЧЧ:мм)'"));
		СписокВыбора.Добавить(Неопределено, Представление);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДолгПриИзмененииСервер()

	Если ЗначениеЗаполнено(Объект.Долг) Тогда
		Объект.Контакт = Объект.Долг.Контакт;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Контакт) Тогда
		Массив = Новый Массив();
		Массив.Добавить(Новый СвязьПараметраВыбора("Отбор.Контакт", "Объект.Контакт", РежимИзмененияСвязанногоЗначения.НеИзменять));
		Элементы.Долг.СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
	Иначе
		Элементы.Долг.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив());
	КонецЕсли;
	
	Объект.ВалютаДолга  = Объект.Долг.Валюта;
	Если Объект.ВалютаДолга = ВалютаУчета ИЛИ НЕ ЗначениеЗаполнено(Объект.ВалютаДолга) Тогда
		КурсЗайма      = 1;
		КратностьЗайма = 1;
	Иначе
		КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДолга, Объект.Дата, ВалютаУчета);
		КурсЗайма      = КурсИКратность.Курс;
		КратностьЗайма = КурсИКратность.Кратность;
	КонецЕсли; 
	
	РассчитатьОстаткиДолга();
	
	ПрочитатьПараметрыГрафикаПогашения();
	Если Объект.СуммаВозвратаДолга = 0 И Объект.СуммаПроцентовВВалютеДолга = 0 Тогда
		ЗаполнитьСуммамиГрафикаСервер();
	КонецЕсли; 
	
	Если Объект.СуммаВозвратаДолга = 0 И Объект.СуммаПроцентовВВалютеДолга = 0 Тогда
		Если ОстатокЗадолженности > 0 Тогда
			Объект.СуммаВозвратаДолга = ОстатокЗадолженности;
			Объект.СуммаПолучено = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаВозвратаДолга,
				 Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
			Объект.СуммаПроцентов = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПроцентовВВалютеДолга,
				Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
		Иначе
			Объект.СуммаВозвратаДолга = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПолучено,
				Объект.ВалютаПолучено, Объект.ВалютаДолга, КурсВыдано, КурсЗайма, КратностьВыдано, КратностьЗайма);
			Объект.СуммаПроцентовВВалютеДолга = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПроцентов,
				Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
		КонецЕсли;
	КонецЕсли; 
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	 
	ОстатокЗадолженностиНаКонец = ОстатокЗадолженности - Объект.СуммаВозвратаДолга;
	Если Объект.СуммаСписания > ОстатокЗадолженностиНаКонец ИЛИ ОстатокЗадолженностиНаКонец = 0 Тогда
		Объект.СуммаСписания  = 0;
		Объект.СтатьяСписания = Неопределено;
		СписатьОстатокДолга   = Ложь;
	КонецЕсли; 
	Если СписатьОстатокДолга Тогда
		Объект.СуммаСписания  = ОстатокЗадолженностиНаКонец;
	КонецЕсли; 
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ДолгНачалоВыбораИзСпискаСервер()
	
	СписокВыбора = Элементы.Долг.СписокВыбора;
	
	ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.МоментВремени(), КонецДня(ТекущаяДатаСеанса()));
	РазделыУчета.ЗаполнитьСписокВыбораДолговКонтакта(СписокВыбора, Объект.Контакт, ДатаОстатков, Истина, Ложь);
	
	Если НЕ ЗначениеЗаполнено(СписокВыбора) Тогда
		ДатаОстатков = ?(Объект.Проведен, Объект.Ссылка.Дата, КонецДня(ТекущаяДатаСеанса()));
		Представление = НСтр("ru = '<Нет долгов контакта %1 на %2>'");
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Представление, 
			?(ЗначениеЗаполнено(Объект.Контакт), """" + Объект.Контакт + """", НСтр("ru = '<Контакт не задан>'")), 
			Формат(ДатаОстатков, "ДФ='дд.ММ.гггг (ЧЧ:мм)'"));
		СписокВыбора.Добавить(Неопределено, Представление);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КошелекПриИзмененииСервер()
	
	Для Каждого СтрокаРасхода Из Объект.ДополнительныеРасходы Цикл
		СтрокаРасхода.КошелекРасхода = Объект.Кошелек;
	КонецЦикла; 

	ВидимостьФинансовойЦели = ЗначениеЗаполнено(Объект.Кошелек) И Объект.Кошелек.ИспользоватьДляНакоплений;
	
	Объект.ВалютаПолучено = Объект.Кошелек.Валюта;
	
	Если Объект.ВалютаПолучено = ВалютаУчета ИЛИ НЕ ЗначениеЗаполнено(Объект.ВалютаПолучено) Тогда
		КурсВыдано      = 1;
		КратностьВыдано = 1;
	Иначе
		КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаПолучено, Объект.Дата, ВалютаУчета);
		КурсВыдано      = КурсИКратность.Курс;
		КратностьВыдано = КурсИКратность.Кратность;
	КонецЕсли; 
	
	Объект.СуммаПолучено = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаВозвратаДолга,
		Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
	Объект.СуммаПроцентов = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(Объект.СуммаПроцентовВВалютеДолга,
		Объект.ВалютаДолга, Объект.ВалютаПолучено, КурсЗайма, КурсВыдано, КратностьЗайма, КратностьВыдано);
	
	СуммаПлатежаВсего = СуммаПлатежаВсего(ЭтаФорма);
	
	Если НЕ ЗначениеЗаполнено(Объект.ФинансоваяЦель) Тогда
		Объект.ФинансоваяЦель = Справочники.ФинансовыеЦели.ОбщиеНакопления;
	КонецЕсли; 
	
	ОбновитьОстаткиДенег();
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ОбновитьОстаткиДенег()

	РаботаСФормамиДокументов.ОбновитьОстатокПоОбъектуУчета(ЭтотОбъект, "ОстатокВКошельке", 
			ПланыСчетов.РазделыУчета.Деньги, Объект.Кошелек, , Объект.ФинансоваяЦель, "ОстатокФинансоваяЦель");

КонецПроцедуры
 

&НаКлиенте
Процедура ПослеЗакрытияРедактораАналитикиСтатьи(Результат, ИмяРеквизитаАналитикаСтатьи) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.ЗначенияСубконтоАналитика") Тогда
		
		Объект[ИмяРеквизитаАналитикаСтатьи] = Результат;
		Модифицированность = Истина;
		
		Если ИмяРеквизитаАналитикаСтатьи = "АналитикаСтатьиДоходовПоПроцентам" Тогда
			ОформлениеАналитикиСтатьиПоПроцентам(ЭтаФорма);
		ИначеЕсли ИмяРеквизитаАналитикаСтатьи = "АналитикаСтатьиСписания" Тогда
			ОформлениеАналитикиСтатьиСписания(ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОформлениеАналитикиСтатьиПоПроцентам(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	// Текст гиперссылки
	Форма.АналитикаСтатьиДоходовПоПроцентамСтрокой = ?(ЗначениеЗаполнено(Объект.АналитикаСтатьиДоходовПоПроцентам), Объект.АналитикаСтатьиДоходовПоПроцентам, НСтр("ru = '<не задана>'"));
	
	// Видимость, доступность
	Элементы.АналитикаСтатьиДоходовПоПроцентамСтрокой.Видимость   = Элементы.СтатьяДоходовПоПроцентам.Видимость;
	Элементы.АналитикаСтатьиДоходовПоПроцентамСтрокой.Доступность = Элементы.СтатьяДоходовПоПроцентам.Доступность И ЗначениеЗаполнено(Объект.СтатьяДоходовПоПроцентам);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОформлениеАналитикиСтатьиСписания(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	// Текст гиперссылки
	Форма.АналитикаСтатьиСписанияСтрокой = ?(ЗначениеЗаполнено(Объект.АналитикаСтатьиСписания), Объект.АналитикаСтатьиСписания, НСтр("ru = '<не задана>'"));
	
	// Видимость, доступность
	Элементы.АналитикаСтатьиСписанияСтрокой.Видимость   = Элементы.СтатьяСписания.Видимость;
	Элементы.АналитикаСтатьиСписанияСтрокой.Доступность = Элементы.СтатьяСписания.Доступность И ЗначениеЗаполнено(Объект.СтатьяСписания);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Функция СуммаПлатежаВсего(Форма)
	
	Объект = Форма.Объект;
	
	Возврат Объект.СуммаПолучено 
			+ Объект.СуммаПроцентов 
			- Объект.ДополнительныеРасходы.Итог("СуммаРасхода");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Функция ВалютыПлатежаИДолгаОтличаются(Объект)
	
	Возврат ЗначениеЗаполнено(Объект.ВалютаПолучено) 
			И ЗначениеЗаполнено(Объект.ВалютаДолга)
			И Объект.ВалютаПолучено <> Объект.ВалютаДолга;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьЗаголовокГруппыОсновнойДолг(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	СтрСуммаДолга = Формат(Объект.СуммаПолучено, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаПолучено), " " + Объект.ВалютаПолучено, "");
	Если ВалютыПлатежаИДолгаОтличаются(Объект) И Объект.СуммаВозвратаДолга <> 0 Тогда
		СтрСуммаДолга = СтрСуммаДолга + " / " + Формат(Объект.СуммаВозвратаДолга, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаДолга), " " + Объект.ВалютаДолга, "");
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = 'Сумма основного долга: %1 '");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, СтрСуммаДолга);
	Элементы.ГруппаОсновнойДолг.ЗаголовокСвернутогоОтображения = "► " + ТекстЗаголовка;
	Элементы.ГруппаОсновнойДолг.Заголовок = "▼ " + ТекстЗаголовка;
	
	Если Объект.СуммаПолучено < 0 ИЛИ Объект.СуммаВозвратаДолга < 0 Тогда
		Элементы.ГруппаОсновнойДолг.ЦветТекстаЗаголовка = Форма.СтильПоясняющийОшибкуТекст;
	Иначе
		Элементы.ГруппаОсновнойДолг.ЦветТекстаЗаголовка = Форма.СтильЗаголовокГруппыЦвет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьЗаголовокГруппыПроценты(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	СтрСуммаПроцентов = Формат(Объект.СуммаПроцентов, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаПолучено), " " + Объект.ВалютаПолучено, "");
	Если ВалютыПлатежаИДолгаОтличаются(Объект) И Объект.СуммаПроцентовВВалютеДолга <> 0 Тогда
		СтрСуммаПроцентов = СтрСуммаПроцентов + " / " + Формат(Объект.СуммаПроцентовВВалютеДолга, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаДолга), " " + Объект.ВалютаДолга, "");
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = 'Проценты: %1 '");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, СтрСуммаПроцентов);
	Элементы.ГруппаПроценты.ЗаголовокСвернутогоОтображения = "► " + ТекстЗаголовка;
	Элементы.ГруппаПроценты.Заголовок = "▼ " + ТекстЗаголовка;
	
	Если Объект.СуммаПроцентов < 0 ИЛИ Объект.СуммаПроцентовВВалютеДолга < 0 Тогда
		Элементы.ГруппаПроценты.ЦветТекстаЗаголовка = Форма.СтильПоясняющийОшибкуТекст;
	Иначе
		Элементы.ГруппаПроценты.ЦветТекстаЗаголовка = Форма.СтильЗаголовокГруппыЦвет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьЗаголовокГруппыДополнительныеРасходы(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ИтогДопРасходов = Объект.ДополнительныеРасходы.Итог("СуммаРасхода");
	ИтогДопРасходовСтрокой = Формат(ИтогДопРасходов, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаПолучено), " " + Объект.ВалютаПолучено, "");
	
	ТекстЗаголовка = НСтр("ru = 'Дополнительные расходы: %1 '");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, ИтогДопРасходовСтрокой);
	Элементы.ГруппаДополнительныеРасходы.ЗаголовокСвернутогоОтображения = "► " + ТекстЗаголовка;
	Элементы.ГруппаДополнительныеРасходы.Заголовок = "▼ " + ТекстЗаголовка;
	
	Если ИтогДопРасходов < 0 Тогда
		Элементы.ГруппаДополнительныеРасходы.ЦветТекстаЗаголовка = Форма.СтильПоясняющийОшибкуТекст;
	Иначе
		Элементы.ГруппаДополнительныеРасходы.ЦветТекстаЗаголовка = Форма.СтильЗаголовокГруппыЦвет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ОбновитьЗаголовокГруппыОстатокДолгаСписаниеДолга(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ТекстЗаголовка = ?(Форма.СписатьОстатокДолга, НСтр("ru = 'Списать остаток долга %1'"), НСтр("ru = 'Остаток долга: %1'"));
	ТекстЗаголовка = "► " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
		Формат(Форма.ОстатокЗадолженностиНаКонец, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаДолга), " " + Объект.ВалютаДолга, ""));
	
	Если Форма.ОстатокПроцентов <> 0 Тогда
		ТекстЗаголовка = ТекстЗаголовка + "; " + НСтр("ru = 'Остаток процентов: %1'");
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
			Формат(Форма.ОстатокПроцентов, "ЧДЦ=2; ЧН=0") + ?(ЗначениеЗаполнено(Объект.ВалютаДолга), " " + Объект.ВалютаДолга, ""));
	КонецЕсли;
	
	Элементы.ГруппаОстатокДолгаСписаниеДолга.ЗаголовокСвернутогоОтображения = ТекстЗаголовка + " ";
	
	Если Форма.ОстатокЗадолженностиНаКонец < 0 Тогда
		Элементы.ГруппаОстатокДолгаСписаниеДолга.ЦветТекстаЗаголовка = Форма.СтильПоясняющийОшибкуТекст;
	Иначе
		Элементы.ГруппаОстатокДолгаСписаниеДолга.ЦветТекстаЗаголовка = Форма.СтильЗаголовокГруппыЦвет;
	КонецЕсли;
	
КонецПроцедуры

// Обслуживание плановых операций
&НаСервере
Процедура ЗаполнитьДанныеПоШаблону(СсылкаНаШаблон)
	
	РаботаСФормамиДокументов.ОбновитьФормуОперацииПоШаблону(ЭтотОбъект, СсылкаНаШаблон);
	
	// Обслуживание аналитики статей:
	АналитикаСтатей.ПриСозданииНаСервере(ЭтаФорма, "", "", "СтатьяРасхода", "АналитикаСтатьи", "ДополнительныеРасходыКоманднаяПанель");
	
	// Заполнение дополнительных колонок и реквизитов
	ЗаполнитьДобавленныеКолонки();
	
	// Общие настройки форм элементов документов
	РаботаСФормамиДокументов.ФормаДокументаПриСозданииНаСервере(ЭтаФорма);
	
	// Настройка внешнего вида
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

// Обслуживание аналитики статей:
&НаСервере
Процедура УправлениеРеквизитамиАналитики(ИспользоватьЗначенияПоУмолчанию = Ложь, ПотеряДанныхРазрешена = Ложь)

	АналитикаСтатей.УправлениеФормой(ЭтаФорма, ИспользоватьЗначенияПоУмолчанию, ПотеряДанныхРазрешена);

КонецПроцедуры
 
// Обслуживание аналитики статей:
&НаСервере
Процедура СтатьяПриИзмененииСервер(ИДСтроки)

	СтрокаДокумента = Объект.ДополнительныеРасходы.НайтиПоИдентификатору(ИДСтроки);
	Если СтрокаДокумента <> Неопределено Тогда
		АналитикаСтатей.ПриИзмененииСтатьиВТабличнойЧасти(ЭтаФорма, СтрокаДокумента.СтатьяРасхода, "ДополнительныеРасходы", ИДСтроки);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект.СуммаПолучено = 0;
		Объект.СуммаВозвратаДолга = 0;
		Объект.СуммаПроцентов = 0;
		Объект.СуммаПроцентовВВалютеДолга = 0;
		ДолгПриИзмененииСервер();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура АналитикаСтатьиДоходовПоПроцентамСтрокойНажатиеЗавершение(Выбор, ДополнительныеПараметры) Экспорт

	Если Выбор = Неопределено Тогда
		Возврат;
	ИначеЕсли Выбор.Значение = "Изменить" Тогда
		ПараметрыЗаполнения = Новый Структура("Статья,ПакетАналитики", Объект.СтатьяДоходовПоПроцентам, Объект.АналитикаСтатьиДоходовПоПроцентам);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияРедактораАналитикиСтатьи", ЭтаФорма, "АналитикаСтатьиДоходовПоПроцентам");
		ОткрытьФорму("ОбщаяФорма.РедакторАналитикиСтатьи", ПараметрыЗаполнения, ЭтаФорма,,,, ОписаниеОповещения);
		
	ИначеЕсли Выбор.Значение = "Очистить" Тогда
		Объект.АналитикаСтатьиДоходовПоПроцентам = Неопределено;
		Модифицированность = Истина;
		ОформлениеАналитикиСтатьиПоПроцентам(ЭтаФорма);
		
	КонецЕсли;
	

КонецПроцедуры
 
&НаКлиенте
Процедура АналитикаСтатьиСписанияСтрокойНажатиеЗавершение(Выбор, ДополнительныеПараметры) Экспорт

	Если Выбор = Неопределено Тогда
		Возврат;
	ИначеЕсли Выбор.Значение = "Изменить" Тогда
		ПараметрыЗаполнения = Новый Структура("Статья,ПакетАналитики", Объект.СтатьяСписания, Объект.АналитикаСтатьиСписания);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияРедактораАналитикиСтатьи", ЭтаФорма, "АналитикаСтатьиСписания");
		ОткрытьФорму("ОбщаяФорма.РедакторАналитикиСтатьи", ПараметрыЗаполнения, ЭтаФорма,,,, ОписаниеОповещения);
		
	ИначеЕсли Выбор.Значение = "Очистить" Тогда
		Объект.АналитикаСтатьиСписания = Неопределено;
		Модифицированность = Истина;
		ОформлениеАналитикиСтатьиСписания(ЭтаФорма);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяДатаКлиентСервер()
	#Если Сервер Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли 
КонецФункции 


// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

