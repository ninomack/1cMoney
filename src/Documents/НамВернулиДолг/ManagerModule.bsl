#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

Процедура СформироватьДвиженияДокумента(СсылкаНаДокумент, ДвиженияДокумента, ДополнительныеСвойства) Экспорт

	СчетДолга     = ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов;
	СчетПроцентов = ПланыСчетов.РазделыУчета.ПроцентыПоДолгам;
	СчетКапитала  = ПланыСчетов.РазделыУчета.Капитал;
	
	// Получаем таблицу для формирования движений
	Если ТипЗнч(ДополнительныеСвойства) <> Тип("Структура") Или Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ПроверитьДополнительныеСвойстваОперации(СсылкаНаДокумент, ДополнительныеСвойства, Истина);
	КонецЕсли;
	ТаблицыДокумента = ДополнительныеСвойства.ТаблицыДокумента;
	
	// Проводки для самого займа
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.Основное Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		
		Если СтрокаДокумента.СуммаПроцентовВВалютеДолга <> 0 Тогда
			// Начисление процентов
			РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
				СчетПроцентов, СтрокаДокумента.Долг, , 
					СтрокаДокумента.СуммаПроцентовВВалютеДолга, СтрокаДокумента.ВалютаДолга,
				СчетКапитала, СтрокаДокумента.СтатьяДоходовПоПроцентам, СтрокаДокумента.АналитикаСтатьиДоходовПоПроцентам, 
					СтрокаДокумента.СуммаПроцентовВВалютеДолга, СтрокаДокумента.ВалютаДолга,
				СтрокаДокумента.Комментарий);
		КонецЕсли; 
		
		Если СтрокаДокумента.СуммаПолучено <> 0 ИЛИ СтрокаДокумента.СуммаВозвратаДолга <> 0 Тогда
			// Возврат тела долга
			РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
				СчетДенег, СтрокаДокумента.Кошелек, СтрокаДокумента.ФинансоваяЦель, 
					СтрокаДокумента.СуммаПолучено, СтрокаДокумента.ВалютаПолучено,
				СчетДолга, СтрокаДокумента.Долг, , 
					СтрокаДокумента.СуммаВозвратаДолга, СтрокаДокумента.ВалютаДолга,
				СтрокаДокумента.Комментарий);
		КонецЕсли; 
		
		Если СтрокаДокумента.СуммаПроцентов <> 0 ИЛИ СтрокаДокумента.СуммаПроцентовВВалютеДолга <> 0 Тогда
			// Возврат процентов
			РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
				СчетДенег, СтрокаДокумента.Кошелек, СтрокаДокумента.ФинансоваяЦель, 
					СтрокаДокумента.СуммаПроцентов, СтрокаДокумента.ВалютаПолучено,
				СчетПроцентов, СтрокаДокумента.Долг, , 
					СтрокаДокумента.СуммаПроцентовВВалютеДолга, СтрокаДокумента.ВалютаДолга,
				СтрокаДокумента.Комментарий);
		КонецЕсли; 
		
		Если СтрокаДокумента.СуммаСписания <> 0 Тогда
			// Списание тела долга
			РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
				СчетКапитала, СтрокаДокумента.СтатьяСписания, СтрокаДокумента.АналитикаСтатьиСписания, 
					СтрокаДокумента.СуммаСписания, СтрокаДокумента.ВалютаДолга,
				СчетДолга, СтрокаДокумента.Долг, , 
					СтрокаДокумента.СуммаСписания, СтрокаДокумента.ВалютаДолга,
				СтрокаДокумента.Комментарий);
		КонецЕсли; 
		
	КонецЦикла; 

	// Проводки по доп.расходам на выдачу займа
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.ДополнительныеРасходы Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		
		РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
			СчетКапитала, СтрокаДокумента.СтатьяРасхода, СтрокаДокумента.АналитикаСтатьи, 
				СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СчетДенег, СтрокаДокумента.КошелекРасхода, СтрокаДокумента.ФинансоваяЦель, 
				СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СтрокаДокумента.Комментарий);
		
	КонецЦикла; 
	
	// Записываем движения в регистр фактических оборотов бюджета
	Если ТаблицыДокумента.Свойство("ОборотыБюджета") Тогда
		ДвиженияДокумента.ФактическиеОборотыБюджета.Загрузить(ТаблицыДокумента.ОборотыБюджета);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру ДополнительныеСвойства значениями, необходимыми для дальнейшей обработки документа
//
//Параметры:
//	Операция               - ссылка или объект документа, зависит от контекста вызова этой процедуры
//	ДополнительныеСвойства - Структура или Неопределено - сордержит дополнительные свойства документа
//	ПроверятьТаблицыДокумента - Булево - заполнять ли таблицы для формирования движений
//
Процедура ПроверитьДополнительныеСвойстваОперации(Операция, ДополнительныеСвойства, ПроверятьТаблицыДокумента = Ложь) Экспорт

	// Выполняем проверку, одинаковую для всех документов
	ОбслуживаниеДокументов.ОбщаяПроверкаДополнительныхСвойствДокумента(Операция, ДополнительныеСвойства);
	
	Если ПроверятьТаблицыДокумента И Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ДополнительныеСвойства.Вставить("ТаблицыДокумента", ПолучитьВыборкиДляФормированияДвижений(Операция.Ссылка, ДополнительныеСвойства));
	КонецЕсли;
	
КонецПроцедуры

// Обновляет итоговые суммы и описание операции
Процедура ОбновитьИтоговыеСуммыДокумента(ДокументОбъект) Экспорт
	
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	
	ДокументОбъект.ВалютаДолга    = ДокументОбъект.Долг.Валюта;
	ДокументОбъект.ВалютаПолучено = ДокументОбъект.Кошелек.Валюта;

	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦель) Тогда
		ДокументОбъект.ФинансоваяЦель = Справочники.ФинансовыеЦели.ОбщиеНакопления;
	КонецЕсли; 
	
	ДокументОбъект.СуммаДополнительныхРасходов = ДокументОбъект.ДополнительныеРасходы.Итог("СуммаРасхода");
	Если ЗначениеЗаполнено(ДокументОбъект.СуммаСписания) Тогда
		Если ДокументОбъект.ВалютаДолга = ДокументОбъект.ВалютаПолучено Тогда
			РасходПоСуммеСписания = ДокументОбъект.СуммаСписания;
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.СуммаПолучено) И ЗначениеЗаполнено(ДокументОбъект.СуммаВозвратаДолга) Тогда 
			РасходПоСуммеСписания = Окр(ДокументОбъект.СуммаСписания * ДокументОбъект.СуммаПолучено / ДокументОбъект.СуммаВозвратаДолга, 14,2);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.СуммаПроцентов) И ЗначениеЗаполнено(ДокументОбъект.СуммаПроцентовВВалютеДолга) Тогда 
			РасходПоСуммеСписания = Окр(ДокументОбъект.СуммаСписания * ДокументОбъект.СуммаПроцентов / ДокументОбъект.СуммаПроцентовВВалютеДолга, 14,2);
		Иначе
			КурсИКратностьДолга    = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДокументОбъект.ВалютаДолга, ДокументОбъект.Дата, ВалютаУчета);
			КурсИКратностьКошелька = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДокументОбъект.ВалютаПолучено, ДокументОбъект.Дата, ВалютаУчета);
			РасходПоСуммеСписания = РаботаСКурсамиВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(ДокументОбъект.СуммаСписания,
					ДокументОбъект.ВалютаДолга, ДокументОбъект.ВалютаПолучено, КурсИКратностьДолга.Курс, КурсИКратностьКошелька.Курс,
					КурсИКратностьДолга.Кратность, КурсИКратностьКошелька.Кратность);
		КонецЕсли; 
		ДокументОбъект.СуммаДополнительныхРасходов = ДокументОбъект.СуммаДополнительныхРасходов + РасходПоСуммеСписания;
	КонецЕсли; 
	
	ДокументОбъект.ВсегоСуммаПоступления = ДокументОбъект.СуммаПолучено + ДокументОбъект.СуммаПроцентов;
	
	Если ДокументОбъект.ЭтоШаблон Тогда
		Возврат;
	КонецЕсли; 

	Если ЗначениеЗаполнено(ДокументОбъект.СуммаСписания) И НЕ ЗначениеЗаполнено(ДокументОбъект.СуммаПолучено) Тогда
		ДокументОбъект.ОписаниеОперации = НСтр("ru = 'Списан остаток долга [%1]'");
		ДокументОбъект.ОписаниеОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ДокументОбъект.ОписаниеОперации, ДокументОбъект.Долг);
	Иначе
		ДокументОбъект.ОписаниеОперации = НСтр("ru = 'Нам вернули долг [%1] в кошелек [%2]'");
		ДокументОбъект.ОписаниеОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ДокументОбъект.ОписаниеОперации, ДокументОбъект.Долг, ДокументОбъект.Кошелек);
		Если ЗначениеЗаполнено(ДокументОбъект.СуммаСписания) Тогда
			ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + НСтр("ru = '. Остаток долга списан'");
		КонецЕсли; 
	КонецЕсли; 
	Если ДокументОбъект.ДополнительныеРасходы.Количество() > 0 Тогда
		СписокРасходов = ДокументОбъект.ДополнительныеРасходы.Выгрузить(,"СтатьяРасхода");
		СписокРасходов.Свернуть("СтатьяРасхода");
		ОписаниеРасходов = "";
		Для Каждого СтрокаРасхода Из СписокРасходов Цикл
			Если ЗначениеЗаполнено(СтрокаРасхода.СтатьяРасхода) Тогда
				ОписаниеРасходов = ОписаниеРасходов + ?(ОписаниеРасходов = "", "", ", ") + СтрокаРасхода.СтатьяРасхода;
			КонецЕсли; 
		КонецЦикла; 
		Если ЗначениеЗаполнено(ОписаниеРасходов) Тогда
			ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + ". " + НСтр("ru = 'Доп. расходы'") + ": " + ОписаниеРасходов;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДокументОбъект.Комментарий) Тогда
		ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + " (" + ДокументОбъект.Комментарий + ")";
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("ЭтоШаблон");
	Поля.Добавить("ОписаниеОперации");
	Поля.Добавить("ВсегоСуммаПоступления");
	Поля.Добавить("ВалютаПолучено");
	Поля.Добавить("Кошелек");
	Поля.Добавить("Контакт");
	Поля.Добавить("Долг");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Данные.ЭтоШаблон Тогда
		
		Представление = ДеньгиКлиентСервер.СокращенноеПредставление(Данные.ОписаниеОперации, 250, Ложь);
		
	Иначе
		
		Представление = НСтр("ru='Нам вернули долг от'") + " " + Формат(Данные.Дата, "ДФ='дд.ММ.гггг (ЧЧ:мм)'")
				+ ": " + Формат(Данные.ВсегоСуммаПоступления, "ЧДЦ=2; ЧН=0.00") + " " + Данные.ВалютаПолучено
				+ ?(ЗначениеЗаполнено(Данные.Контакт), " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Контакт)) , "")
				+ ?(ЗначениеЗаполнено(Данные.Долг), " " + НСтр("ru='в счет'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Долг)) , "")
				+ " " + НСтр("ru='в'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Кошелек))
				; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		РаботаСФормамиДокументов.ОпределитьФормуСпискаДляВыбора(Тип("ДокументСсылка.НамВернулиДолг"), Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВыборкиДляФормированияДвижений(СсылкаНаДокумент, ДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("ВалютаУчета", ДополнительныеСвойства.ВалютаУчета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеквизитыДокумента.Ссылка КАК Регистратор,
	|	РеквизитыДокумента.Дата КАК Период,
	|	РеквизитыДокумента.Долг,
	|	РеквизитыДокумента.СуммаВозвратаДолга,
	|	РеквизитыДокумента.ВалютаДолга,
	|	РеквизитыДокумента.СуммаПолучено,
	|	ЕСТЬNULL(ПараметрыГрафика.КошелекДляПогашения.Валюта, РеквизитыДокумента.ВалютаПолучено) КАК ВалютаПолучено,
	|	ЕСТЬNULL(ПараметрыГрафика.КошелекДляПогашения, РеквизитыДокумента.Кошелек) КАК Кошелек,
	|	ЕСТЬNULL(ПараметрыГрафика.КошелекДляПогашения.ИспользоватьДляНакоплений, ЕСТЬNULL(РеквизитыДокумента.Кошелек.ИспользоватьДляНакоплений, ЛОЖЬ)) КАК ИспользоватьДляНакоплений,
	|	РеквизитыДокумента.Комментарий,
	|	РеквизитыДокумента.СуммаСписания,
	|	РеквизитыДокумента.СтатьяСписания,
	|	РеквизитыДокумента.ФинансоваяЦель,
	|	РеквизитыДокумента.СуммаПроцентов,
	|	РеквизитыДокумента.СуммаПроцентовВВалютеДолга,
	|	РеквизитыДокумента.СтатьяДоходовПоПроцентам,
	|	РеквизитыДокумента.АналитикаСтатьиДоходовПоПроцентам,
	|	РеквизитыДокумента.АналитикаСтатьиСписания,
	|	РеквизитыДокумента.ЭтоШаблон,
	|	РеквизитыДокумента.Проведен,
	|	ЕСТЬNULL(РеквизитыДокумента.Кошелек.ТипСчета, ЗНАЧЕНИЕ(Справочник.КошелькиИсчета.ПустаяСсылка)) КАК ТипСчета
	|ИЗ
	|	Документ.НамВернулиДолг КАК РеквизитыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыГрафикаПогашенияДолгов КАК ПараметрыГрафика
	|		ПО РеквизитыДокумента.Ссылка = ПараметрыГрафика.ШаблонОперации
	|			И (РеквизитыДокумента.ЭтоШаблон)
	|ГДЕ
	|	РеквизитыДокумента.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДопРасходы.Ссылка КАК Регистратор,
	|	ДопРасходы.Ссылка.Дата КАК Период,
	|	ДопРасходы.КошелекРасхода,
	|	ДопРасходы.СтатьяРасхода,
	|	ДопРасходы.СуммаРасхода,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.Валюта, &ВалютаУчета) КАК ВалютаКошелька,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакоплений,
	|	ЕСТЬNULL(ДопРасходы.Ссылка.Комментарий, """") КАК Комментарий,
	|	ДопРасходы.АналитикаСтатьи,
	|	ДопРасходы.ФинансоваяЦель,
	|	ЕСТЬNULL(ДопРасходы.Ссылка.ЭтоШаблон, ЛОЖЬ) КАК ЭтоШаблон,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ТипСчета, ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Наличность)) КАК ТипСчета
	|ИЗ
	|	Документ.НамВернулиДолг.ДополнительныеРасходы КАК ДопРасходы
	|ГДЕ
	|	ДопРасходы.Ссылка В(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор,
	|	ДопРасходы.НомерСтроки";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	СтруктураТаблиц = Новый Структура("Основное, ДополнительныеРасходы, ОбъектыОперации");
	СтруктураТаблиц.Основное              = ПакетРезультатов[0].Выгрузить();
	СтруктураТаблиц.ДополнительныеРасходы = ПакетРезультатов[1].Выгрузить();
	
	// Таблицы для записи объектов операции и фактических оборотов бюджета
	ТаблицаНабора = ОбслуживаниеДокументов.НоваяТаблицаРегистраОбъектыОпераций();
	Если СтруктураТаблиц.Основное.Количество() > 0 И 
			(СтруктураТаблиц.Основное[0].ЭтоШаблон ИЛИ СтруктураТаблиц.Основное[0].Проведен ИЛИ ДополнительныеСвойства.ЭтоПлановаяОперация) Тогда
		ОборотыБюджета = ОбслуживаниеДокументов.НоваяТаблицаРегистровОборотовБюджета();
		ОборотыБюджета.Колонки.Добавить("Примечание");
	Иначе
		ОборотыБюджета = Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.Основное Цикл
		
		СтатьяДолг = ?(ЗначениеЗаполнено(СтрокаДокумента.Долг), СтрокаДокумента.Долг, Справочники.ГрафыБюджета.ВозвратВыданногоЗайма);
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		Если СтрокаДокумента.ИспользоватьДляНакоплений = Истина Тогда
			ФинЦель = ?(Не ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦель), Справочники.ФинансовыеЦели.ОбщиеНакопления, СтрокаДокумента.ФинансоваяЦель);
		Иначе
			ФинЦель = Справочники.ФинансовыеЦели.ПустаяСсылка();
		КонецЕсли;
		
		ВалютаПолучено = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаПолучено), СтрокаДокумента.ВалютаПолучено, ДополнительныеСвойства.ВалютаУчета);
		ВалютаДолга    = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаДолга), СтрокаДокумента.ВалютаДолга, ДополнительныеСвойства.ВалютаУчета);
		
		// Основная сумма долга
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенег;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.Кошелек;
		ЗаписьНабора1.ФинансоваяЦель    = ФинЦель;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
		ЗаписьНабора1.СуммаПоступления  = СтрокаДокумента.СуммаПолучено;
		ЗаписьНабора1.Валюта            = ВалютаПолучено;
		
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов;
		ЗаписьНабора2.ПредметУчета      = СтрокаДокумента.Долг;
		ЗаписьНабора2.ФинансоваяЦель    = ФинЦель;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
		ЗаписьНабора2.СуммаПоступления  = СтрокаДокумента.СуммаВозвратаДолга;
		ЗаписьНабора2.Валюта            = ВалютаДолга;
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено И СтрокаДокумента.СуммаПолучено <> 0 Тогда
			
			ЗаписьНабора = ОборотыБюджета.Добавить();
			ЗаписьНабора.Период           = СтрокаДокумента.Период;
			ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
			ЗаписьНабора.РазделБюджета    = ФинЦель;
			ЗаписьНабора.СтатьяБюджета    = СтатьяДолг;
			ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			ЗаписьНабора.Валюта           = ВалютаПолучено;
			ЗаписьНабора.Кошелек          = СтрокаДокумента.Кошелек;
			ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаПолучено;
			ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			ЗаписьНабора.Примечание       = "Основной долг";
			
			// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
			Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.Кошелек;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
				ЗаписьНабора.Валюта           = ВалютаПолучено;
				ЗаписьНабора.Кошелек          = Неопределено;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаПолучено;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				ЗаписьНабора.Примечание       = "Основной долг (кредитная карта)";
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинЦель) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
			
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		// Сумма списания долга
		Если ЗначениеЗаполнено(СтрокаДокумента.СуммаСписания) Тогда
			
			ЗаписьНабора3 = ТаблицаНабора.Добавить();
			ЗаписьНабора3.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора3.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора3.Операция          = СсылкаНаДокумент;
			ЗаписьНабора3.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
			ЗаписьНабора3.ПредметУчета      = СтрокаДокумента.СтатьяСписания;
			ЗаписьНабора3.ФинансоваяЦель    = ФинЦель;
			ЗаписьНабора3.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			ЗаписьНабора3.СуммаСписания     = СтрокаДокумента.СуммаСписания;
			ЗаписьНабора3.Валюта            = ВалютаДолга;
			
			ЗаписьНабора4 = ТаблицаНабора.Добавить();
			ЗаписьНабора4.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора4.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора4.Операция          = СсылкаНаДокумент;
			ЗаписьНабора4.РазделУчета       = ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов;
			ЗаписьНабора4.ПредметУчета      = СтрокаДокумента.Долг;
			ЗаписьНабора4.ФинансоваяЦель    = ФинЦель;
			ЗаписьНабора4.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			ЗаписьНабора4.СуммаСписания     = СтрокаДокумента.СуммаСписания;
			ЗаписьНабора4.Валюта            = ВалютаДолга;
			
			Если ЗначениеЗаполнено(ФинЦель) Тогда
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора3);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
				
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора4);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
			КонецЕсли; 
			
		КонецЕсли; 
		
		// Сумма процентов за пользование долгом
		Если ЗначениеЗаполнено(СтрокаДокумента.СуммаПроцентов) Тогда
			
			ЗаписьНабора5 = ТаблицаНабора.Добавить();
			ЗаписьНабора5.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора5.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора5.Операция          = СсылкаНаДокумент;
			ЗаписьНабора5.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
			ЗаписьНабора5.ПредметУчета      = СтрокаДокумента.СтатьяДоходовПоПроцентам;
			ЗаписьНабора5.ФинансоваяЦель    = ФинЦель;
			ЗаписьНабора5.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			ЗаписьНабора5.СуммаПоступления  = СтрокаДокумента.СуммаПроцентовВВалютеДолга;
			ЗаписьНабора5.Валюта            = ВалютаДолга;
			
			ЗаписьНабора6 = ТаблицаНабора.Добавить();
			ЗаписьНабора6.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора6.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора6.Операция          = СсылкаНаДокумент;
			ЗаписьНабора6.РазделУчета       = СчетДенег;
			ЗаписьНабора6.ПредметУчета      = СтрокаДокумента.Кошелек;
			ЗаписьНабора6.ФинансоваяЦель    = ФинЦель;
			ЗаписьНабора6.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			ЗаписьНабора6.СуммаПоступления  = СтрокаДокумента.СуммаПроцентов;
			ЗаписьНабора6.Валюта            = ВалютаПолучено;
			
			Если ЗначениеЗаполнено(ФинЦель) Тогда
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора5);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
				
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора6);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
			КонецЕсли; 
			
			// Фактические обороты бюджета
			Если ОборотыБюджета <> Неопределено Тогда
				
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтатьяДолг;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаПолучено;
				ЗаписьНабора.Кошелек          = СтрокаДокумента.Кошелек;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаПроцентов;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				ЗаписьНабора.Примечание       = "Проценты";
				
				// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
				Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
					ЗаписьНабора = ОборотыБюджета.Добавить();
					ЗаписьНабора.Период           = СтрокаДокумента.Период;
					ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
					ЗаписьНабора.РазделБюджета    = ФинЦель;
					ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.Кошелек;
					ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
					ЗаписьНабора.Валюта           = ВалютаПолучено;
					ЗаписьНабора.Кошелек          = Неопределено;
					ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаПроцентов;
					ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
					ЗаписьНабора.Примечание       = "Проценты (кредитная карта)";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Если валюта долга отличается от валюты кошелька
		Если ВалютаПолучено <> ВалютаДолга Тогда
			
			// Добавляем строки с обменом валют
			ЗаписьНабора7 = ТаблицаНабора.Добавить();
			ЗаписьНабора7.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора7.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора7.Операция          = СсылкаНаДокумент;
			ЗаписьНабора7.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
			ЗаписьНабора7.ПредметУчета      = Справочники.СтатьиДоходов.ОбменВалюты;
			ЗаписьНабора7.ФинансоваяЦель    = ФинЦель;
			ЗаписьНабора7.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
			ЗаписьНабора7.СуммаПоступления  = СтрокаДокумента.СуммаПолучено + СтрокаДокумента.СуммаПроцентов;
			ЗаписьНабора7.Валюта            = ВалютаПолучено;
			
						
			ЗаписьНабора8 = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора8, ЗаписьНабора7);
			ЗаписьНабора8.ПредметУчета      = Справочники.СтатьиРасходов.ОбменВалюты;
			ЗаписьНабора8.СуммаПоступления  = 0;
			ЗаписьНабора8.СуммаСписания     = СтрокаДокумента.СуммаВозвратаДолга + СтрокаДокумента.СуммаПроцентовВВалютеДолга;
			ЗаписьНабора8.Валюта            = ВалютаДолга;
			
			Если ЗначениеЗаполнено(ФинЦель) Тогда
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора7);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
				
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора8);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
			КонецЕсли; 
		
		КонецЕсли; 
		
	КонецЦикла; 
	
	// Дополнительные расходы по возврату долга
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.ДополнительныеРасходы Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		ФинЦель = ?(ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦель) И СтрокаДокумента.ФинансоваяЦель <> Справочники.ФинансовыеЦели.ОбщиеНакопления, 
			СтрокаДокумента.ФинансоваяЦель, Неопределено);
		ВалютаКошелька = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаКошелька), СтрокаДокумента.ВалютаКошелька, ДополнительныеСвойства.ВалютаУчета);
			
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенег;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.КошелекРасхода;
		ЗаписьНабора1.ФинансоваяЦель    = Финцель;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
		ЗаписьНабора1.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора1.Валюта            = ВалютаКошелька;
		
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
		ЗаписьНабора2.ПредметУчета      = СтрокаДокумента.СтатьяРасхода;
		ЗаписьНабора2.ФинансоваяЦель    = Финцель;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Поступление;
		ЗаписьНабора2.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора2.Валюта            = ВалютаКошелька;
		
		Если ЗначениеЗаполнено(ФинЦель) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
			
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено Тогда
			
			ЗаписьНабора = ОборотыБюджета.Добавить();
			ЗаписьНабора.Период           = СтрокаДокумента.Период;
			ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
			ЗаписьНабора.РазделБюджета    = ФинЦель;
			ЗаписьНабора.СтатьяБюджета    = ?(ЗначениеЗаполнено(СтрокаДокумента.СтатьяРасхода), СтрокаДокумента.СтатьяРасхода, Справочники.СтатьиРасходов.ПрочиеРасходы);
			ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
			ЗаписьНабора.Валюта           = ВалютаКошелька;
			ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекРасхода;
			ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
			ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			ЗаписьНабора.Примечание       = "Доп.расходы";
			
			// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
			Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекРасхода;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаКошелька;
				ЗаписьНабора.Кошелек          = Неопределено;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				ЗаписьНабора.Примечание       = "Доп.расходы (кредитная карта)";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаНабора.Свернуть("Дата, ЭтоШаблон, Операция, РазделУчета, ПредметУчета, ФинансоваяЦель, ТипПоказателя, Валюта", "СуммаПоступления, СуммаСписания");
	СтруктураТаблиц.Вставить("ОбъектыОперации", ТаблицаНабора);
	
	Если ОборотыБюджета <> Неопределено Тогда
		ОборотыБюджета.Свернуть("Период,Регистратор,РазделБюджета,СтатьяБюджета,ТипПоказателя,Валюта,Кошелек,Комментарий,Примечание", "Сумма");
		СтруктураТаблиц.Вставить("ОборотыБюджета", ОборотыБюджета);
	КонецЕсли;
	
	Возврат СтруктураТаблиц;

КонецФункции

#КонецОбласти	


#КонецЕсли