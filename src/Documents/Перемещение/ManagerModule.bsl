#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

// Записывает в регистр сведений информацию об использованных в документе объектах учета.
//	Данная информация является вспомогательной и не зависит от проведенности или пометки на удаление документа
//
//Параметры:
//	СсылкаНаДокумент       - ДокументСсылка.Перевод - регистратор движений
//	ДополнительныеСвойства - Структура - сордержит дополнительные свойства документа
//	ТаблицыДокумента       - Структура - набор таблиц с информацией о документе (см. СтруктураТаблицФормированияДвижений())
//
Процедура СформироватьДвиженияДокумента(СсылкаНаДокумент, ДвиженияДокумента, ДополнительныеСвойства = Неопределено) Экспорт

	// Получаем таблицу для формирования движений
	Если ТипЗнч(ДополнительныеСвойства) <> Тип("Структура") Или Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ПроверитьДополнительныеСвойстваОперации(СсылкаНаДокумент, ДополнительныеСвойства, Истина);
	КонецЕсли;
	ТаблицыДокумента = ДополнительныеСвойства.ТаблицыДокумента;
	
	// Проводки для самого перевода
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.Основное Цикл
		
		СчетОткуда = ?(СтрокаДокумента.ИспользоватьДляНакопленийОткуда = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		СчетКуда   = ?(СтрокаДокумента.ИспользоватьДляНакопленийКуда = Истина,   ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		// Добавляем проводки с учетом валют кредита и кошелька:
		РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
			СчетКуда, СтрокаДокумента.КошелекКуда, СтрокаДокумента.ФинансоваяЦельКуда, СтрокаДокумента.СуммаОперации, СтрокаДокумента.ВалютаОперации,
			СчетОткуда, СтрокаДокумента.КошелекОткуда, СтрокаДокумента.ФинансоваяЦельОткуда, СтрокаДокумента.СуммаОперации, СтрокаДокумента.ВалютаОперации,
			СтрокаДокумента.Комментарий);
		
	КонецЦикла; 

	// Проводки по доп.расходам на перевод средств
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.ДополнительныеРасходы Цикл
		
		СчетДенег   = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина,   ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		// Добавляем проводки с учетом валют кошельков:
		РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
			ПланыСчетов.РазделыУчета.Капитал, СтрокаДокумента.СтатьяРасхода, СтрокаДокумента.АналитикаСтатьи, СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СчетДенег, СтрокаДокумента.КошелекРасхода, СтрокаДокумента.ФинансоваяЦель, СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СтрокаДокумента.Комментарий);
		
	КонецЦикла; 
	
	// Записываем движения в регистр фактических оборотов бюджета
	Если ТаблицыДокумента.Свойство("ОборотыБюджета") Тогда
		ДвиженияДокумента.ФактическиеОборотыБюджета.Загрузить(ТаблицыДокумента.ОборотыБюджета);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру ДополнительныеСвойства значениями, необходимыми для дальнейшей обработки документа
//
//Параметры:
//	Операция               - ссылка или объект документа, зависит от контекста вызова этой процедуры
//	ДополнительныеСвойства - Структура или Неопределено - сордержит дополнительные свойства документа
//	ПроверятьТаблицыДокумента - Булево - заполнять ли таблицы для формирования движений
//
Процедура ПроверитьДополнительныеСвойстваОперации(Операция, ДополнительныеСвойства, ПроверятьТаблицыДокумента = Ложь) Экспорт

	// Выполняем проверку, одинаковую для всех документов
	ОбслуживаниеДокументов.ОбщаяПроверкаДополнительныхСвойствДокумента(Операция, ДополнительныеСвойства);
	
	Если ПроверятьТаблицыДокумента И Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ДополнительныеСвойства.Вставить("ТаблицыДокумента", ПолучитьВыборкиДляФормированияДвижений(Операция.Ссылка, ДополнительныеСвойства));
	КонецЕсли;
	
КонецПроцедуры

// Обновляет итоговые суммы и описание операции
Процедура ОбновитьИтоговыеСуммыДокумента(ДокументОбъект) Экспорт
	
	ДокументОбъект.РасходыИзКошелькаПоступления = 0;
	ДокументОбъект.РасходыИзКошелькаСписания    = 0;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.СчетОткуда) Тогда
		Если ДокументОбъект.КошелекОткуда.ИспользоватьДляНакоплений Тогда
			ДокументОбъект.СчетОткуда = ПланыСчетов.РазделыУчета.Накопления;
		Иначе
			ДокументОбъект.СчетОткуда = ПланыСчетов.РазделыУчета.СвободныеДеньги;
		КонецЕсли; 
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.СчетКуда) Тогда
		Если ДокументОбъект.КошелекКуда.ИспользоватьДляНакоплений Тогда
			ДокументОбъект.СчетКуда = ПланыСчетов.РазделыУчета.Накопления;
		Иначе
			ДокументОбъект.СчетКуда = ПланыСчетов.РазделыУчета.СвободныеДеньги;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ДокументОбъект.КошелекОткуда.ИспользоватьДляНакоплений И НЕ ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦельОткуда) Тогда
		ДокументОбъект.ФинансоваяЦельОткуда = Справочники.ФинансовыеЦели.ОбщиеНакопления;
	ИначеЕсли Не ДокументОбъект.КошелекОткуда.ИспользоватьДляНакоплений И ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦельОткуда) Тогда
		ДокументОбъект.ФинансоваяЦельОткуда = Справочники.ФинансовыеЦели.ПустаяСсылка();
	КонецЕсли;
	
	Если ДокументОбъект.КошелекКуда.ИспользоватьДляНакоплений И НЕ ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦельКуда) Тогда
		ДокументОбъект.ФинансоваяЦельКуда = Справочники.ФинансовыеЦели.ОбщиеНакопления;
	ИначеЕсли Не ДокументОбъект.КошелекКуда.ИспользоватьДляНакоплений И ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦельКуда) Тогда
		ДокументОбъект.ФинансоваяЦельКуда = Справочники.ФинансовыеЦели.ПустаяСсылка();
	КонецЕсли;
	
	ДокументОбъект.ВалютаОперации           = ?(ЗначениеЗаполнено(ДокументОбъект.КошелекОткуда), ДокументОбъект.КошелекОткуда.Валюта, Константы.ВалютаУчета.Получить());
	СписокРасходов = Новый Соответствие;
	РасходыИзКошелькаПоступления   = 0;
	СуммаРасходовВКошелькеОткуда = 0;
	Для Каждого СтрокаРасхода Из ДокументОбъект.ДополнительныеРасходы Цикл
		Если СтрокаРасхода.КошелекРасхода = ДокументОбъект.КошелекОткуда И ЗначениеЗаполнено(СтрокаРасхода.СтатьяРасхода) Тогда
			СписокРасходов.Вставить(СтрокаРасхода.СтатьяРасхода);
			ДокументОбъект.РасходыИзКошелькаСписания = ДокументОбъект.РасходыИзКошелькаСписания + СтрокаРасхода.СуммаРасхода;
		ИначеЕсли СтрокаРасхода.КошелекРасхода = ДокументОбъект.КошелекКуда И ЗначениеЗаполнено(СтрокаРасхода.СтатьяРасхода) Тогда
			СписокРасходов.Вставить(СтрокаРасхода.СтатьяРасхода);
			ДокументОбъект.РасходыИзКошелькаПоступления = ДокументОбъект.РасходыИзКошелькаПоступления + СтрокаРасхода.СуммаРасхода;
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(СтрокаРасхода.ФинансоваяЦель) Тогда
			Если СтрокаРасхода.КошелекРасхода = ДокументОбъект.КошелекОткуда Тогда
				СтрокаРасхода.ФинансоваяЦель = ДокументОбъект.ФинансоваяЦельОткуда;
			Иначе
				СтрокаРасхода.ФинансоваяЦель = ДокументОбъект.ФинансоваяЦельКуда;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла; 
	
	ДокументОбъект.СписаноСУчетомРасходов   = ДокументОбъект.СуммаОперации + ДокументОбъект.РасходыИзКошелькаПоступления + ДокументОбъект.РасходыИзКошелькаСписания;
	ДокументОбъект.ПолученоСУчетомРасходов  = ДокументОбъект.СуммаОперации - ДокументОбъект.РасходыИзКошелькаПоступления;
	
	Если ДокументОбъект.ЭтоШаблон Тогда
		Возврат;
	КонецЕсли; 

	ДокументОбъект.ОписаниеОперации = НСтр("ru = 'Перемещение из кошелька [%1] в кошелек [%2]'");
	ДокументОбъект.ОписаниеОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ДокументОбъект.ОписаниеОперации, ДокументОбъект.КошелекОткуда, ДокументОбъект.КошелекКуда);
	
	Если СписокРасходов.Количество() > 0 Тогда
		ОписаниеРасходов = "";
		Для Каждого ЭлементСписка Из СписокРасходов Цикл
			ОписаниеРасходов = ОписаниеРасходов + ?(ОписаниеРасходов = "", "", ", ")
				+ ЭлементСписка.Ключ; 
		КонецЦикла; 
		ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + ". " + НСтр("ru = 'Доп. расходы'") + ": " + ОписаниеРасходов; 
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("ЭтоШаблон");
	Поля.Добавить("ОписаниеОперации");
	Поля.Добавить("КошелекОткуда");
	Поля.Добавить("КошелекКуда");
	Поля.Добавить("СуммаОперации");
	Поля.Добавить("ВалютаОперации");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Данные.ЭтоШаблон Тогда
		
		Представление = ДеньгиКлиентСервер.СокращенноеПредставление(Данные.ОписаниеОперации, 250, Ложь);
		
	Иначе
		
		Представление = НСтр("ru='Перемещение от'") + " " + Формат(Данные.Дата, "ДФ='дд.ММ.гггг (ЧЧ:мм)'")
				+ ": " + Формат(Данные.СуммаОперации, "ЧДЦ=2; ЧН=0.00") + " " + Данные.ВалютаОперации
				+ " " + НСтр("ru='из'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.КошелекОткуда))
				+ " " + НСтр("ru='в'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.КошелекКуда)); 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		РаботаСФормамиДокументов.ОпределитьФормуСпискаДляВыбора(Тип("ДокументСсылка.Перемещение"), Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции


Функция ПолучитьВыборкиДляФормированияДвижений(СсылкаНаДокумент, ДополнительныеСвойства) 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("ВалютаУчета", ДополнительныеСвойства.ВалютаУчета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеквизитыДокумента.Ссылка КАК Регистратор,
	|	РеквизитыДокумента.Дата КАК Период,
	|	РеквизитыДокумента.СчетКуда,
	|	РеквизитыДокумента.КошелекКуда,
	|	РеквизитыДокумента.ФинансоваяЦельКуда,
	|	ЕСТЬNULL(РеквизитыДокумента.КошелекКуда.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакопленийКуда,
	|	РеквизитыДокумента.СчетОткуда,
	|	РеквизитыДокумента.КошелекОткуда,
	|	РеквизитыДокумента.ФинансоваяЦельОткуда,
	|	ЕСТЬNULL(РеквизитыДокумента.КошелекОткуда.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакопленийОткуда,
	|	РеквизитыДокумента.СуммаОперации,
	|	РеквизитыДокумента.ВалютаОперации,
	|	РеквизитыДокумента.Комментарий,
	|	РеквизитыДокумента.ЭтоШаблон,
	|	ЕСТЬNULL(РеквизитыДокумента.КошелекОткуда.ТипСчета, ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Наличность)) КАК ОткудаТипСчета,
	|	ЕСТЬNULL(РеквизитыДокумента.КошелекКуда.ТипСчета, ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Наличность)) КАК КудаТипСчета,
	|	РеквизитыДокумента.Проведен
	|ИЗ
	|	Документ.Перемещение КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДопРасходы.Ссылка КАК Регистратор,
	|	ДопРасходы.Ссылка.Дата КАК Период,
	|	ДопРасходы.КошелекРасхода,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакоплений,
	|	ДопРасходы.СтатьяРасхода,
	|	ДопРасходы.ФинансоваяЦель,
	|	ДопРасходы.СуммаРасхода,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.Валюта, &ВалютаУчета) КАК ВалютаКошелька,
	|	ЕСТЬNULL(ДопРасходы.Ссылка.Комментарий, """") КАК Комментарий,
	|	ДопРасходы.АналитикаСтатьи,
	|	ЕСТЬNULL(ДопРасходы.Ссылка.ЭтоШаблон, ЛОЖЬ) КАК ЭтоШаблон,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ТипСчета, ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Наличность)) КАК ТипСчета
	|ИЗ
	|	Документ.Перемещение.ДополнительныеРасходы КАК ДопРасходы
	|ГДЕ
	|	ДопРасходы.Ссылка В(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор,
	|	ДопРасходы.НомерСтроки";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	СтруктураТаблиц = Новый Структура("Основное, ДополнительныеРасходы, ОбъектыОперации");
	СтруктураТаблиц.Основное              = ПакетРезультатов[0].Выгрузить();
	СтруктураТаблиц.ДополнительныеРасходы = ПакетРезультатов[1].Выгрузить();
	
	// Таблицы для записи объектов операции и фактических оборотов бюджета
	ТаблицаНабора = ОбслуживаниеДокументов.НоваяТаблицаРегистраОбъектыОпераций();
	Если СтруктураТаблиц.Основное.Количество() > 0 И СтруктураТаблиц.Основное[0].Проведен Тогда
		ОборотыБюджета = ОбслуживаниеДокументов.НоваяТаблицаРегистровОборотовБюджета();
	Иначе
		ОборотыБюджета = Неопределено;
	КонецЕсли;
	
	// Таблица для формирования движений шаблонов и плановой операции
	// В перемещении и обмене валют для формирования движений плановых операций невозможно использовать фактические обороты бюджета, т.к. они не 
	//	учитывают перемещения между кошельками в пределах одного раздела бюджета, а для кредитных карт создают дополнительные обороты.
	ЭтоШаблон = СтруктураТаблиц.Основное.Количество() > 0 И СтруктураТаблиц.Основное[0].ЭтоШаблон;
	ОборотыПлановыхОпераций = ОбслуживаниеДокументов.НоваяТаблицаРегистровОборотовБюджета();
	
	КошелекОткуда = Неопределено;
	КошелекКуда   = Неопределено;
	
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.Основное Цикл
		
		КошелекОткуда = СтрокаДокумента.КошелекОткуда;
		КошелекКуда   = СтрокаДокумента.КошелекКуда;
		
		СчетДенегОткуда = ?(СтрокаДокумента.ИспользоватьДляНакопленийОткуда = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		Если СтрокаДокумента.ИспользоватьДляНакопленийОткуда Тогда
			ФинЦельОткуда = ?(Не ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦельОткуда), Справочники.ФинансовыеЦели.ОбщиеНакопления, СтрокаДокумента.ФинансоваяЦельОткуда);
		Иначе
			ФинЦельОткуда = Справочники.ФинансовыеЦели.ПустаяСсылка();
		КонецЕсли;
		
		ВалютаОперации      = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаОперации), СтрокаДокумента.ВалютаОперации, ДополнительныеСвойства.ВалютаУчета);
		
		СчетДенегКуда = ?(СтрокаДокумента.ИспользоватьДляНакопленийКуда = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		Если СтрокаДокумента.ИспользоватьДляНакопленийКуда Тогда
			ФинЦельКуда = ?(Не ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦельКуда), Справочники.ФинансовыеЦели.ОбщиеНакопления, СтрокаДокумента.ФинансоваяЦельКуда);
		Иначе
			ФинЦельКуда = Справочники.ФинансовыеЦели.ПустаяСсылка();
		КонецЕсли;
			
		КредиткаИсточник   = СтрокаДокумента.ОткудаТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная;
		КредиткаПолучатель = СтрокаДокумента.КудаТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная;
		
		// списание суммы
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенегОткуда;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.КошелекОткуда;
		ЗаписьНабора1.ФинансоваяЦель    = ФинЦельОткуда;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
		ЗаписьНабора1.СуммаСписания     = СтрокаДокумента.СуммаОперации;
		ЗаписьНабора1.Валюта            = ВалютаОперации;
		
		// Оборот плановой операции
		Если ЭтоШаблон Или ДополнительныеСвойства.ЭтоПлановаяОперация Тогда
			
			СтрокаПлановойОперации = ОборотыПлановыхОпераций.Добавить();
			СтрокаПлановойОперации.Период          = СтрокаДокумента.Период;
			СтрокаПлановойОперации.Регистратор     = СсылкаНаДокумент;
			СтрокаПлановойОперации.РазделБюджета   = ФинЦельОткуда;
			СтрокаПлановойОперации.Валюта          = ВалютаОперации;
			СтрокаПлановойОперации.Кошелек         = СтрокаДокумента.КошелекОткуда;
			СтрокаПлановойОперации.ТипПоказателя   = Перечисления.ТипыБюджетныхПоказателей.Списание;
			
			// Статья бюджета зависит от типа счета и перемещения между финансовыми целями
			Если ЗначениеЗаполнено(ФинЦельКуда) И ФинЦельОткуда <> ФинЦельКуда Тогда
				
				СтрокаПлановойОперации.СтатьяБюджета = ФинЦельКуда;
				
			ИначеЕсли ЗначениеЗаполнено(ФинЦельОткуда) И Не ЗначениеЗаполнено(ФинЦельКуда) Тогда
				
				СтрокаПлановойОперации.СтатьяБюджета = Справочники.ГрафыБюджета.ФинЦельВозвратИзНакопления;
				
			Иначе
				
				// Типы счетов и финансовые цели одинаковы: это перемещение не бюджетируется.
				//Статьей является кошелек назначения
				СтрокаПлановойОперации.СтатьяБюджета   = КошелекКуда;
				
			КонецЕсли;
			
			СтрокаПлановойОперации.Сумма = СтрокаДокумента.СуммаОперации;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинЦельОткуда) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		
		// получение суммы
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = СчетДенегКуда;
		ЗаписьНабора2.ПредметУчета      = СтрокаДокумента.КошелекКуда;
		ЗаписьНабора2.ФинансоваяЦель    = ФинЦельКуда;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
		ЗаписьНабора2.СуммаПоступления  = СтрокаДокумента.СуммаОперации;
		ЗаписьНабора2.Валюта            = ВалютаОперации;
		
		// Оборот плановой операции
		Если ЭтоШаблон Или ДополнительныеСвойства.ЭтоПлановаяОперация Тогда
			
			СтрокаПлановойОперации = ОборотыПлановыхОпераций.Добавить();
			СтрокаПлановойОперации.Период          = СтрокаДокумента.Период;
			СтрокаПлановойОперации.Регистратор     = СсылкаНаДокумент;
			СтрокаПлановойОперации.РазделБюджета   = ФинЦельКуда;
			СтрокаПлановойОперации.Валюта          = ВалютаОперации;
			СтрокаПлановойОперации.Кошелек         = СтрокаДокумента.КошелекКуда;
			СтрокаПлановойОперации.ТипПоказателя   = Перечисления.ТипыБюджетныхПоказателей.Поступление;
			
			// Статья бюджета зависит от типа счета и перемещения между финансовыми целями
			Если ЗначениеЗаполнено(ФинЦельОткуда) Тогда
				
				СтрокаПлановойОперации.СтатьяБюджета = ФинЦельОткуда;
				
			ИначеЕсли ЗначениеЗаполнено(ФинЦельКуда) И Не ЗначениеЗаполнено(ФинЦельОткуда) Тогда
				
				СтрокаПлановойОперации.СтатьяБюджета = Справочники.ГрафыБюджета.ФинЦельПереводВНакопление;
				
			Иначе
				
				// Типы счетов и финансовые цели одинаковы: это перемещение не бюджетируется.
				//Статьей является кошелек назначения
				СтрокаПлановойОперации.СтатьяБюджета   = КошелекОткуда;
				
			КонецЕсли;
			
			СтрокаПлановойОперации.Сумма = СтрокаДокумента.СуммаОперации;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинЦельКуда) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено Тогда
			
			Если СтрокаДокумента.ФинансоваяЦельОткуда <> СтрокаДокумента.ФинансоваяЦельКуда Тогда
				
				// Регистрируем перемещение денег межу разделами бюджета (между фин.целями или финцелями и свободными деньгами)
				
				// Расход из свободных денег или накоплений на цель-источник
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельОткуда;
				ЗаписьНабора.СтатьяБюджета    = ?(ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦельКуда), СтрокаДокумента.ФинансоваяЦельКуда, Справочники.ГрафыБюджета.ФинЦельВозвратИзНакопления);
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
				ЗаписьНабора.Валюта           = ВалютаОперации;
				ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекОткуда;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				
				// Если используется кредитная карта, автоматически добавляем перевод с кредитки в доступные деньги
				Если КредиткаИсточник И Не КредиткаПолучатель Тогда
					ЗаписьНабора = ОборотыБюджета.Добавить();
					ЗаписьНабора.Период           = СтрокаДокумента.Период;
					ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
					ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельОткуда;
					ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекОткуда;
					ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
					ЗаписьНабора.Валюта           = ВалютаОперации;
					ЗаписьНабора.Кошелек          = Неопределено;
					ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
					ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				КонецЕсли;
				
				// Приход в свободные деньги или в накопления на цель-получатель
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельКуда;
				ЗаписьНабора.СтатьяБюджета    = ?(ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦельОткуда), СтрокаДокумента.ФинансоваяЦельОткуда, Справочники.ГрафыБюджета.ФинЦельПереводВНакопление);
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаОперации;
				ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекКуда;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				
				// Если используется кредитная карта, автоматически добавляем перевод из доступных денег на кредитку
				Если КредиткаПолучатель И Не КредиткаИсточник Тогда
					ЗаписьНабора = ОборотыБюджета.Добавить();
					ЗаписьНабора.Период           = СтрокаДокумента.Период;
					ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
					ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельКуда;
					ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекКуда;
					ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
					ЗаписьНабора.Валюта           = ВалютаОперации;
					ЗаписьНабора.Кошелек          = Неопределено;
					ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
					ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				КонецЕсли;
				
			ИначеЕсли СтрокаДокумента.КошелекОткуда <> СтрокаДокумента.КошелекКуда Тогда
				
				Если КредиткаИсточник Тогда
					ЗаписьНабора = ОборотыБюджета.Добавить();
					ЗаписьНабора.Период           = СтрокаДокумента.Период;
					ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
					ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельКуда;
					ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекОткуда;
					ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
					ЗаписьНабора.Валюта           = ВалютаОперации;
					ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекКуда;
					ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
					ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				КонецЕсли;
				
				Если КредиткаПолучатель Тогда
					ЗаписьНабора = ОборотыБюджета.Добавить();
					ЗаписьНабора.Период           = СтрокаДокумента.Период;
					ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
					ЗаписьНабора.РазделБюджета    = СтрокаДокумента.ФинансоваяЦельОткуда;
					ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекКуда;
					ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
					ЗаписьНабора.Валюта           = ВалютаОперации;
					ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекОткуда;
					ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаОперации;
					ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
				КонецЕсли;
				
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; 
	
	// Дополнительные расходы по обмену валюты
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.ДополнительныеРасходы Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		ФинЦель = ?(ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦель) И СтрокаДокумента.ФинансоваяЦель <> Справочники.ФинансовыеЦели.ОбщиеНакопления, 
			СтрокаДокумента.ФинансоваяЦель, Неопределено);
		ВалютаКошелька    = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаКошелька), СтрокаДокумента.ВалютаКошелька, ДополнительныеСвойства.ВалютаУчета);
			
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенег;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.КошелекРасхода;
		ЗаписьНабора1.ФинансоваяЦель    = Финцель;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
		ЗаписьНабора1.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора1.Валюта            = ВалютаКошелька;
		
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
		ЗаписьНабора2.ПредметУчета      = СтрокаДокумента.СтатьяРасхода;
		ЗаписьНабора2.ФинансоваяЦель    = Финцель;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
		ЗаписьНабора2.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора2.Валюта            = ВалютаКошелька;
		
		// Оборот плановой операции
		Если ЭтоШаблон Или ДополнительныеСвойства.ЭтоПлановаяОперация Тогда
			
			СтрокаПлановойОперации = ОборотыПлановыхОпераций.Добавить();
			СтрокаПлановойОперации.Период          = СтрокаДокумента.Период;
			СтрокаПлановойОперации.Регистратор     = СсылкаНаДокумент;
			СтрокаПлановойОперации.РазделБюджета   = Финцель;
			СтрокаПлановойОперации.Валюта          = ВалютаКошелька;
			СтрокаПлановойОперации.Кошелек         = СтрокаДокумента.КошелекРасхода;
			СтрокаПлановойОперации.ТипПоказателя   = Перечисления.ТипыБюджетныхПоказателей.Списание;
			СтрокаПлановойОперации.СтатьяБюджета   = СтрокаДокумента.СтатьяРасхода;
			СтрокаПлановойОперации.Сумма           = СтрокаДокумента.СуммаРасхода;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинЦель) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
			
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено Тогда
			
			ЗаписьНабора = ОборотыБюджета.Добавить();
			ЗаписьНабора.Период           = СтрокаДокумента.Период;
			ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
			ЗаписьНабора.РазделБюджета    = ФинЦель;
			ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.СтатьяРасхода;
			ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
			ЗаписьНабора.Валюта           = ВалютаКошелька;
			ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
			ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			
			// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
			Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекРасхода;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаКошелька;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; 

	ТаблицаНабора.Свернуть("Дата, ЭтоШаблон, Операция, РазделУчета, ПредметУчета, ФинансоваяЦель, ТипПоказателя, Валюта", "СуммаПоступления, СуммаСписания");
	СтруктураТаблиц.Вставить("ОбъектыОперации", ТаблицаНабора);
	
	// Обороты плановой операции
	Если ЭтоШаблон Или ДополнительныеСвойства.ЭтоПлановаяОперация Тогда
		
		ОборотыПлановыхОпераций.Свернуть("Период,Регистратор,РазделБюджета,СтатьяБюджета,ТипПоказателя,Валюта,Кошелек", "Сумма");
		СтруктураТаблиц.Вставить("ОборотыПлановыхОпераций", ОборотыПлановыхОпераций);
		
	КонецЕсли;
	
	Если ОборотыБюджета <> Неопределено Тогда
		ОборотыБюджета.Свернуть("Период,Регистратор,РазделБюджета,СтатьяБюджета,ТипПоказателя,Валюта,Кошелек,Комментарий", "Сумма");
		СтруктураТаблиц.Вставить("ОборотыБюджета", ОборотыБюджета);
	КонецЕсли;
	
	Возврат СтруктураТаблиц;

КонецФункции


#КонецОбласти	


#КонецЕсли