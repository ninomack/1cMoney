#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


Процедура СформироватьДвиженияДокумента(СсылкаНаДокумент, ДвиженияДокумента, ДополнительныеСвойства) Экспорт

	// Получаем таблицу для формирования движений
	Если ТипЗнч("ДополнительныеСвойства") <> Тип("Структура") Или Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ПроверитьДополнительныеСвойстваОперации(СсылкаНаДокумент, ДополнительныеСвойства, Истина);
	КонецЕсли;
	ТаблицыДокумента = ДополнительныеСвойства.ТаблицыДокумента;
	
	// Проводки для самого займа
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.Основное Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		// Добавляем проводки с учетом валют кредита и кошелька:
		РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
			ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов, СтрокаДокумента.Долг, Неопределено, СтрокаДокумента.СуммаЗайма, СтрокаДокумента.ВалютаЗайма,
			СчетДенег, СтрокаДокумента.Кошелек, СтрокаДокумента.ФинансоваяЦель, СтрокаДокумента.СуммаВыдано, СтрокаДокумента.ВалютаВыдано,
			СтрокаДокумента.Комментарий);
		
	КонецЦикла; 

	// Проводки по доп.расходам на выдачу займа
	Для Каждого СтрокаДокумента Из ТаблицыДокумента.ДополнительныеРасходы Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		// Добавляем проводки с учетом валют долга и кошелька:
		РазделыУчета.ДобавитьПроводкуВДвижения(ДвиженияДокумента.ЖурналОпераций, СтрокаДокумента.Период, СтрокаДокумента.Регистратор,
			ПланыСчетов.РазделыУчета.Капитал, СтрокаДокумента.СтатьяРасхода, СтрокаДокумента.АналитикаСтатьи, СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СчетДенег, СтрокаДокумента.КошелекРасхода, СтрокаДокумента.ФинансоваяЦель,  СтрокаДокумента.СуммаРасхода, СтрокаДокумента.ВалютаКошелька,
			СтрокаДокумента.Комментарий);
		
	КонецЦикла;
	
	// Записываем движения в регистр фактических оборотов бюджета
	Если ТаблицыДокумента.Свойство("ОборотыБюджета") Тогда
		ДвиженияДокумента.ФактическиеОборотыБюджета.Загрузить(ТаблицыДокумента.ОборотыБюджета);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет структуру ДополнительныеСвойства значениями, необходимыми для дальнейшей обработки документа
//
//Параметры:
//	Операция               - ссылка или объект документа, зависит от контекста вызова этой процедуры
//	ДополнительныеСвойства - Структура или Неопределено - сордержит дополнительные свойства документа
//	ПроверятьТаблицыДокумента - Булево - заполнять ли таблицы для формирования движений
//
Процедура ПроверитьДополнительныеСвойстваОперации(Операция, ДополнительныеСвойства, ПроверятьТаблицыДокумента = Ложь) Экспорт

	// Выполняем проверку, одинаковую для всех документов
	ОбслуживаниеДокументов.ОбщаяПроверкаДополнительныхСвойствДокумента(Операция, ДополнительныеСвойства);
	
	Если ПроверятьТаблицыДокумента И Не ДополнительныеСвойства.Свойство("ТаблицыДокумента") Тогда
		ДополнительныеСвойства.Вставить("ТаблицыДокумента", ПолучитьВыборкиДляФормированияДвижений(Операция.Ссылка, ДополнительныеСвойства));
	КонецЕсли;
	
КонецПроцедуры

// Обновляет итоговые суммы и описание операции
Процедура ОбновитьИтоговыеСуммыДокумента(ДокументОбъект) Экспорт
	
	ВалютаОперации = ?(ЗначениеЗаполнено(ДокументОбъект.Кошелек), ДокументОбъект.Кошелек.Валюта, Константы.ВалютаУчета.Получить());
	ДокументОбъект.ВалютаВыдано   = ДокументОбъект.Кошелек.Валюта;
	ДокументОбъект.ВалютаЗайма    = ДокументОбъект.Долг.Валюта;
	ДокументОбъект.СуммаДополнительныхРасходов = ДокументОбъект.ДополнительныеРасходы.Итог("СуммаРасхода");
	ДокументОбъект.ВсегоРасход = ДокументОбъект.СуммаДополнительныхРасходов + ДокументОбъект.СуммаВыдано;
	
	// Контакт должен соответствовать долгу
	Если ЗначениеЗаполнено(ДокументОбъект.Долг) Тогда
		ДокументОбъект.Контакт = ДокументОбъект.Долг.Контакт;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ФинансоваяЦель) Тогда
		ДокументОбъект.ФинансоваяЦель = Справочники.ФинансовыеЦели.ОбщиеНакопления;
	КонецЕсли; 
	
	Если ДокументОбъект.ЭтоШаблон Тогда
		Возврат;
	КонецЕсли; 

	ДокументОбъект.ОписаниеОперации = НСтр("ru = 'Мы дали в долг [%1] из кошелька [%2]'");
	ДокументОбъект.ОписаниеОперации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ДокументОбъект.ОписаниеОперации, 
								ДокументОбъект.Долг, ДокументОбъект.Кошелек);
	Если ДокументОбъект.ДополнительныеРасходы.Количество() > 0 Тогда
		ОписаниеРасходов = "";
		Для Каждого СтрокаРасхода Из ДокументОбъект.ДополнительныеРасходы Цикл
			Если ЗначениеЗаполнено(СтрокаРасхода.СтатьяРасхода) Тогда
				ОписаниеРасходов = ОписаниеРасходов + ?(ОписаниеРасходов = "", "", ", ") 
					+ СтрокаРасхода.СтатьяРасхода;
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(СтрокаРасхода.ФинансоваяЦель) Тогда
				СтрокаРасхода.ФинансоваяЦель = Справочники.ФинансовыеЦели.ОбщиеНакопления;
			КонецЕсли; 
		КонецЦикла; 
		Если ЗначениеЗаполнено(ОписаниеРасходов) Тогда
			ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + ". " + НСтр("ru = 'Доп. расходы'") + ": " + ОписаниеРасходов;
		КонецЕсли; 
	КонецЕсли; 
	ДокументОбъект.ОписаниеОперации = ДокументОбъект.ОписаниеОперации + 
				?(ЗначениеЗаполнено(ДокументОбъект.Комментарий), " (" + ДокументОбъект.Комментарий + ")", "");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("ЭтоШаблон");
	Поля.Добавить("ОписаниеОперации");
	Поля.Добавить("Контакт");
	Поля.Добавить("Долг");
	Поля.Добавить("Кошелек");
	Поля.Добавить("СуммаВыдано");
	Поля.Добавить("ВалютаВыдано");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Данные.ЭтоШаблон Тогда
		Представление = ДеньгиКлиентСервер.СокращенноеПредставление(Данные.ОписаниеОперации, 250, Ложь);
	Иначе
		Представление = НСтр("ru='Мы дали в долг от'") + " " + Формат(Данные.Дата, "ДФ='дд.ММ.гггг (ЧЧ:мм)'")
				+ ": " + Формат(Данные.СуммаВыдано, "ЧДЦ=2; ЧН=0.00") + " " + Данные.ВалютаВыдано
				+ ?(ЗначениеЗаполнено(Данные.Контакт), " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Контакт)) , "")
				+ ?(ЗначениеЗаполнено(Данные.Долг), " " + НСтр("ru='в счет'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Долг)) , "")
				+ " " + НСтр("ru='из'") + " " + ДеньгиКлиентСервер.СокращенноеПредставление(Строка(Данные.Кошелек))
				; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
		
		РаботаСФормамиДокументов.ОпределитьФормуСпискаДляВыбора(Тип("ДокументСсылка.МыДалиВДолг"), Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВыборкиДляФормированияДвижений(СсылкаНаДокумент, ДополнительныеСвойства) 

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("ВалютаУчета", ДополнительныеСвойства.ВалютаУчета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеквизитыДокумента.Ссылка КАК Регистратор,
	|	РеквизитыДокумента.Дата КАК Период,
	|	РеквизитыДокумента.Долг,
	|	РеквизитыДокумента.ФинансоваяЦель,
	|	ЕСТЬNULL(РеквизитыДокумента.Кошелек.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакоплений,
	|	РеквизитыДокумента.СуммаЗайма,
	|	РеквизитыДокумента.ВалютаЗайма,
	|	РеквизитыДокумента.СуммаВыдано,
	|	РеквизитыДокумента.ВалютаВыдано,
	|	РеквизитыДокумента.Кошелек,
	|	РеквизитыДокумента.Комментарий,
	|	РеквизитыДокумента.ЭтоШаблон,
	|	ЕСТЬNULL(РеквизитыДокумента.Кошелек.ТипСчета, ЗНАЧЕНИЕ(Справочник.КошелькиИсчета.ПустаяСсылка)) КАК ТипСчета,
	|	РеквизитыДокумента.Проведен
	|ИЗ
	|	Документ.МыДалиВДолг КАК РеквизитыДокумента
	|ГДЕ
	|	РеквизитыДокумента.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДопРасходы.Ссылка КАК Регистратор,
	|	ДопРасходы.Ссылка.Дата КАК Период,
	|	ДопРасходы.КошелекРасхода,
	|	ДопРасходы.СтатьяРасхода,
	|	ДопРасходы.СуммаРасхода,
	|	ДопРасходы.ФинансоваяЦель,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.Валюта, &ВалютаУчета) КАК ВалютаКошелька,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ИспользоватьДляНакоплений, ЛОЖЬ) КАК ИспользоватьДляНакоплений,
	|	ЕСТЬNULL(ДопРасходы.Ссылка.Комментарий, """") КАК Комментарий,
	|	ДопРасходы.АналитикаСтатьи,
	|	ДопРасходы.Ссылка.ЭтоШаблон,
	|	ЕСТЬNULL(ДопРасходы.КошелекРасхода.ТипСчета, ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Наличность)) КАК ТипСчета
	|ИЗ
	|	Документ.МыДалиВДолг.ДополнительныеРасходы КАК ДопРасходы
	|ГДЕ
	|	ДопРасходы.Ссылка В(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор,
	|	ДопРасходы.НомерСтроки";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	СтруктураТаблиц = Новый Структура("Основное, ДополнительныеРасходы, ОбъектыОпераций");
	СтруктураТаблиц.Основное              = ПакетРезультатов[0].Выгрузить();
	СтруктураТаблиц.ДополнительныеРасходы = ПакетРезультатов[1].Выгрузить();
	
		// Таблицы для записи объектов операции и фактических оборотов бюджета
	ТаблицаНабора = ОбслуживаниеДокументов.НоваяТаблицаРегистраОбъектыОпераций();
	Если СтруктураТаблиц.Основное.Количество() > 0 И 
			(СтруктураТаблиц.Основное[0].ЭтоШаблон ИЛИ СтруктураТаблиц.Основное[0].Проведен ИЛИ ДополнительныеСвойства.ЭтоПлановаяОперация) Тогда
		ОборотыБюджета = ОбслуживаниеДокументов.НоваяТаблицаРегистровОборотовБюджета();
	Иначе
		ОборотыБюджета = Неопределено;
	КонецЕсли;
	
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.Основное Цикл
		
		СтатьяДолг = ?(ЗначениеЗаполнено(СтрокаДокумента.Долг), СтрокаДокумента.Долг, Справочники.ГрафыБюджета.ВыдачаВзаймы);
		
		Если СтрокаДокумента.ИспользоватьДляНакоплений Тогда
			ФинЦель = ?(Не ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦель), Справочники.ФинансовыеЦели.ОбщиеНакопления, СтрокаДокумента.ФинансоваяЦель);
		Иначе
			ФинЦель = Справочники.ФинансовыеЦели.ПустаяСсылка();
		КонецЕсли;
			
		ВалютаВыдано   = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаВыдано), СтрокаДокумента.ВалютаВыдано, ДополнительныеСвойства.ВалютаУчета);
		ВалютаЗайма    = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаЗайма), СтрокаДокумента.ВалютаЗайма, ДополнительныеСвойства.ВалютаУчета);
		
		// Расход из кошелька
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенег;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.Кошелек;
		ЗаписьНабора1.ФинансоваяЦель    = ФинЦель;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Списание;
		ЗаписьНабора1.СуммаСписания     = СтрокаДокумента.СуммаВыдано;
		ЗаписьНабора1.Валюта            = ВалютаВыдано;
		
		// Увеличение долга
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = ПланыСчетов.РазделыУчета.ОсновныеСуммыДолгов;
		ЗаписьНабора2.ПредметУчета      = СтатьяДолг;
		ЗаписьНабора2.ФинансоваяЦель    = ФинЦель;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Списание;
		ЗаписьНабора2.СуммаСписания     = СтрокаДокумента.СуммаЗайма;
		ЗаписьНабора2.Валюта            = ВалютаЗайма;
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено Тогда
			
			ЗаписьНабора = ОборотыБюджета.Добавить();
			ЗаписьНабора.Период           = СтрокаДокумента.Период;
			ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
			ЗаписьНабора.РазделБюджета    = ФинЦель;
			ЗаписьНабора.СтатьяБюджета    = СтатьяДолг;
			ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
			ЗаписьНабора.Валюта           = ВалютаВыдано;
			ЗаписьНабора.Кошелек          = СтрокаДокумента.Кошелек;
			ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаВыдано;
			ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			
			// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
			Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.Кошелек;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаВыдано;
				ЗаписьНабора.Кошелек          = Неопределено;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаВыдано;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинЦель) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
			
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		Если ВалютаЗайма <> ВалютаВыдано Тогда
			
			// Добавляем строки с обменом валют
			ЗаписьНабора3 = ТаблицаНабора.Добавить();
			ЗаписьНабора3.Дата              = СтрокаДокумента.Период;
			ЗаписьНабора3.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
			ЗаписьНабора3.Операция          = СсылкаНаДокумент;
			ЗаписьНабора3.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
			ЗаписьНабора3.ПредметУчета      = Справочники.СтатьиРасходов.ОбменВалюты;
			ЗаписьНабора3.ФинансоваяЦель    = СтрокаДокумента.ФинансоваяЦель;
			ЗаписьНабора3.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Перемещение;
			ЗаписьНабора3.СуммаСписания     = СтрокаДокумента.СуммаВыдано;
			ЗаписьНабора3.Валюта            = ВалютаВыдано;
			
			ЗаписьНабора4 = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора4, ЗаписьНабора3);
			ЗаписьНабора4.ПредметУчета      = Справочники.СтатьиДоходов.ОбменВалюты;
			ЗаписьНабора4.СуммаСписания     = 0;
			ЗаписьНабора4.СуммаПоступления  = СтрокаДокумента.СуммаЗайма;
			ЗаписьНабора4.Валюта            = ВалютаЗайма;
			
			Если ЗначениеЗаполнено(ФинЦель) Тогда
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора3);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
				
				ЗаписьНабора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора4);
				ЗаписьНабора.ФинансоваяЦель = Неопределено;
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЦикла; 
	
	Для Каждого СтрокаДокумента Из СтруктураТаблиц.ДополнительныеРасходы Цикл
		
		СчетДенег = ?(СтрокаДокумента.ИспользоватьДляНакоплений = Истина, ПланыСчетов.РазделыУчета.Накопления, ПланыСчетов.РазделыУчета.СвободныеДеньги);
		ФинЦель = ?(ЗначениеЗаполнено(СтрокаДокумента.ФинансоваяЦель) И СтрокаДокумента.ФинансоваяЦель <> Справочники.ФинансовыеЦели.ОбщиеНакопления, 
			СтрокаДокумента.ФинансоваяЦель, Неопределено);
		ВалютаКошелька = ?(ЗначениеЗаполнено(СтрокаДокумента.ВалютаКошелька), СтрокаДокумента.ВалютаКошелька, ДополнительныеСвойства.ВалютаУчета);
			
		ЗаписьНабора1 = ТаблицаНабора.Добавить();
		ЗаписьНабора1.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора1.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора1.Операция          = СсылкаНаДокумент;
		ЗаписьНабора1.РазделУчета       = СчетДенег;
		ЗаписьНабора1.ПредметУчета      = СтрокаДокумента.КошелекРасхода;
		ЗаписьНабора1.ФинансоваяЦель    = Финцель;
		ЗаписьНабора1.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Списание;
		ЗаписьНабора1.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора1.Валюта            = ВалютаКошелька;
		
		ЗаписьНабора2 = ТаблицаНабора.Добавить();
		ЗаписьНабора2.Дата              = СтрокаДокумента.Период;
		ЗаписьНабора2.ЭтоШаблон         = СтрокаДокумента.ЭтоШаблон;
		ЗаписьНабора2.Операция          = СсылкаНаДокумент;
		ЗаписьНабора2.РазделУчета       = ПланыСчетов.РазделыУчета.Капитал;
		ЗаписьНабора2.ПредметУчета      = СтрокаДокумента.СтатьяРасхода;
		ЗаписьНабора2.ФинансоваяЦель    = Финцель;
		ЗаписьНабора2.ТипПоказателя     = Перечисления.ТипыБюджетныхПоказателей.Списание;
		ЗаписьНабора2.СуммаСписания     = СтрокаДокумента.СуммаРасхода;
		ЗаписьНабора2.Валюта            = ВалютаКошелька;
		
		Если ЗначениеЗаполнено(ФинЦель) Тогда
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора1);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
			
			ЗаписьНабора = ТаблицаНабора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗаписьНабора2);
			ЗаписьНабора.ФинансоваяЦель = Неопределено;
		КонецЕсли; 
		
		// Фактические обороты бюджета
		Если ОборотыБюджета <> Неопределено Тогда
			
			ЗаписьНабора = ОборотыБюджета.Добавить();
			ЗаписьНабора.Период           = СтрокаДокумента.Период;
			ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
			ЗаписьНабора.РазделБюджета    = ФинЦель;
			ЗаписьНабора.СтатьяБюджета    = ?(ЗначениеЗаполнено(СтрокаДокумента.СтатьяРасхода), СтрокаДокумента.СтатьяРасхода, Справочники.СтатьиРасходов.ПрочиеРасходы);
			ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Списание;
			ЗаписьНабора.Валюта           = ВалютаКошелька;
			ЗаписьНабора.Кошелек          = СтрокаДокумента.КошелекРасхода;
			ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
			ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			
			// Если оплата произошла с кредитной карты, автоматически добавляем перевод с кредитки в доступные деньги
			Если СтрокаДокумента.ТипСчета = Перечисления.ТипыСчетов.БанковскаяКартаКредитная Тогда
				ЗаписьНабора = ОборотыБюджета.Добавить();
				ЗаписьНабора.Период           = СтрокаДокумента.Период;
				ЗаписьНабора.Регистратор      = СтрокаДокумента.Регистратор;
				ЗаписьНабора.РазделБюджета    = ФинЦель;
				ЗаписьНабора.СтатьяБюджета    = СтрокаДокумента.КошелекРасхода;
				ЗаписьНабора.ТипПоказателя    = Перечисления.ТипыБюджетныхПоказателей.Поступление;
				ЗаписьНабора.Валюта           = ВалютаКошелька;
				ЗаписьНабора.Кошелек          = Неопределено;
				ЗаписьНабора.Сумма            = СтрокаДокумента.СуммаРасхода;
				ЗаписьНабора.Комментарий      = СтрокаДокумента.Комментарий;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; 

	ТаблицаНабора.Свернуть("Дата, ЭтоШаблон, Операция, РазделУчета, ПредметУчета, ФинансоваяЦель, ТипПоказателя, Валюта", "СуммаПоступления, СуммаСписания");
	СтруктураТаблиц.Вставить("ОбъектыОперации", ТаблицаНабора);
	
	Если ОборотыБюджета <> Неопределено Тогда
		ОборотыБюджета.Свернуть("Период,Регистратор,РазделБюджета,СтатьяБюджета,ТипПоказателя,Валюта,Кошелек,Комментарий", "Сумма");
		СтруктураТаблиц.Вставить("ОборотыБюджета", ОборотыБюджета);
	КонецЕсли;
	
	Возврат СтруктураТаблиц;

КонецФункции

#КонецОбласти

#КонецЕсли