////////////////////////////////////////////////////////////////////////////////
//Отчет.РегламентированныйОтчет3НДФЛ.Форма.ДоходПредпринимателя2018кв1
//  Заполнение информации о доходах предпринимателя
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Конвертация параметров формы в значения ДропРеквизитов формы
	Обработки.ПомощникЗаполнения3НДФЛ.ЗаполнитьДопРеквизитыФормыДокументаПомощника(ЭтотОбъект);
	
	// Чтение структуры документа
	СтрукураДокументаНаСервере = Обработки.ПомощникЗаполнения3НДФЛ.СтруктураДокументаСТаблицамиИзХранилищ(ДопРеквизитыФормы.СтруктураДокумента);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтрукураДокументаНаСервере, , "СтрокиОтчетаПредпринимателя");
	СтрокиОтчетаПредпринимателя.Загрузить(СтрукураДокументаНаСервере.СтрокиОтчетаПредпринимателя);
	ИспользоватьДляСтандартныхВычетов = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДопРеквизитыФормы.СписокОшибок <> Неопределено Тогда
		// Если форма открывается из списка собщений помощника заполнения, в параметрах будет передан массив "СписокОшибок"
		ПоказатьСписокОшибок(ДопРеквизитыФормы.СписокОшибок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		#Область ПоказатьВопрос
		ТекстВопроса = НСтр("ru='Записать изменения перед закрытием этой формы?'");
		
		ДополнительныеПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Записать'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет,    НСтр("ru='Закрыть без записи'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отменить закрытие'"));
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, Заголовок);
		#КонецОбласти
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидДеятельностиИндексПриИзменении(Элемент)
	
	Если ВидДеятельностиИндекс <> 1 И  ВидДеятельностиИндекс <> 5 Тогда
		РасходыПодтвержденыДокументами = Истина;
	КонецЕсли;
	Если ВидДеятельностиИндекс <> 1 Тогда
		ВидДеятельностиКод = "";
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаПриИзменении(Элемент)
	ПересчитатьИтоги(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляСтандартныхВычетовПриИзменении(Элемент)
	
	ИзменитьРаспределениеПоМесяцам();
	УправлениеФормой(ЭтотОбъект);
	ОбновитьНадписиФормы(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура РасходыПодтвержденыДокументамиПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура МатериальныеРасходыПриИзменении(Элемент)
	
	ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АмортизацияОСПриИзменении(Элемент)
	
	ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочиеРасходыПриИзменении(Элемент)
	
	ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаОплатуТрудаВсегоПриИзменении(Элемент)
	
	Если ПоТрудовымДоговорам > НаОплатуТрудаВсего Тогда
		ПоТрудовымДоговорам = НаОплатуТрудаВсего;
	КонецЕсли; 
	ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоТрудовымДоговорамПриИзменении(Элемент)
	
	Если ПоТрудовымДоговорам > НаОплатуТрудаВсего Тогда
		НаОплатуТрудаВсего = ПоТрудовымДоговорам;
	КонецЕсли; 
	ПересчитатьИтоги(ЭтотОбъект);
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_СтрокиОтчетаПредпринимателя

&НаКлиенте
Процедура СтрокиОтчетаПредпринимателяПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СтрокиОтчетаПредпринимателяПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Элемент.ТекущиеДанные.СуммаДохода = 0;
		СуммаДохода = СтрокиОтчетаПредпринимателя.Итог("СуммаДохода");
		ОбновитьИтогиТаблицы(ЭтотОбъект);
		ОбновитьНадписиФормы(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокиОтчетаПредпринимателяПриИзменении(Элемент)
	
	СуммаДохода = СтрокиОтчетаПредпринимателя.Итог("СуммаДохода");
	ОбновитьИтогиТаблицы(ЭтотОбъект);
	ОбновитьНадписиФормы(ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьДокументКлиент();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Форма.Элементы.ГруппаДоходыПоМесяцам.Видимость          = Форма.ИспользоватьДляСтандартныхВычетов;
	Форма.Элементы.СтраницаОбщейСуммыПоДокументам.Видимость = Форма.РасходыПодтвержденыДокументами;
	Если Форма.РасходыПодтвержденыДокументами Тогда
		Форма.Элементы.ГруппаСтраницыЗаполнения.ТекущаяСтраница = Форма.Элементы.СтраницаОбщейСуммыПоДокументам;
	Иначе
		Форма.Элементы.ГруппаСтраницыЗаполнения.ТекущаяСтраница = Форма.Элементы.СтраницаОбщейСуммыПоНорме;
	КонецЕсли;

	Форма.Элементы.СуммаДохода.Доступность  = Не Форма.ИспользоватьДляСтандартныхВычетов;
	Форма.Элементы.СуммаДохода1.Доступность = Не Форма.ИспользоватьДляСтандартныхВычетов;
	Форма.Элементы.РасходыПодтвержденыДокументами.Доступность = (Форма.ВидДеятельностиИндекс = 1 Или Форма.ВидДеятельностиИндекс = 5);
	Форма.Элементы.ВидДеятельностиКод.Доступность = (Форма.ВидДеятельностиИндекс = 1);
	
	ОбновитьИтогиТаблицы(Форма);
	ОбновитьНадписиФормы(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСписокОшибок(СписокОшибок)

	ОчиститьСообщения();
	Для каждого Ошибка Из СписокОшибок Цикл
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст  = Ошибка.ОписаниеОшибки;
		Сообщение.Поле   = Ошибка.ИмяРеквизита;
		Сообщение.Сообщить(); 
		
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьИтоги(Форма)

	Если Форма.РасходыПодтвержденыДокументами Тогда
		Форма.СуммаВычета = Форма.МатериальныеРасходы + Форма.АмортизацияОС 
				+ Форма.ПрочиеРасходы + Форма.НаОплатуТрудаВсего;
	Иначе
		Форма.СуммаВычета = СуммаВычетаПоНорме(Форма.СуммаДохода); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СуммаВычетаПоНорме(СуммаДохода)

	Возврат Окр(СуммаДохода * 0.2, 2); 

КонецФункции

&НаКлиенте
Процедура ИзменитьРаспределениеПоМесяцам()

	Если ИспользоватьДляСтандартныхВычетов И СуммаДохода > 0 
		И СуммаДохода <> СтрокиОтчетаПредпринимателя.Итог("СуммаДохода") Тогда
		
		ВМесяц = Цел(СуммаДохода / 12);
		Остаток = СуммаДохода - ВМесяц * 12;
		Для Счетчик = 1 По 12 Цикл
			СтрокиОтчетаПредпринимателя[Счетчик -1].СуммаДохода = ВМесяц;
		КонецЦикла;
		СтрокиОтчетаПредпринимателя[11].СуммаДохода = СтрокиОтчетаПредпринимателя[11].СуммаДохода + Остаток;
		
	Иначе
		СуммаДохода = СтрокиОтчетаПредпринимателя.Итог("СуммаДохода");
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтогиТаблицы(Форма)

	Форма.Элементы.СтрокиОтчетаПредпринимателяСуммаДохода.ТекстПодвала = Формат(Форма.СтрокиОтчетаПредпринимателя.Итог("СуммаДохода"), "ЧДЦ=2") 

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНадписиФормы(Форма)

	Вычет = СуммаВычетаПоНорме(Форма.СуммаДохода);
	Форма.Элементы.ПодсказкаВычетПоНорме.Заголовок = СтрШаблон(НСтр("ru='Вычет по норме равен %1 руб.'"), 
			Формат(Вычет, "ЧДЦ=2") ); 

КонецПроцедуры


///////////////////////////////////////////////////////
// Проверка и запись документа

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьДокументКлиент();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокументКлиент()

	РезультатПроверки = РезультатПроверкиДокумента();
	
	Если РезультатПроверки.БезОшибок Тогда
		Модифицированность = Ложь;
		ВернутьРезультатВПомощник(РезультатПроверки.РезультатДляВозврата);
	Иначе
		
		ПоказатьСписокОшибок(РезультатПроверки.СписокОшибок);
		
		#Область ПоказатьВопрос
		ТекстВопроса = НСтр("ru='В документе обнаружены ошибки. Записать документ в текущем состоянии?'");
		
		ДополнительныеПараметры = Новый Структура("РезультатПроверки", РезультатПроверки);
		Оповещение = Новый ОписаниеОповещения("ЗаписатьДокументКлиентЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Записать с ошибками'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет,    НСтр("ru='Исправить ошибки'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Закрыть не записывая'"));
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, Заголовок);
		#КонецОбласти
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РезультатДляЗаписи()

	СтруктураДокумента = ДопРеквизитыФормы.СтруктураДокумента;
	ЗаполнитьЗначенияСвойств(СтруктураДокумента, ЭтотОбъект, , "СтрокиОтчетаПредпринимателя");
	СтруктураДокумента.Вставить("СтрокиОтчетаПредпринимателя", Новый ХранилищеЗначения(СтрокиОтчетаПредпринимателя.Выгрузить()));
	
	Результат = Новый Структура;
	Результат.Вставить("ВидДокумента", ДопРеквизитыФормы.ВидДокумента);
	Результат.Вставить("СтавкаНалога", 13);
	Результат.Вставить("Представление", СтрШаблон("Доход предпринимателя ""%1""", ?(ВидДеятельностиИндекс = 1, 
							ВидДеятельностиКод, 
							Элементы.ВидДеятельностиИндекс.СписокВыбора.НайтиПоЗначению(ВидДеятельностиИндекс).Представление)));
	Результат.Вставить("СтруктураДокумента", СтруктураДокумента);
	Результат.Вставить("СуммаДохода", СуммаДохода);
	Результат.Вставить("СуммаВычета", СуммаВычета);

	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РезультатПроверкиДокумента()

	Результат = Новый Структура();
	Результат.Вставить("РезультатДляВозврата", РезультатДляЗаписи());
	
	Результат.Вставить("СписокОшибок", Новый Массив);
	Результат.Вставить("БезОшибок", Обработки.ПомощникЗаполнения3НДФЛ.ДокументНеСодержитОшибок(
			ДопРеквизитыФормы.ВидДокумента, ДопРеквизитыФормы.ГодОтчета, Результат.РезультатДляВозврата.СтруктураДокумента, Результат.СписокОшибок, Неопределено));
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЗаписатьДокументКлиентЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Модифицированность = Ложь;
		ВернутьРезультатВПомощник(ДополнительныеПараметры.РезультатПроверки.РезультатДляВозврата);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВернутьРезультатВПомощник(Результат = Неопределено)

	Если Результат = Неопределено Тогда
		Результат = РезультатДляЗаписи();
	КонецЕсли;
	
	Закрыть(Результат);

КонецПроцедуры



#КонецОбласти


