
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокКошельковДляПрогноза.ЗагрузитьЗначения(Параметры.СписокКошельков.ВыгрузитьЗначения());
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокКошельков", СписокКошельковДляПрогноза);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КошелькиИСчета.Ссылка КАК Кошелек,
	|	ВЫБОР
	|		КОГДА КошелькиИСчета.ЭтоГруппа
	|				И НЕ КошелькиИСчета.ПометкаУдаления
	|			ТОГДА 0
	|		КОГДА КошелькиИСчета.ЭтоГруппа
	|				И КошелькиИСчета.ПометкаУдаления
	|			ТОГДА 1
	|		КОГДА НЕ КошелькиИСчета.ЭтоГруппа
	|				И КошелькиИСчета.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА КошелькиИСчета.Ссылка В (&СписокКошельков)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.КошелькиИСчета КАК КошелькиИСчета
	|ГДЕ
	|	КошелькиИСчета.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	КошелькиИСчета.Наименование ИЕРАРХИЯ, КошелькиИСчета.ЭтоГруппа УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗаполнитьДеревоВыборкой(Выборка, ДеревоКошельков);

	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоВыборкой(Выборка, УзелДерева)

	Пока Выборка.Следующий() Цикл
	
		СтрокаДерева = УзелДерева.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДерева, Выборка);
		ЗаполнитьДеревоВыборкой(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), СтрокаДерева)
	
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокПомеченнымиКошельками(Узел)

	Для Каждого СтрокаДерева Из Узел.ПолучитьЭлементы() Цикл
		Если СтрокаДерева.Пометка Тогда
			СписокКошельковДляПрогноза.Добавить(СтрокаДерева.Кошелек);
		КонецЕсли; 
		ЗаполнитьСписокПомеченнымиКошельками(СтрокаДерева);
	КонецЦикла; 

КонецПроцедуры
 
&НаКлиенте
Процедура ИзменитьПометкуСтрокДерева(УзелДерева, Пометка)

	Для Каждого СтрокаДерева Из УзелДерева.ПолучитьЭлементы() Цикл
		СтрокаДерева.Пометка = ?(Пометка = Неопределено, НЕ СтрокаДерева.Пометка, Пометка);
		ИзменитьПометкуСтрокДерева(СтрокаДерева, Пометка);
	КонецЦикла; 

КонецПроцедуры
 

&НаКлиенте
Процедура Применить(Команда)
	
	СписокКошельковДляПрогноза.Очистить();
	ЗаполнитьСписокПомеченнымиКошельками(ДеревоКошельков);
	Закрыть(СписокКошельковДляПрогноза);
	
КонецПроцедуры


&НаКлиенте
Процедура ПометитьВсе(Команда)
	
	ИзменитьПометкуСтрокДерева(ДеревоКошельков, Истина);
	
КонецПроцедуры


&НаКлиенте
Процедура СнятьВсеПометки(Команда)
	
	ИзменитьПометкуСтрокДерева(ДеревоКошельков, Ложь);
	
КонецПроцедуры


&НаКлиенте
Процедура ИнвертироватьПометки(Команда)
	
	ИзменитьПометкуСтрокДерева(ДеревоКошельков, Неопределено);
	
КонецПроцедуры
 