////////////////////////////////////////////////////////////////////////////////
//Обработка.ПлановыеОперации.Форма.ОперацииЗаПериод
//  Список операций за выбранный период
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "ВыбраннаяДата,ПредставлениеПериода");
	Если ТипЗнч(Параметры.ТаблицаОперацийЗаПериод) = Тип("ТаблицаЗначений") Тогда
		ТаблицаОперацийЗаДень.Загрузить(Параметры.ТаблицаОперацийЗаПериод);
	Иначе
		ТаблицаОперацийЗаДень.Загрузить(Параметры.ТаблицаОперацийЗаПериод.Выгрузить());
	КонецЕсли; 
	
	ОбновитьПредставленияТаблицыОпераций();
	
	Заголовок = "Операции за период %1";
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Заголовок, ПредставлениеПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменился список операций на период" Тогда
		ОбработкаОповещенияСервер(Параметр);
		Если ТаблицаОперацийЗаДень.Количество() = 0 Тогда
			Закрыть();
		КонецЕсли; 
	ИначеЕсли ИмяСобытия = "Записана операция" Тогда
		ОбновитьСписокОперацийЗаПериод();
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы



#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_ТаблицаОпераций

&НаКлиенте
Процедура ТаблицаОперацийЗаДеньВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборКоманды(Новый Структура("Имя","ОперацииСоздать"));
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура ОбработатьВыборКоманды(Команда)

	ТекДанные = Элементы.ТаблицаОперацийЗаДень.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураОперации = Новый Структура("ИДСтрокиТаблицыОпераций,ШаблонОперации,ПлановаяДата,АктуальнаяДата,ПлановаяОперация,Пропустить,Выполнена,
			|СуммаПоступления,ВалютаПоступления,СуммаСписания,ВалютаСписания,
			|СуммаДолга,СуммаПроцентов,СуммаКомиссии");
	ЗаполнитьЗначенияСвойств(СтруктураОперации, ТекДанные);
	СтруктураОперации.Вставить("Выбран", Истина);
	ВыбранноеЗначение = Новый Структура("ТипВыбранногоЗначения,ИмяКоманды,СтруктураОперации,ВыбраннаяДата", 
		"КомандаСпискаНаДень", Команда.Имя, СтруктураОперации, ВыбраннаяДата);
	ОповеститьОВыборе(ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура ШаблоныИзменитьШаблон(Команда)
	
	ТекДанные = Элементы.ТаблицаОперацийЗаДень.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураОперации = Новый Структура("ИДСтрокиТаблицыОпераций,ШаблонОперации,ПлановаяДата,АктуальнаяДата,ПлановаяОперация,Пропустить,Выполнена,
			|СуммаПоступления,ВалютаПоступления,СуммаСписания,ВалютаСписания,
			|СуммаДолга,СуммаПроцентов,СуммаКомиссии");
	ЗаполнитьЗначенияСвойств(СтруктураОперации, ТекДанные);
	СтруктураОперации.Вставить("Выбран", Истина);
	Если ЗначениеЗаполнено(СтруктураОперации.ШаблонОперации) Тогда
		ПоказатьЗначение(, СтруктураОперации.ШаблонОперации);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПредставленияТаблицыОпераций()

	Для Каждого СтрокаОперации Из ТаблицаОперацийЗаДень Цикл
		Если СтрокаОперации.СуммаПоступления <> 0 И СтрокаОперации.СуммаСписания <> 0  Тогда
			Если (СтрокаОперации.СуммаПоступления = СтрокаОперации.СуммаСписания ИЛИ СтрокаОперации.СуммаПоступления = -СтрокаОперации.СуммаСписания) И СтрокаОперации.ВалютаПоступления = СтрокаОперации.ВалютаСписания Тогда
				СуммаОперации  =  "±" + Формат(СтрокаОперации.СуммаПоступленияВВалюте, "ЧДЦ=2") + " " + СтрокаОперации.ВалютаПоступления; 
			Иначе
				СуммаОперации  =  "+" + Формат(СтрокаОперации.СуммаПоступленияВВалюте, "ЧДЦ=2") + " " + СтрокаОперации.ВалютаПоступления 
							+ "-" + Формат(СтрокаОперации.СуммаСписанияВВалюте, "ЧДЦ=2") + " " + СтрокаОперации.ВалютаСписания; 
			КонецЕсли; 
		ИначеЕсли СтрокаОперации.СуммаПоступленияВВалюте <> 0 И СтрокаОперации.СуммаСписанияВВалюте = 0  Тогда
			СуммаОперации  =  "+" + Формат(СтрокаОперации.СуммаПоступленияВВалюте, "ЧДЦ=2") + " " + СтрокаОперации.ВалютаПоступления; 
		ИначеЕсли СтрокаОперации.СуммаСписанияВВалюте <> 0  Тогда
			СуммаОперации  =  "-" + Формат(СтрокаОперации.СуммаСписанияВВалюте, "ЧДЦ=2") + " " + СтрокаОперации.ВалютаСписания;
		Иначе //Если СтрокаОперации.СуммаПоступления = 0 И СтрокаОперации.СуммаСписания = 0  Тогда
			Если СтрокаОперации.ВалютаПоступления = СтрокаОперации.ВалютаСписания Тогда
				СуммаОперации  =  "" + Формат(СтрокаОперации.СуммаПоступленияВВалюте, "ЧДЦ=2; ЧН=") + " " + СтрокаОперации.ВалютаПоступления; 
			Иначе
				СуммаОперации  =  "+" + Формат(СтрокаОперации.СуммаПоступленияВВалюте, "ЧДЦ=2; ЧН=") + " " + СтрокаОперации.ВалютаПоступления 
							+ "-" + Формат(СтрокаОперации.СуммаСписанияВВалюте, "ЧДЦ=2; ЧН=") + " " + СтрокаОперации.ВалютаСписания; 
			КонецЕсли; 
		КонецЕсли;
		СтрокаОперации.СуммаПредставления = СуммаОперации;
		СтрокаОперации.Представление = Строка(СтрокаОперации.ШаблонОперации) + ": " + СтрокаОперации.ОписаниеПлановойОперации;
	КонецЦикла; 

	ТаблицаОперацийЗаДень.Сортировать("Выполнена,Пропустить,АктуальнаяДата УБЫВ");
	
КонецПроцедуры
 
&НаСервере
Процедура ОбработкаОповещенияСервер(Параметр)

	СтруктураТекущихДанных = Неопределено;
	ТекСтрока = Элементы.ТаблицаОперацийЗаДень.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ТекСтрока = ТаблицаОперацийЗаДень.НайтиПоИдентификатору(ТекСтрока);
		СтруктураТекущихДанных = Новый Структура("ШаблонОперации,ПлановаяДата", ТекСтрока.ШаблонОперации, ТекСтрока.ПлановаяДата);
	КонецЕсли; 
	
	Таблица = Неопределено;
	Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ТаблицаОперацийЗаПериод", Таблица) И Таблица <> Неопределено Тогда
		
		Если ТипЗнч(Таблица) = Тип("ТаблицаЗначений") Тогда
			ТаблицаОперацийЗаДень.Загрузить(Таблица);
		Иначе
			ТаблицаОперацийЗаДень.Загрузить(Таблица.Выгрузить());
		КонецЕсли; 
		ОбновитьПредставленияТаблицыОпераций();
		
		Если СтруктураТекущихДанных <> Неопределено Тогда
			СтрокиДанных = ТаблицаОперацийЗаДень.НайтиСтроки(СтруктураТекущихДанных);
			Если СтрокиДанных.Количество() > 0 Тогда
				Элементы.ТаблицаОперацийЗаДень.ТекущаяСтрока = СтрокиДанных[0].ПолучитьИдентификатор();
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОперацийЗаПериод()

	Если ВладелецФормы <> Неопределено Тогда
		
	КонецЕсли;

КонецПроцедуры
 

#КонецОбласти








