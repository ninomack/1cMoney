////////////////////////////////////////////////////////////////////////////////
//Обработка.ЗагрузкаВыпискиБанка.Форма.РедкаторАналитики
//  Уточнение занчений аналитики в загружаемой операции
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработаннаяСтрока = -1;
	ТаблицаАналитики.Загрузить(Параметры.ТаблицаАналитики.Выгрузить());
	Для каждого СтрокаАналитики Из ТаблицаАналитики Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаАналитики.Значение) И ЗначениеЗаполнено(СтрокаАналитики.КлючевоеСлово) Тогда
			СтрокаАналитики.Иконка = БиблиотекаКартинок.БыстроСоздатьЭлементСписка;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(ТаблицаАналитики);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы



#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицы

&НаКлиенте
Процедура ТаблицаАналитикиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ТаблицаАналитики.ТекущиеДанные;
	
	Если ПустаяСтрока(Текст) И Не ЗначениеЗаполнено(ДанныеСтроки.Значение) Тогда
		Текст = ДанныеСтроки.Представление;
		ПараметрыПолученияДанных.СтрокаПоиска = ДанныеСтроки.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАналитикиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ТаблицаАналитики.ТекущиеДанные;
	
	ТипВыбранного = ТипЗнч(ВыбранноеЗначение);
	Если ТипВыбранного = Тип("Строка") Тогда
		ОбслуживаниеСправочниковКлиент.РасширенноеПолучениеДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, ДанныеСтроки.Представление);
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ТаблицаАналитикиПередНачаломИзменения(Элемент, Отказ)
	
	Если ОбработаннаяСтрока = Элементы.ТаблицаАналитики.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	
	ОбработаннаяСтрока = Элементы.ТаблицаАналитики.ТекущаяСтрока;
	
	ДанныеСтроки = Элементы.ТаблицаАналитики.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Или не ЗначениеЗаполнено(ДанныеСтроки.ВидАналитики) Тогда
		Возврат;
	КонецЕсли;
	
	УточнитьПараметрыВыбора(ДанныеСтроки.ПолучитьИдентификатор());
	Элементы.ТаблицаАналитики.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАналитикиЗначениеПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.ТаблицаАналитики.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Или не ЗначениеЗаполнено(ДанныеСтроки.ВидАналитики) Тогда
		Возврат;
	КонецЕсли;
	ДанныеСтроки.Иконка = ?(ЗначениеЗаполнено(ДанныеСтроки.Значение) Или Не ЗначениеЗаполнено(ДанныеСтроки.КлючевоеСлово), Новый Картинка, БиблиотекаКартинок.БыстроСоздатьЭлементСписка);
	ДанныеСтроки.Представление = ?(ЗначениеЗаполнено(ДанныеСтроки.Значение), СТрока(ДанныеСтроки.Значение), ДанныеСтроки.КлючевоеСлово);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАналитикиЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	ДанныеСтроки = Элементы.ТаблицаАналитики.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Или не ЗначениеЗаполнено(ДанныеСтроки.ВидАналитики) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки.КлючевоеСлово = "";
	ДанныеСтроки.Представление = "";
	ДанныеСтроки.Значение = Неопределено;
	ДанныеСтроки.Иконка = Новый Картинка;
	
	Элементы.ТаблицаАналитики.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УточнитьПараметрыВыбора(ИДСтроки)

	ДанныеСтроки = ТаблицаАналитики.НайтиПоИдентификатору(ИДСтроки);
	Если ДанныеСтроки = Неопределено Или не ЗначениеЗаполнено(ДанныеСтроки.ВидАналитики) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоля = Новый Массив(Элементы.ТаблицаАналитикиЗначение.ПараметрыВыбора);
	Если ПараметрыПоля.Количество() > 2 Тогда
		ПараметрыПоля.Удалить(2);
	КонецЕсли;
	
	Источники = Новый Массив;
	ЕстьВладелец = Ложь;
	Для каждого Тип Из ДанныеСтроки.ВидАналитики.ТипЗначения.Типы() Цикл
		
		МетаданныеТипа = Метаданные.НайтиПоТипу(Тип);
		Если МетаданныеТипа= Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЭтоСправочник(МетаданныеТипа) Тогда
			Источники.Добавить(МетаданныеТипа.Имя);
			Если МетаданныеТипа.Владельцы.Количество() > 0 Тогда
				ЕстьВладелец = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	МассивСвязей = Новый Массив;
	Если ЕстьВладелец Тогда
		МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", 
				"Элементы.ТаблицаАналитики.ТекущиеДанные.ВидАналитики", РежимИзмененияСвязанногоЗначения.Очищать));
	КонецЕсли;
	Элементы.ТаблицаАналитикиЗначение.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязей);
	
	Если Источники.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоля.Добавить(Новый ПараметрВыбора("РасширенныйВыбор.ИсточникиДанных", Новый ФиксированныйМассив(Источники)));
	Элементы.ТаблицаАналитикиЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыПоля);
	
КонецПроцедуры


#КонецОбласти
