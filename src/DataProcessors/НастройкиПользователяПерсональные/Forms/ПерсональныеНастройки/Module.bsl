#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СведенияОПользователе(Команда)
	
	ПоказатьЗначение(, АвторизованныйПользователь);
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Оповестить("Изменилась настройка", Новый Структура("ПриЗапускеОткрыватьРаздел,ОсновнойКошелек", ПриЗапускеОткрыватьРаздел, ОсновнойКошелек));
	Закрыть();
	
КонецПроцедуры
 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЭтоВебКлиент = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	ВыполнитьПроверкуПравДоступа("СохранениеДанныхПользователя", Метаданные);
	
	ПредлагатьПерейтиНаСайтПриЗапуске = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"ОбщиеНастройкиПользователя", 
		"ПредлагатьПерейтиНаСайтПриЗапуске",
		Ложь);

	// СтандартныеПодсистемы.БазоваяФункциональность
	Если ЭтоВебКлиент Тогда
		Элементы.ЗапрашиватьПодтверждениеПриЗавершенииПрограммы.Видимость = Ложь;
	КонецЕсли;
	
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
	// СтандартныеПодсистемы.Пользователи
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	Элементы.СведенияОПользователе.Заголовок = АвторизованныйПользователь;
	// Конец СтандартныеПодсистемы.Пользователи
	
	ПрочитатьНастройкиПользователя();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиПользователя()

	// СтандартныеПодсистемы.БазоваяФункциональность
	ЗапрашиватьПодтверждениеПриЗавершенииПрограммы = СтандартныеПодсистемыСервер.ЗапрашиватьПодтверждениеПриЗавершенииПрограммы();
	
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
	// ДЕНЬГИ
	
	// Общие настройки пользователя
	ПриЗапускеОткрыватьРаздел     = ПользовательскиеНастройкиДеньгиСервер.ПриЗапускеОткрыватьРаздел();
	СпособОткрытияФормОбъектов    = ПользовательскиеНастройкиДеньгиСервер.СпособОткрытияФорм();
	ТипДатыНовойОперации          = ПользовательскиеНастройкиДеньгиСервер.ТипДатыНовойОперации();
	
	// Значения по умолчанию
	ОсновнойКошелек   = ПользовательскиеНастройкиДеньгиСервер.ОсновнойКошелек();
	ОсновнойКонтакт   = ПользовательскиеНастройкиДеньгиСервер.ОсновнойКонтакт();
	ОсновноеИмущество = ПользовательскиеНастройкиДеньгиСервер.ОсновноеИмущество();
	
	// Конец ДЕНЬГИ

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ЗапрашиватьПодтверждениеПриЗавершенииПрограммы", ЗапрашиватьПодтверждениеПриЗавершенииПрограммы);
	
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
	// ДЕНЬГИ
	
	// Общие настройки пользователя
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьПриЗапускеОткрыватьРаздел(ПриЗапускеОткрыватьРаздел);
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьСпособОткрытияФорм(СпособОткрытияФормОбъектов);
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьТипДатыНовойОперации(ТипДатыНовойОперации);
	
	// Значения по умолчанию
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьОсновнойКошелек(ОсновнойКошелек);
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьОсновнойКонтакт(ОсновнойКонтакт);
	ПользовательскиеНастройкиДеньгиСервер.ЗаписатьОсновноеИмущество(ОсновноеИмущество);
	
	// Конец ДЕНЬГИ
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменилась настройка" И Параметр.Свойство("ПриЗапускеОткрыватьРаздел") Тогда
		ЕстьМодифицированность = Модифицированность;
		ПриЗапускеОткрыватьРаздел =  Параметр.ПриЗапускеОткрыватьРаздел;
		Модифицированность = ЕстьМодифицированность;
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти


