
////////////////////////////////////////////////////////////////////////////////
// Модуль формы <ПолноеИмяФормы>
//  
//Параметры формы:  
//	ИмяПараметра - Тип - Описание 
//  
//  
////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьРазделУчетаПриИзменении(Элемент)
	
	Если НЕ ИспользоватьРазделУчета ИЛИ НЕ ЗначениеЗаполнено(НомерРаздела) Тогда
		ИспользоватьОбъектУчета = Ложь;
	КонецЕсли;
	 
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделУчетаПриИзменении(Элемент)
	
	ИспользоватьРазделУчета = Истина;
	ПроверитьТипОбъекта(ЭтаФорма, НомерРаздела, ОбъектУчета);
	Если НЕ ИспользоватьРазделУчета ИЛИ НЕ ЗначениеЗаполнено(НомерРаздела) Тогда
		ИспользоватьОбъектУчета = Ложь;
	КонецЕсли;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектУчетаПриИзменении(Элемент)
	
	ИспользоватьОбъектУчета = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектУчетаОчистка(Элемент, СтандартнаяОбработка)
	
	ПроверитьТипОбъекта(ЭтаФорма, НомерРаздела, ОбъектУчета);
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Применить(Команда)
	
	ЗначениеВыбора = Новый Структура("ТипВыбранногоЗначения,Отбор_ИспользоватьРазделУчета,Отбор_НомерРаздела,Отбор_ИспользоватьОбъектУчета,Отбор_ОбъектУчета,Отбор_ИспользоватьВидОперации,Отбор_ВидОперации", 
		"ПараметрыОтбораСпискаОпераций", ИспользоватьРазделУчета, НомерРаздела, ИспользоватьОбъектУчета, ОбъектУчета, ИспользоватьВидОперации, ВидОперации);
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	ЗначениеВыбора = Новый Структура("ТипВыбранногоЗначения,Отбор_ИспользоватьРазделУчета,Отбор_НомерРаздела,Отбор_ИспользоватьОбъектУчета,Отбор_ОбъектУчета,Отбор_ИспользоватьВидОперации,Отбор_ВидОперации", 
		"ПараметрыОтбораСпискаОпераций", ИспользоватьРазделУчета, НомерРаздела, ИспользоватьОбъектУчета, ОбъектУчета, ИспользоватьВидОперации, ВидОперации);
	ОповеститьОВыборе(ЗначениеВыбора);
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтбор(Команда)
	
	ИспользоватьРазделУчета = Ложь;
	ИспользоватьОбъектУчета = Ложь;
	ИспользоватьВидОперации = Ложь;
	ЗначениеВыбора = Новый Структура("ТипВыбранногоЗначения,Отбор_ИспользоватьРазделУчета,Отбор_НомерРаздела,Отбор_ИспользоватьОбъектУчета,Отбор_ОбъектУчета,Отбор_ИспользоватьВидОперации,Отбор_ВидОперации", 
		"ПараметрыОтбораСпискаОпераций", ИспользоватьРазделУчета, НомерРаздела, ИспользоватьОбъектУчета, ОбъектУчета, ИспользоватьВидОперации, ВидОперации);
	ОповеститьОВыборе(ЗначениеВыбора);
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры




////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	// Подготовка реквизитов формы
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, 
		"ИспользоватьРазделУчета,НомерРаздела,ИспользоватьОбъектУчета,ОбъектУчета,НомерРаздела,ИспользоватьВидОперации,ВидОперации");
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаДебетовая);
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаУниверсальная);
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.БанковскийСчет);
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.Наличность);
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.Прочее);
	ТипыСчетовДеньги.Добавить(Перечисления.ТипыСчетов.ЭлектронныйКошелек);
		
	ТипыСчетовКредитныеКарты.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаКредитная);
	ТипыСчетовКредитныеКарты.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаУниверсальная);
	
	ПроверитьТипОбъекта(ЭтаФорма, НомерРаздела, ОбъектУчета);
	
	// Настройка внешнего вида
	УправлениеФормой(ЭтаФорма);
	ЗакрыватьПриВыборе = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьТипОбъекта(Знач Форма, Знач НомерРаздела, ОбъектУчета)

	Элементы = Форма.Элементы;
	
	Если НомерРаздела = 0 ИЛИ НомерРаздела = 1 ИЛИ НомерРаздела = 2 Тогда
		ТипОбъекта = Новый ОписаниеТипов("СправочникСсылка.КошелькиИСчета");
	ИначеЕсли НомерРаздела = 3 Тогда
		ТипОбъекта = Новый ОписаниеТипов("СправочникСсылка.Имущество");
	ИначеЕсли НомерРаздела = 4 ИЛИ НомерРаздела = 5 Тогда
		ТипОбъекта = Новый ОписаниеТипов("СправочникСсылка.Долги");
	ИначеЕсли НомерРаздела = 6 Тогда
		ТипОбъекта = Новый ОписаниеТипов("СправочникСсылка.СтатьиДоходов");
	ИначеЕсли НомерРаздела = 7 Тогда
		ТипОбъекта = Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходов");
	КонецЕсли; 

	Если НЕ ТипОбъекта.СодержитТип(ТипЗнч(ОбъектУчета)) Тогда
		Элементы.ОбъектУчета.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	КонецЕсли; 
	
	ОбъектУчета = ТипОбъекта.ПривестиЗначение(ОбъектУчета);
	
	Если НомерРаздела = 1 Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(Форма.ТипыСчетовДеньги.ВыгрузитьЗначения())));
		Элементы.ОбъектУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	ИначеЕсли НомерРаздела = 2 Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(Форма.ТипыСчетовКредитныеКарты.ВыгрузитьЗначения())));
		Элементы.ОбъектУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	ИначеЕсли НомерРаздела = 4 Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьДляУчетаВыданныхДолгов", Истина));
		Элементы.ОбъектУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	ИначеЕсли НомерРаздела = 5 Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьДляУчетаВзятыхДолгов", Истина));
		Элементы.ОбъектУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	
	Элементы.ИспользоватьОбъектУчета.Доступность = Форма.ИспользоватьРазделУчета И ЗначениеЗаполнено(Форма.НомерРаздела);
	Элементы.ОбъектУчета.Доступность             = Форма.ИспользоватьРазделУчета И ЗначениеЗаполнено(Форма.НомерРаздела);
	
	Если Форма.НомерРаздела = 1 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Кошелек или группа'") ;
	ИначеЕсли Форма.НомерРаздела = 2 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Кошелек или группа'") ;
	ИначеЕсли Форма.НомерРаздела = 3 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Имущество или группа'") ;
	ИначеЕсли Форма.НомерРаздела = 4 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Долг (выданный)'") ;
	ИначеЕсли Форма.НомерРаздела = 5 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Долг (взятый)'") ;
	ИначеЕсли Форма.НомерРаздела = 6 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Статья дохода'") ;
	ИначеЕсли Форма.НомерРаздела = 7 Тогда
		Элементы.ИспользоватьОбъектУчета.Заголовок = НСтр("ru = 'Статья расхода'") ;
	КонецЕсли;
	
КонецПроцедуры




