

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СтрокаСвойствКошелька());
	Если ОстатокНаНачало < 0 Тогда
		ДолгНаНачало = -ОстатокНаНачало;
		ТипОстатка = "Долг";
	Иначе
		ДеньгиНаНачало = ОстатокНаНачало;
		ТипОстатка = "Остаток";
	КонецЕсли;
	
	ТипыСКредитнымЛимитом.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаДебетовая);
	ТипыСКредитнымЛимитом.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаКредитная);
	ТипыСКредитнымЛимитом.Добавить(Перечисления.ТипыСчетов.БанковскаяКартаУниверсальная);
	ТипыСКредитнымЛимитом.Добавить(Перечисления.ТипыСчетов.ЭлектронныйКошелек);
	
	// Сброс расположения и размеров формы
	ОбщегоНазначенияДеньги.СброситьРазмерИПоложениеФормы(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры
 
&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	ПроверяемыеРеквизиты.Добавить("Наименование");
	ПроверяемыеРеквизиты.Добавить("ТипСчета");
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ВалютаУчета;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСчетаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ТипСчета) Тогда
		ТипСчета = ПредопределенноеЗначение("Перечисление.ТипыСчетов.БанковскаяКартаДебетовая");
	КонецЕсли;
	
	Если ТипыСКредитнымЛимитом.НайтиПоЗначению(ТипСчета) = Неопределено Тогда
		
		ТипОстатка = "Остаток";
		Если ОстатокНаНачало < 0 Тогда
			ДеньгиНаНачало  = ДолгНаНачало;
			ДолгНаНачало    = 0;
			ОстатокНаНачало = ДеньгиНаНачало;
		КонецЕсли;
		
		КредитныйЛимит = 0;
		
	ИначеЕсли ТипСчета = ПредопределенноеЗначение("Перечисление.ТипыСчетов.БанковскаяКартаКредитная") И ИспользоватьДляНакоплений Тогда 
		
		ИспользоватьДляНакоплений = Ложь;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект); 
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньгиНаНачалоПриИзменении(Элемент)
	
	Если ДолгНаНачало <> 0 И ДеньгиНаНачало = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТипОстатка      = "Остаток";
	ОстатокНаНачало = ДеньгиНаНачало;
	ДолгНаНачало    = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ДолгНаНачалоПриИзменении(Элемент)
	
	Если ДолгНаНачало = 0 И ДеньгиНаНачало <> 0  Тогда
		Возврат;
	КонецЕсли;
	
	ТипОстатка      = "Долг";
	ОстатокНаНачало = -ДолгНаНачало;
	ДеньгиНаНачало  = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОстатка1ПриИзменении(Элемент)
	
	Если ТипОстатка = "Долг" Тогда
		Если ДеньгиНаНачало <> 0 Тогда
			ДолгНаНачало = ДеньгиНаНачало;
		КонецЕсли;
		ОстатокНаНачало = -ДолгНаНачало;
		ДеньгиНаНачало  = 0;
	Иначе
		Если ДолгНаНачало <> 0 Тогда
			ДеньгиНаНачало = ДолгНаНачало;
		КонецЕсли;
		ОстатокНаНачало = ДеньгиНаНачало;
		ДолгНаНачало    = 0;
	КонецЕсли;
	
КонецПроцедуры




#КонецОбласти



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	ВыполнитьВыбор();
КонецПроцедуры
 


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаСвойствКошелька()
	Возврат "Валюта,ВалютаУчета,ИспользоватьДляНакоплений,Ключ,ТипСчета,КредитныйЛимит,Наименование,ОстатокНаНачало";
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	
	ВидимостьКредитногоЛимита = Форма.ТипыСКредитнымЛимитом.НайтиПоЗначению(Форма.ТипСчета) <> Неопределено;
	
	Элементы.КредитныйЛимит.Видимость = ВидимостьКредитногоЛимита;
	
	Элементы.ТипОстатка2.Доступность = ВидимостьКредитногоЛимита;
	Элементы.ДолгНаНачало.Видимость  = ВидимостьКредитногоЛимита;
	Элементы.ДекорацияДолгНаНачало.Заголовок = ?(ВидимостьКредитногоЛимита, "", НСтр("ru='не предусмотрен'"));

	Элементы.ИспользоватьДляНакоплений1.Доступность = Форма.ТипСчета <> ПредопределенноеЗначение("Перечисление.ТипыСчетов.БанковскаяКартаКредитная");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	ВыполнитьВыбор();
	Модифицированность = Ложь;
	Закрыть(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура(СтрокаСвойствКошелька());
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект, СтрокаСвойствКошелька(), "ВалютаУчета");
	Модифицированность = Ложь;
	Закрыть(Результат);

КонецПроцедуры


#КонецОбласти
