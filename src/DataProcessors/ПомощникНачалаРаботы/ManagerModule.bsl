
#Область ПрограммныйИнтерфейс

// Возвращает структуру, описывающую текущее состояние помощника начала работы
//	Структура используется для сохранения в константу "СостояниеПомощникаНачалаРаботы", чтобы
//	иметь возможность продолжить работу с помощником после перезапуска приложения
//
//Параметры:
//	Нет
//
//Возвращаемое значение:
//	Структура:
//		* ПомощникЗавершен - Булево - Признак того, что работа с помощником завершена
//		* Сценарий - Строка - наименование сценария, выбранного пользователем
//		* СпособПолученияФайлов - Строка - способ получения файлов из мобильного приложения
//		* ТекущийШагПомощника - Строка - наименование текущего шага помощника начала работы
//
Функция СтруктураСостоянияПомощника() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ПомощникЗавершен",        Ложь);
	Результат.Вставить("Сценарий",                "");
	Результат.Вставить("СпособПолученияФайлов",   "");
	Результат.Вставить("ТекущийШагПомощника",     "");

	Возврат Результат;
	
КонецФункции

// Возвращает текущее состояние помощника, сохраненное в константе, или заполненное значениями по умолчанию
//
//Параметры:
//	Нет
//
//Возвращаемое значение:
//	Структура - подробное описание см. в СтруктураСостоянияПомощника()
//
Функция ТекущееСостояниеПомощника() Экспорт

	НовоеСостояние = СтруктураСостоянияПомощника();
	ТекущееСостояние = Константы.СостояниеПомощникаНачалаРаботы.Получить().Получить();
	Если ТекущееСостояние = Неопределено Тогда
		// Заполняем значениями по умолчанию
		
		ТекущееСостояние = НовоеСостояние;
		ТекущееСостояние.ПомощникЗавершен         = Ложь;
		ТекущееСостояние.Сценарий                 = "НовыйПользователь";
		ТекущееСостояние.СпособПолученияФайлов    = "Файл";
		ТекущееСостояние.ТекущийШагПомощника      = "СтраницаПользователь";
		
	Иначе
		// Проверяем наличие актуальных ключей
		
		Для каждого КлючИЗначение Из НовоеСостояние Цикл
			Если Не ТекущееСостояние.Свойство(КлючИЗначение.Ключ) Тогда
				ТекущееСостояние.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТекущееСостояние;

КонецФункции
 

#КонецОбласти


#Область ОбработчикиСобытий



#КонецОбласти


#Область СлужебныеПроцедурыИФункции


#КонецОбласти